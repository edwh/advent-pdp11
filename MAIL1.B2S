1	ON ERROR GOTO 25000 &
\	MAP (M) TO$=2%,		! Destination PPN &
		FROM$=2%,	! Sender's PPN &
		SUBJ$=30%,	! Topic &
		DAT%,		! Date message was sent &
		TIM%,		! Time message was sent &
		FLAG%,		! Info about message &
		TEXT$=1024%	! Actual message text &

10	J$=SYS(CHR$(12%)) &
\	PKG.LOC$=FNTIDYACCT$(SWAP%(CVT$%(RIGHT(J$,5%)))) &
\	CRLF$=CHR$(13%)+CHR$(10%) &
\	X$='' &
\	X$=X$+CHR$(I%) FOR I%=0% TO 127% &
\	USER.PPN%=PEEK(PEEK(PEEK(520%)+8%)+24%) &
\	OPEN '_KB:' AS FILE #1% &
\	COM.LIST$='/EXIT    /HELP    /?       /READ    /SEND    '+ &
		  '/SUBJECT /LIST    /PURGE   /' &
\	OPEN PKG.LOC$+'MAIL  .DTA' AS FILE #3%, &
	ORGANIZATION INDEXED VARIABLE, &
	MAP M, &
	ACCESS MODIFY, &
	ALLOW MODIFY, &
	PRIMARY KEY TO$ DUPLICATES, &
	ALTERNATE KEY FROM$ DUPLICATES, &
	ALTERNATE KEY SUBJ$ DUPLICATES &
\	J$=SYS(CHR$(6%)+CHR$(-7%)) &

20	PRINT #1%, CRLF$;'MAIL V1.0 ';ERT$(0%);' at ';TIME$(0%);' on '; &
	DATE$(0%);CRLF$ &
\	OPEN PKG.LOC$+'MAIL  .NTC' FOR INPUT AS FILE #2% &
\	FIELD #2%, 512% AS J$ &
\	UNTIL 0% &
\	GET #2% &
\	PRINT #1%, J$; &
\	NEXT &

30	&

100	PRINT IF CCPOS%(1%) &
\	J$=SYS(CHR$(2%)) &
\	J$=SYS(CHR$(6%)+CHR$(-21%)+CHR$(0%)) &
\	PRINT #1%, CRLF$;'MAIL> '; &
\	INPUT LINE #1%, ARG$ &
\	ARG$=CVT$$(ARG$,4%) &
\	COM$=FNGETARG$ &
\	COM$=CVT$$(COM$,255%) &
\	IF LEN(COM$)=0% THEN PRINT #1%, CRLF$; &
	'Type HELP or ? for the help text.' &
\	GOTO 100 &

110	J%=INSTR(1%,COM.LIST$,'/'+COM$) &
\	IF J%=0% THEN &
	PRINT #1%, CRLF$;'Illegal command - ? or HELP for help.' &
\	GOTO  100 &

120	COM%=(J%-1%)/8%+1% &
\	ON COM% GOTO 32767,	! EXIT &
		     1000,	! HELP &
		     1000,	! ? &
		     2000,	! READ &
		     3000,	! SEND &
		     4000,	! SUBJECT &
		     5000,	! LIST &
		     6000	! PURGE &

1000	! HELP &
	&
	&
	OPEN PKG.LOC$+'MAIL  .HLP' FOR INPUT AS FILE #2%, MODE 4096% &
\	FIELD #2%, 512% AS J$ &
\	UNTIL 0% &
\	GET #2% &
\	PRINT #1%, J$; &
\	NEXT &

1010	PRINT #1%, CRLF$;'Help file (';PKG.LOC$+'MAIL  .HLP) not found.' &
\	GOTO 100 &

2000	! READ &
	&
	&
	TYP$=FNGETARG$ &
\	GOTO 2010 IF LEN(TYP$) &
\	PRINT #1%, CRLF$;'Read which messages <ALL> ? '; &
\	INPUT LINE #1%, TYP$ &
\	TYP$=CVT$$(TYP$,255%) &
\	TYP$='ALL' UNLESS LEN(TYP$) &

2010	TYP$=CVT$$(TYP$,255%) &
\	IF TYP$='OWN' THEN &
	J%=FNREADACCT%(USER.PPN%) &
\	GOTO 100 &

2020	IF TYP$='ALL' THEN &
	J%=FNREADACCT%(USER.PPN%)+FNREADACCT%(-1%) &
\	GOTO 100 &

2030	IF TYP$='SENT' THEN &
	J%=FNREADSENT% &
\	GOTO 100 &

2040	J$=SYS(CHR$(6%)+CHR$(-10%)+TYP$) &
\	J%=SWAP%(CVT$%(RIGHT(J$,5%))) &
\	IF J%<>0% THEN &
	J%=FNREADACCT%(J%) IF (SWAP%(USER.PPN%) AND 255%)=1% &
\	PRINT #1%, CRLF$; &
	"Only privileged users may read other people's mail." &
	UNLESS (SWAP%(USER.PPN%) AND 255%)=1% &
\	GOTO 100 &

2100	PRINT #1%, CRLF$;'?Illegal read option - use OWN,ALL,[nnn,nnn] or SENT.' &
\	GOTO 100 &

3000	! SEND &
	&
	&
	TYP$=FNGETARG$ &
\	GOTO 3010 IF LEN(TYP$) &
\	PRINT #1%, CRLF$;'To <ALL> ? '; &
\	INPUT LINE #1%, TYP$ &
\	TYP$='ALL' UNLESS LEN(CVT$$(TYP$,255%)) &

3010	TYP$=CVT$$(TYP$,255%) &
\	IF TYP$='ALL' THEN &
	TO$=CHR$(255%)+CHR$(255%) &
	! [*,*] &
\	GOTO 3100

3020	J$=SYS(CHR$(6%)+CHR$(-10%)+TYP$) &
\	J%=SWAP%(CVT$%(RIGHT(J$,5%))) &
\	IF J%<>0% THEN &
	TO$=MID(J$,5%,2%) &
\	GOTO 3100 &

3030	PRINT #1%, CRLF$;'?Illegal account.' &
\	GOTO 100 &

3100	FROM$=CVT%$(SWAP%(USER.PPN%)) &
\	FLAG%=0% &
\	GOTO 3200 UNLESS (SWAP%(USER.PPN%) AND 255%)=1% &

3110	PRINT #1%, CRLF$;'Protect <NO> ? '; &
\	INPUT LINE #1%, YN$ &
\	YN$='NO' UNLESS LEN(CVT$$(YN$,255%)) &

3120	YN$=CVT$$(YN$,255%) &
\	FLAG%=FLAG%+2% IF LEFT('YES',LEN(YN$))=YN$ &

3200	SUBJ$=FNGETARG$ &
\	GOTO 3210 IF LEN(CVT$$(SUBJ$,255%)) &
\	PRINT #1%, CRLF$;'Subject <GENERAL> ? '; &
\	INPUT #1%, SUBJ$ &
\	SUBJ$='GENERAL' UNLESS LEN(CVT$$(SUBJ$,255%)) &

3210	SUBJ$=CVT$$(SUBJ$,255%) &
\	FIL$=FNGETARG$ &
\	GOTO 3220 IF LEN(FIL$) &
\	PRINT #1%, CRLF$;'Input file <_KB:> ? '; &
\	INPUT LINE #1%, FIL$ &
\	FIL$='_KB:' UNLESS LEN(CVT$$(FIL$,255%)) &

3220	FIL$=CVT$$(FIL$,255%) &
\	J$=SYS(CHR$(6%)+CHR$(-21%)+CHR$(255%)) &
\	OPEN FIL$ FOR INPUT AS FILE #2% &
\	J$=SYS(CHR$(6%)+CHR$(-21%)+CHR$(0%)) &
\	GOTO 3240

3230	PRINT #1%, CRLF$;"Can't open input file ("; &
	RIGHT(ERT$(ERR),2%);")." &
\	GOTO 100

3240	J$='' &
\	UNTIL 0% &
\	INPUT LINE #2%, JJ$ &
\	J$=J$+JJ$ &
\	GOTO 3250 IF LEN(J$)>1024% &
\	NEXT &

3250	IF LEN(J$)<=1024% THEN &
	TEXT$=J$ &
	ELSE &
	PRINT #1%, CRLF$;'Sorry, maximum of 1024 characters.' &
\	CLOSE #2% &
\	GOTO 100 &

3260	DAT%=PEEK(512%) &
\	TIM%=24%*60%-TIME(0%)/60% &
\	CLOSE #2% &

3270	PUT #3%, COUNT 2%+2%+30%+2%+2%+2%+LEN(XLATE(TEXT$,X$)) &
\	PRINT #1%, CRLF$;'Mail sent.' &
\	GOTO 100 &

4000	! SUBJECT &
	&
	&
	SUBJ.SEARCH$=FNGETARG$ &
\	GOTO 4010 IF LEN(SUBJ.SEARCH$) &
\	PRINT #1%, CRLF$;'Subject to search for <GENERAL> ? '; &
\	INPUT #1%, SUBJ.SEARCH$ &
\	SUBJ.SEARCH$='GENERAL' UNLESS LEN(SUBJ.SEARCH$) &

4010	SUBJ.SEARCH$=CVT$$(SUBJ.SEARCH$,255%) &
\	GET #3%, KEY #2% EQ SUBJ.SEARCH$ &
\	GOTO 4030 &

4020	PRINT #1%, CRLF$;'No messages on ';SUBJ.SEARCH$ &
\	GOTO 100 &

4030	IF SWAP%(CVT$%(TO$))=USER.PPN% OR TO$=STRING$(2%,255%) OR &
	(SWAP%(USER.PPN%) AND 255%)=1% THEN &
	J%=FNDISPLAY% &
\	FLAG%=FLAG%+1% UNLESS (FLAG% AND 1%) &
\	UPDATE #3%, COUNT 2%+2%+30%+2%+2%+2%+LEN(XLATE(TEXT$,X$)) &

4035	GET #3% &
\	GOTO 4030 IF CVT$$(SUBJ$,255%)=SUBJ.SEARCH$ &

4040	PRINT #1%, CRLF$;'No more messages on ';SUBJ.SEARCH$ &
\	GOTO 100 &

5000	! LIST &
	&
	&
	TYP$=FNGETARG$ &
\	GOTO 5010 IF LEN(TYP$) &
\	PRINT #1%, CRLF$;'List type <SUBJECTS> ? '; &
\	INPUT #1%, TYP$ &
\	TYP$='SUBJECTS' UNLESS LEN(TYP$) &

5010	TYP$=CVT$$(TYP$,255%) &
\	GOTO 5100 UNLESS LEFT('SUBJECTS',LEN(TYP$))=TYP$ &
\	RESTORE #3%, KEY #2% &
\	PRINT #1%, CRLF$;'Subject';TAB(40%);'No.';CRLF$; &
			 '-------';TAB(40%);'---' &
\	THIS.SUBJ.COUNT%=1% &
\	GET #3% &
\	LAST.SUBJ$=SUBJ$ &

5020	GET #3% &
\	GOTO 5030 UNLESS CVT$$(SUBJ$,255%)=CVT$$(LAST.SUBJ$,255%) &
\	THIS.SUBJ.COUNT%=THIS.SUBJ.COUNT%+1% &
\	GOTO 5020 &

5030	PRINT #1%, LAST.SUBJ$;TAB(40%);NUM1$(THIS.SUBJ.COUNT%) &
\	LAST.SUBJ$=SUBJ$ &
\	THIS.SUBJ.COUNT%=1% &
\	GOTO 5020 &

5040	PRINT #1%, CRLF$;'List complete.' &
\	GOTO 100 &

5100	PRINT #1%, CRLF$;'?Illegal LIST type.' &
\	GOTO 100 &

6000	! PURGE &
	&
	&
	IF (SWAP%(USER.PPN%) AND 255%)<>1% THEN &
	PRINT #1%, CRLF$;'Only privileged users may PURGE.' &
\	GOTO 100 &

6010	RESTORE #3% &
\	PRINT #1% &

6020	GET #3% &
\	GOTO 6020 UNLESS FLAG% AND 1% &
\	J$=FNTIDYACCT$(SWAP%(CVT$%(TO$))) &
\	J$='ALL' IF J$='[255,255]' &
\	PRINT #1%, CRLF$;'Mail for ';J$; &
	' from ';FNTIDYACCT$(SWAP%(CVT$%(FROM$))); &
	'.  Subject : ';CVT$$(SUBJ$,132%);'.';CRLF$;'Sent at '; &
	TIME$(TIM%);' on ';DATE$(DAT%); &
\	IF FLAG%<>0% THEN &
	PRINT #1%, '  ('; &
\	J$='' &
\	J$=',Read' IF FLAG% AND 1% &
\	J$=J$+',Delete protected' IF FLAG% AND 2% &
\	PRINT #1%, RIGHT(J$,2%);')'; &
\	PRINT #1% &

6030	J%=FNDELETE% &
\	GOTO 6020 &

15000	! FUNCTIONS &
	&
	&
	DEF* FNTIDYACCT$(PPN%) &
\	J$=NUM1$(SWAP%(PPN%) AND 255%) &
\	J$=' '+J$ WHILE LEN(J$)<3% &
\	J$=J$+','+NUM1$(PPN% AND 255%) &
\	J$=J$+' ' WHILE LEN(J$)<7% &
\	FNTIDYACCT$='['+J$+']' &
\	FNEND &

15010	! FNREADACCT%(PPN%) &
	&
	&
	! Displays all messages for the supplied account. &
	&
	DEF* FNREADACCT%(PPN%) &
\	RED%=0% &

15015	GET #3%, KEY #0% EQ CVT%$(SWAP%(PPN%)) &
\	GOTO 15030 &

15020	GOTO 15040 IF RED% &
\	J$='No mail found for account '+FNTIDYACCT$(PPN%)+'.' IF PPN%<>-1% &
\	J$='No mail found for ALL.' IF PPN%=-1% &
\	PRINT #1%, CRLF$;J$ &
\	FNEXIT &

15030	J%=FNDISPLAY% &
\	RED%=-1% &
\	FLAG%=FLAG%+1% UNLESS (FLAG% AND 1%) &
\	GOTO 15015 IF FNDELETE% IF PPN%<>-1% &
\	UPDATE #3%, COUNT 2%+2%+30%+2%+2%+2%+LEN(XLATE(TEXT$,X$)) &

15035	GET #3% &
\	GOTO 15040 UNLESS CVT%$(SWAP%(PPN%))=TO$ &
\	GOTO 15030 &

15040	PRINT #1%, CRLF$;'No more mail for account ';FNTIDYACCT$(PPN%) &
	UNLESS PPN%=-1% &
\	PRINT #1%, CRLF$;'No more mail for ALL' IF PPN%=-1% &
\	FNEND &

15050	! FNDISPLAY% &
	&
	&
	! Displays the mail in the current record. &
	&
	DEF* FNDISPLAY% &
\	J$=FNTIDYACCT$(SWAP%(CVT$%(TO$))) &
\	J$='ALL' IF J$='[255,255]' &
\	PRINT #1%, CRLF$;'Mail for ';J$; &
	' from ';FNTIDYACCT$(SWAP%(CVT$%(FROM$))); &
	'.  Subject : ';CVT$$(SUBJ$,132%);'.';CRLF$;'Sent at '; &
	TIME$(TIM%);' on ';DATE$(DAT%); &
\	IF FLAG%<>0% THEN &
	PRINT #1%, '  ('; &
\	J$='' &
\	J$=',Read' IF FLAG% AND 1% &
\	J$=J$+',Delete protected' IF FLAG% AND 2% &
\	PRINT #1%, RIGHT(J$,2%);')'; &

15060	PRINT #1%, CRLF$ &
\	PRINT #1%, CVT$$(TEXT$,128%); &
\	PRINT #1%, CRLF$;CRLF$;'Press any key to continue, or CTRL/C to exit.';&
	STRING$(10%,8%); &
\	J$=SYS(CHR$(4%))+SYS(CHR$(3%)) &
\	GET #1% &

15070	J$=SYS(CHR$(2%)) &
\	PRINT #1%, CHR$(13%);STRING$(60%,32%);CHR$(13%); &
\	FNEND &

15080	DEF* FNDELETE% &
\	FNDELETE%=0% &
\	IF (FLAG% AND 2%) AND (SWAP%(USER.PPN%) AND 255%)<>1% THEN &
	PRINT #1%, CRLF$;"(Mail is protected, can't delete.)" &
\	FNEXIT &

15090	DELETE #3% &
\	RESTORE #3% &
\	PRINT #1%, CHR$(26%);'Mail deleted.                   ' &
\	FNDELETE%=-1% &
\	FNEXIT &

15100	PRINT #1%, CHR$(26%);STRING$(60%,32%);CHR$(13%); &
\	FNEND &

15110	! FNREADSENT &
	&
	&
	DEF* FNREADSENT% &

15120	GET #3%, KEY #1% EQ CVT%$(SWAP%(USER.PPN%)) &
\	GOTO 15140 &

15130	PRINT #1%, CRLF$;'No sent mail.' &
\	FNEXIT &

15140	J%=FNDISPLAY% &
\	GET #3% &
\	GOTO 15140 IF CVT%$(SWAP%(USER.PPN%))=FROM$ &

15150	PRINT #1%, CRLF$;'No more sent mail.' &
\	FNEXIT &

15160	FNEND &

15470	! FNGETARG &
	&
	&
	! This function is used in routines to allow the specification of &
	! all the command arguments on the command line. &
	&
	DEF* FNGETARG$ &
\	FNGETARG$='' &
\	GOTO 15490 UNLESS LEN(ARG$) &
\	JJ%=INSTR(1%,ARG$,' ') &
\	IF JJ%=0% THEN &
	FNGETARG$=ARG$ &
\	ARG$='' &
\	GOTO 15490 &

15480	FNGETARG$=LEFT(ARG$,JJ%-1%) &
\	ARG$=RIGHT(ARG$,JJ%+1%) &

15490	FNEND &

25000	! ERRORS &
	&
	&
	JJJ$=SYS(CHR$(6%)+CHR$(-7%)) &
\	RESUME 30 IF ERL=20 &
\	RESUME 100 IF ERL=1000 AND ERR=11 &
\	GOTO 25020 IF ERL=5020 AND ERR=11 &
\	RESUME 1010 IF ERL=1000 AND ERR=5 &
\	RESUME 32767 IF ERL=100 &
\	RESUME 3250 IF ERL=3240 AND ERR=11 &
\	RESUME 5040 IF ERL=5010 AND ERR=11 &
\	IF ERR=28 AND LINE=15060 THEN &
	J$=SYS(CHR$(2%)) &
\	PRINT #1%, CHR$(26%);CHR$(13%);STRING$(60%,32%);CHR$(13%); &

25005	RESUME 4035 IF ERL=4030 &
\	RESUME 15035 IF ERL=15030 &

25010	RESUME 100 IF ERR=28 OR ERR=11 &
\	RESUME 3030 IF ERL=3020 &
\	RESUME 3230 IF ERL=3220 &
\	RESUME 15020 IF ERL=15015 AND ERR=155 &
\	RESUME 15040 IF ERL=15030 AND ERR=155 &
\	RESUME 2100 IF ERL=2040 &
\	RESUME 15130 IF ERL=15120 &
\	RESUME 15150 IF ERL=15140 &
\	RESUME 4020 IF ERL=4010 &
\	RESUME 4040 IF ERL=4035 &
\	RESUME 100 IF ERL=6020 &
\	RESUME 5040 IF ERL=5010 &

25020	IF ERL=5020 THEN &
	PRINT #1%, LAST.SUBJ$;TAB(40%);NUM1$(THIS.SUBJ.COUNT%) &
\	RESUME 5040 &

29000	PRINT CRLF$;'MAIL : ';ERT$(ERR);' at line';ERL &
\	RESUME 	32767 &

32767	END &

