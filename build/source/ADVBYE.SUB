1	SUB ADVBYE &
\	COMMON ACC$(8%)=7%,AR$=80%,C%,C$=10%,CRLF$=2%, &
	FLAG%(8%),HP%(8%),LEVEL%(8%),KB%(8%),NAM$(8%)=30%,NO.OF.USERS%, &
	XP.(8%),STUN(8%), &
	NPCMESS$(8%)=40%,OB$(8%,9%)=30%,PASS$(8%)=10%, &
	ROOM%(8%),SENT.KB%,SENT.MESS$=80%,SENT.PROG%,SENT.PROJ%, &
	USER%,CI$=1%,WEAPON$(8%)=20%,WEAPON%(8%), &
	ATT.LEVEL%(10%),DEF.LEVEL%(10%),MON.HP%(10%),MON.DAM%(10%), &
	SPEC$(10%)=6%,MON.ROOM%(10%),MON.XP%(10%),RUN.ACC$=9%,MMM$=128%, &
	BLIND%(8%),USER1%,SPY%(10%),FATIGUE%(10%) &
\	FIELD #3%, 1% AS ROOM$, 16% AS EX$, 83% AS PEOPLE$, 100% AS OBJECT$, &
	312% AS DESC$ &
\	DIM #5%, MONSTER$(10000%)=20% &
\	DIM #7%, CHARACTER$(100%)=512% &
\	ON ERROR GOTO 25000 &

500	! REMOVE USER &
	&
	&
	GET #3%, RECORD ROOM%(USER%) &
\	GOTO 501 IF (HP%(USER%)<=0%) OR C%=-1% OR C%=-2% &
	OR ((TIME$(0%)>"09:15" AND TIME$(0%)<"09:25") OR &
	(TIME$(0%)>"11:30" AND TIME$(0%)<"11:40") OR &
	(TIME$(0%)>"02:00" AND TIME$(0%)<"02:10")) &
\	IF (LEVEL%(USER%)>16% OR (FLAG%(USER%) AND 2%^7%))=0% THEN &
	GOTO 530 IF INSTR(1%,'#*',LEFT(PEOPLE$,1%)) &
\	GOTO 530 IF &
	((ROOM%(I%)=ROOM%(USER%)) AND (I%<>USER%)) FOR I%=1% TO 8%  &

501	J$=FNCLEAR$ &
\	SPY%(I%)=0% IF SPY%(I%)=KB%(USER%) FOR I%=1% TO 10% &
\	CH%=0% &
\	CH%=CH%+1% UNTIL CVT$$(LEFT(CHARACTER$(CH%),10%),255%)=CVT$$(LEFT(NAM$(USER%),10%), &
	255%) &
\	IF HP%(USER%)>0% THEN &
	J$=NAM$(USER%) &
\	J$=J$+CHR$(0%) WHILE LEN(J$)<10% &
\	J$=J$+PASS$(USER%) &
\	J$=J$+CVT%$(ROOM%(USER%)) &
\	J$=J$+CVT%$(LEVEL%(USER%)) &
\	J$=J$+CVT%$(HP%(USER%)) &
\	JJ$=NUM1$(INT(XP.(USER%))) &
\	JJ$="0"+JJ$ WHILE LEN(JJ$)<5% &
\	J$=J$+JJ$ &
\	FLAG%(USER%)=FLAG%(USER%) XOR 64% IF FLAG%(USER%) AND 64% &
\	J$=J$+CVT%$(FLAG%(USER%)) &
\	J$=J$+CVT%$(WEAPON%(USER%)) &
\	J$=J$+WEAPON$(USER%) &
\	I%=INSTR(1%,ACC$(USER%),",") &
\	J$=J$+CHR$(VAL(LEFT(ACC$(USER%),I%-1%)))+CHR$(VAL(RIGHT( &
	ACC$(USER%),I%+1%))) &
\	J$=J$+CVT%$(PEEK(512%)) &
\	J$=J$+CVT%$(TIME(0%)/60%) &
\	J$=J$+OB$(USER%,I%) FOR I%=0% TO 9% &
\	J$=J$+CVT%$(FATIGUE%(USER%)) &
\	CHARACTER$(CH%)=J$ &
\	J$=CHARACTER$(0%) &
\	J$=CHARACTER$(100%) &

510	IF HP%(USER%)<=0% THEN &
	CHARACTER$(CH%)=STRING$(361%,0%) &
\	J$=CHARACTER$(0%) &
\	J$=CHARACTER$(100%) &
\	J$=SYS(CHR$(6%)+CHR$(-5%)+CHR$(KB%(USER%))+CRLF$+"You are dead."+CRLF$) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' is dead!') &
\	IF C%<>-1% THEN &
	LSET PEOPLE$=LEFT(PEOPLE$,INSTR(1%,PEOPLE$,"/"))+"!"+CVT$$( &
	NAM$(USER%),128%)+"'s corpse/"+RIGHT(PEOPLE$,INSTR(1%,PEOPLE$,"/")+1%) &
\	LSET OBJECT$=CVT$$(OB$(USER%,I%),128%)+"/"+OBJECT$ IF CVT$$(OB$(USER%,I%) &
	,255%)<>"" &
	FOR I%=0% TO 9% &
\	LSET OBJECT$=CVT$$(WEAPON$(USER%),128%)+"$"+NUM1$(WEAPON%(USER%))+"/"+OBJECT$ &
	IF CVT$$(WEAPON$(USER%),255%)<>"ASMALLDAGGER" &
\	PUT #3%, RECORD ROOM%(USER%) &

520	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' exits.') IF HP%(USER%)>0% &
\	NAM$(USER%)="" &
\	ROOM%(USER%)=0% &
\	STUN(USER%)=0% &
\	LEVEL%(USER%)=0% &
\	FLAG%(USER%)=0% &
\	NO.OF.USERS%=NO.OF.USERS%-1% &
\	GOSUB 540 &
\	J$=SYS(CHR$(6%)+CHR$(11%)+STRING$(20%,0%)+"KB"+CHR$(KB%(USER%))) &
	UNLESS KB%(USER%)=11% &
\	KB%(USER%)=0% &
\	SUBEXIT &

530	CALL ADVOUT(KB%(USER%),CRLF$+ &
	"You can't exit while somebody else is in the room.") &
\	SUBEXIT &

540	OPEN "$LOGOUT.ALL" AS FILE #4%, MODE 4096% &
\	UNTIL 0% &
\	INPUT LINE #4%, J$ &
\	CALL ADVOUT(KB%(USER%),CVT$$(J$,4%)) &
\	NEXT &

550	RETURN &

15000	! FUNCTIONS &
	&
	&

15110	DEF* FNCLEAR$ &
\	IF FLAG%(USER%) AND 1% THEN &
	HP%(USER%)=HP%(USER%)*3%/4% &
\	FLAG%(USER%)=FLAG%(USER%)-1% &

15120	FNEND &

25000	! ERRORS &
	&
	&
	RESUME 550 IF ERL=540 &

29000	OPEN "MSG:LOG1.95" AS FILE #4%, MODE 2% &
\	PRINT #4%, "ADVBYE : ";RIGHT(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERR)),3%); &
	" at line";ERL;" at ";TIME$(0%) &
\	PRINT #4%, "User was ";NAM$(USER%);" in room";ROOM%(USER%) IF &
	(USER%>0% AND USER%<=8%) &
\	CLOSE #4% &
\	RESUME 32767 &

32767	SUBEND &

