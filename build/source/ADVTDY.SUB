1	SUB ADVTDY &
\	COMMON ACC$(8%)=7%,AR$=80%,C%,C$=10%,CRLF$=2%, &
	FLAG%(8%),HP%(8%),LEVEL%(8%),KB%(8%),NAM$(8%)=30%,NO.OF.USERS%, &
	XP.(8%),STUN(8%), &
	NPCMESS$(8%)=40%,OB$(8%,9%)=30%,PASS$(8%)=10%, &
	ROOM%(8%),SENT.KB%,SENT.MESS$=80%,SENT.PROG%,SENT.PROJ%, &
	USER%,CI$=1%,WEAPON$(8%)=20%,WEAPON%(8%), &
	ATT.LEVEL%(10%),DEF.LEVEL%(10%),MON.HP%(10%),MON.DAM%(10%), &
	SPEC$(10%)=6%,MON.ROOM%(10%),MON.XP%(10%),RUN.ACC$=9%,MMM$=128%, &
	BLIND%(8%),USER1%,SPY%(10%),FATIGUE%(10%) &
\	FIELD #3%, 1% AS ROOM$, 16% AS EX$, 83% AS PEOPLE$, 100% AS OBJECT$, &
	312% AS DESC$ &
\	DIM #5%, MONSTER$(10000%)=20% &
\	DIM #7%, CHARACTER$(100%)=512% &
\	DIM #9%, NUM%(0%),SHOUT$(1000%)=60% &
\	ON ERROR GOTO 25000 &

10	OPEN RUN.ACC$+"ADVTDY.RPT<40>" FOR OUTPUT AS FILE #4% &
\	PRINT #4%, CRLF$;"**** ADVTDY report taken at ";TIME$(0%); &
	" on ";DATE$(0%);"****";CRLF$ &
\	J$=SYS(CHR$(6%)+CHR$(-10%)+'DK0:'+RUN.ACC$+'ADVENT.DTA') &
\	J$=SYS(CHR$(6%)+CHR$(17%)+CHR$(255%)+CHR$(255%)+ &
	RIGHT(J$,5%)) &
\	SIZE.FIL%=ASCII(RIGHT(J$,13%))+256%*ASCII(RIGHT(J$,14%)) &
\	PRINT #4%, CRLF$;FNU$('Main data file : '+NUM1$(SIZE.FIL%)+ &
	' rooms.');CRLF$ &
\	FOR ROOM%=1% TO SIZE.FIL% &
\	GET #3%, RECORD ROOM% &
\	LOGGED%=0% &

20	J%=FNLOG%('Is flagged as corrupt') IF ROOM$<>CHR$(ROOM%) &
\	J$=EX$ &
\	FOR I%=2% TO 11% STEP 3% &
\	J%=CVT$%(MID(J$,I%,2%)) &
\	JJ$=MID(J$,I%-1%,1%) &
\	J%=0% UNLESS INSTR(1%,'NESW',JJ$) &
\	IF J% THEN &
	JJ%=FNLOG%('Has exits to a room greater than exists ('+NUM1$(J%)+')') &
	IF J%>SIZE.FIL% &
\	IF J%<=SIZE.FIL% THEN &
	JJ%=FNLOG%('Has a long jump to '+NUM1$(J%)) IF ABS(ROOM%-J%)>100% &
\	JJJ$=MID('SWNE',INSTR(1%,'NESW',JJ$),1%) &
\	JJJ$=JJJ$+CVT%$(ROOM%) &
\	GET #3%, RECORD J% &
\	J$='' &
\	J$='Has a twisty exit to ' IF INSTR(1%,EX$,JJJ$)=0% AND &
	INSTR(1%,EX$,CVT%$(ROOM%)) &
\	J$='Has a one way or corrupt exit to ' IF INSTR(1%,EX$,JJJ$)=0% AND &
	INSTR(1%,EX$,CVT%$(ROOM%))=0% &
\	JJ%=FNLOG%(J$+NUM1$(J%)) IF LEN(J$) &
\	GET #3%, RECORD ROOM% &

25	NEXT I% &

30	J$=PEOPLE$ &
\	JJJ$='' &
\	J%=INSTR(1%,J$,'/') &

40	GOTO 70 UNLESS J% &
\	JJ$=LEFT(J$,J%) &
\	J$=RIGHT(J$,J%+1%) &
\	IF INSTR(1%,'!#*',LEFT(JJ$,1%))=0% OR &
	INSTR(1%,'ABCDEFGHIJKLMNOPQRSTUVWXYX'+ &
		    'abcdefghijklmnopqrstuvwxyz',MID(JJ$,2%,1%))=0% THEN &
	! Less effort than ascii value check. &
	J%=FNLOG%('Has invalid PEOPLE$ - corrected...') &
\	GOTO 60 &

50	JJJ$=JJJ$+JJ$ &

60	J%=INSTR(1%,J$,'/') &
\	GOTO 40 &

70	LSET PEOPLE$=JJJ$ &
\	PUT #3%, RECORD ROOM% &
\	GET #3%, RECORD ROOM% &
\	J%=INSTR(1%,OBJECT$,'~') &

80	GOTO 100 UNLESS J% &
\	JJ%=INSTR(J%,OBJECT$,'/') &
\	GOTO 100 UNLESS JJ% &
\	JJ%=VAL(MID(OBJECT$,J%+1%,JJ%-1%-J%)) &
\	JJ%=FNLOG%('Has valuable treasure ('+NUM1$(JJ%)+')') IF JJ%>500% &

90	J%=INSTR(J%+1%,OBJECT$,'~') &
\	GOTO 80 &

100	J%=INSTR(1%,OBJECT$,'$') &

110	GOTO 130 UNLESS J% &
\	JJ%=INSTR(J%,OBJECT$,'/') &
\	GOTO 130 UNLESS JJ% &
\	JJ%=VAL(MID(OBJECT$,J%+1%,JJ%-1%-J%)) &
\	JJ%=FNLOG%('Has a high DB weapon ('+NUM1$(JJ%)+')') IF JJ%>8% &

120	J%=INSTR(J%+1%,OBJECT$,'$') &
\	GOTO 110 &

130	J$=DESC$ &
\	J%=INSTR(1%,J$,'$') &
\	IF J%=0% THEN &
	LSET DESC$=DESC$+'$' &
\	PUT #3%, RECORD ROOM% &
\	GET #3%, RECORD ROOM% &
\	GOTO 180 &

140	J$=RIGHT(J$,J%+1%) &
\	GOTO 180 UNLESS LEFT(J$,2%)='/C' &
\	J%=INSTR(1%,J$,'/H') &
\	GOTO 150 UNLESS J% &
\	JJ%=INSTR(J%+1%,J$,'/') &
\	J%=VAL(MID(J$,J%+2%,JJ%-2%-J%)) &
\	J%=FNLOG%('Has a hit point special case ('+NUM1$(J%)+')') &

150	J%=INSTR(1%,J$,'$') &
\	GOTO 160 UNLESS J% &
\	JJ%=INSTR(J%+1%,J$,'/') &
\	J%=VAL(MID(J$,J%+1%,JJ%-1%-J%)) &
\	J%=FNLOG%('Has a weapon special case ('+NUM1$(J%)+')') &

160	J%=INSTR(1%,J$,'~') &
\	GOTO 170 UNLESS J% &
\	JJ%=INSTR(J%+1%,J$,'/') &
\	J%=VAL(MID(J$,J%+1%,JJ%-1%-J%)) &
\	J%=FNLOG%('Has a treasure special case ('+NUM1$(J%)+')') &

170	J%=INSTR(1%,J$,'/R') &
\	GOTO 180 UNLESS J% &
\	J%=VAL(MID(J$,J%+2%,INSTR(J%+1%,J$,'/')-2%-J%)) &
\	J%=FNLOG%('Has a special case long jump ('+NUM1$(J%)+')') &
	IF ABS(ROOM%-J%)>100% &

180	&

1000	NEXT ROOM% &

1010	PRINT #4%, CRLF$;FNU$('Character file');CRLF$ &
\	FOR CHAR%=0% TO 100% &
\	GOTO 1030 IF CVT$$(LEFT(CHARACTER$(CHAR%),10%),255%)='' &

1020	J$=CHARACTER$(CHAR%) &
\	LOGGED%=0% &
\	J$=RIGHT(J$,31%) &
\	J$=RIGHT(J$,11%) &
\	J%=FNLOG2%('Has an illegal room number') IF CVT$%(J$)>SIZE.FIL% &
\	J$=RIGHT(J$,3%) &
\	LEV%=CVT$%(J$) &
\	J%=FNLOG2%('Has a high level ('+NUM1$(LEV%)+')') IF LEV%>11% &
\	J$=RIGHT(J$,3%) &
\	J%=FNLOG2%('Has hit points above his maximum') IF &
	CVT$%(J$)>5%*INT((14%+2.8*LEV%^1.8)/5%) &
\	J$=RIGHT(J$,3%) &
\	J$=RIGHT(J$,6%) &
\	J$=RIGHT(J$,3%) &
\	J%=FNLOG2%('Has a high DB weapon ('+NUM1$(CVT$%(J$))+')') &
	IF CVT$%(J$)>8% &
\	J$=RIGHT(J$,3%) &
\	J$=RIGHT(J$,21%) &
\	J$=RIGHT(J$,3%) &
\	J$=RIGHT(J$,5%) &
\	FOR I%=0% TO 9% &
\	I$=LEFT(J$,30%) &
\	J$=RIGHT(J$,31%) &
\	J%=INSTR(1%,I$,'$') &
\	GOTO 1030 UNLESS J% &
\	J%=VAL(RIGHT(I$,J%+1%)) &
\	J%=FNLOG2%('Is carrying a high DB weapon ('+NUM1$(J%)+')') IF J%>8% &
\	GOTO 1040 &

1030	J%=INSTR(1%,I$,'~') &
\	GOTO 1040 UNLESS J% &
\	J%=VAL(RIGHT(I$,J%+1%)) &
\	J%=FNLOG2%('Is carrying valuable treasure ('+NUM1$(J%)+')') IF &
	J%>500% &

1040	NEXT I% &

1050	NEXT CHAR% &

2000	PRINT #4%, CRLF$;'**** Report completed at ';TIME$(0%);' ****';CRLF$ &
\	CLOSE #4% &
\	SUBEXIT &

15000	! FUNCTIONS &
	&
	&
	DEF* FNLOG%(A$) &
\	JJJ$="Room "+NUM1$(ROOM%)+" : " UNLESS LOGGED% &
\	JJJ$=CHR$(9%)+'    & ' IF LOGGED% &
\	LOGGED%=-1% &
\	PRINT #4%, JJJ$;A$ &
\	FNEND &

15010	DEF FNU$(A$)=A$+CRLF$+STRING$(LEN(A$),45%) &

15020	DEF* FNLOG2%(A$) &
\	JJ$=CRLF$+LEFT(CHARACTER$(CHAR%),10%)+" : " UNLESS LOGGED% &
\	JJ$=CHR$(9%)+'   & ' IF LOGGED% &
\	LOGGED%=-1% &
\	PRINT #4%, JJ$;A$ &
\	FNEND &

25000	! ERRORS &
	&
	&
	RESUME 1010 IF ERR=11 AND ERL=10 &
\	RESUME 180 IF ERL>=140 AND ERL<=170 &

25010	IF ERR=52 AND (ERL=80 OR ERL=110) THEN &
	JJ%=FNLOG%('Has an illegal object') &
\	RESUME 90 IF ERL=80 &
\	RESUME 120 IF ERL=110 &

25020	IF ERR=52 AND (ERL=1020 OR ERL=1030) THEN &
	JJ%=FNLOG2%('Is carrying an illegal object') &
\	RESUME 1040 &

29000	OPEN "MSG:LOG1.95" AS FILE #4%, MODE 2% &
\	PRINT #4%, "ADVTDY : ";RIGHT(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERR)),3%); &
	" at line";ERL;" at ";TIME$(0%) &
\	CLOSE #4% &
\	RESUME 32767 &

32767	SUBEND &

