10	SUB ADVMSG &
\	COMMON ACC$(8%)=7%,AR$=80%,C%,C$=10%,CRLF$=2%, &
	FLAG%(8%),HP%(8%),LEVEL%(8%),KB%(8%),NAM$(8%)=30%,NO.OF.USERS%, &
	XP.(8%),STUN(8%), &
	NPCMESS$(8%)=40%,OB$(8%,9%)=30%,PASS$(8%)=10%, &
	ROOM%(8%),SENT.KB%,SENT.MESS$=80%,SENT.PROG%,SENT.PROJ%, &
	USER%,CI$=1%,WEAPON$(8%)=20%,WEAPON%(8%), &
	ATT.LEVEL%(10%),DEF.LEVEL%(10%),MON.HP%(10%),MON.DAM%(10%), &
	SPEC$(10%)=6%,MON.ROOM%(10%),MON.XP%(10%),RUN.ACC$=9%,MMM$=128%, &
	BLIND%(8%),USER1%,SPY%(10%),FATIGUE%(10%) &
\	ON ERROR GOTO 25000 &
\	FIELD #3%, 1% AS ROOM$, 16% AS EX$, 83% AS PEOPLE$, 100% AS OBJECT$, &
	312% AS DESC$ &
\	DIM #5%, MONSTER$(10000%)=20% &
\	DIM #7%, CHARACTER$(100%)=512% &
\	ON ERROR GOTO 25000 &

20	MESS$=LEFT(SENT.MESS$,INSTR(1%,SENT.MESS$," ")-1%) &
\	MESS$=SENT.MESS$ UNLESS LEN(MESS$) &
\	MESS$=CVT$$(MESS$,255%) &
\	GOTO 1000 UNLESS LEN(MESS$) &
\	MESS.AR$=RIGHT(SENT.MESS$,LEN(MESS$)+2%) &
\	GOTO 100 UNLESS LEFT("LOG",LEN(MESS$))=MESS$ &
\	GOTO 80 IF CVT$$(LEFT(NAM$(I%),10%),255%)=CVT$$(LEFT(MESS.AR$,10%),255%) &
	 FOR I%=1% TO 8% &
\	GOTO 1000 UNLESS SENT.KB% &
\	GOTO 1000 IF KB%(I%)=SENT.KB% FOR I%=1% TO 8% UNLESS SENT.KB%=11% &
\	USER%=1% &
\	USER%=USER%+1% WHILE (CVT$$(NAM$(USER%),255%)<>"" AND USER%<=7%) &
\	GOTO 70 IF USER%=8% AND CVT$$(NAM$(8%),255%)<>'' &
\	I%=0% &
\	I%=I%+1% UNTIL CVT$$(LEFT(CHARACTER$(I%),10%),255%)=CVT$$(MESS.AR$, &
	255%) OR I%=100% &
\	SUBEXIT IF CVT$$(LEFT(CHARACTER$(I%),10%),255%)<>CVT$$(MESS.AR$,255%) &
	IF I%=100% &
\	TRY%=0% &
\	ON ERROR GOTO 60 &

30	J$=SYS(CHR$(6%)+CHR$(10%)+STRING$(20%,0%)+"KB"+CHR$(SENT.KB%)) &

31	KB%(USER%)=SENT.KB% &
\	J$=CHARACTER$(I%) &
\	NAM$(USER%)=LEFT(J$,30%) &
\	J$=RIGHT(J$,31%) &
\	PASS$(USER%)=LEFT(J$,10%) &
\	J$=RIGHT(J$,11%) &
\	ROOM%(USER%)=CVT$%(LEFT(J$,2%)) &
\	J$=RIGHT(J$,3%) &
\	LEVEL%(USER%)=CVT$%(LEFT(J$,2%)) &
\	PASS$(USER%)=CHR$(2%) IF LEVEL%(USER%)>16% &
\	SS.SS$=SYS(CHR$(6%)+CHR$(-5%)+CHR$(0%)+CRLF$+NAM$(USER%)+ &
	' logged in at '+TIME$(0%)+' on KB'+NUM1$(KB%(USER%))+CRLF$) &
	IF LEVEL%(USER%)>=30% &
\	J$=RIGHT(J$,3%) &
\	HP%(USER%)=CVT$%(LEFT(J$,2%)) &
\	J$=RIGHT(J$,3%) &
\	XP.(USER%)=VAL(LEFT(J$,5%)) &
\	J$=RIGHT(J$,6%) &
\	FLAG%(USER%)=CVT$%(LEFT(J$,2%)) OR EXTRA.FLAG% &
\	FLAG%(USER%)=FLAG%(USER%) OR NPC% &
\	J$=RIGHT(J$,3%) &
\	WEAPON%(USER%)=CVT$%(LEFT(J$,2%)) &
\	J$=RIGHT(J$,3%) &
\	WEAPON$(USER%)=LEFT(J$,20%) &
\	J$=RIGHT(J$,21%) &
\	PROJ%=ASCII(J$) &
\	PROJ%=PROJ%+255% IF PROJ%<0% &
\	PROG%=ASCII(RIGHT(J$,2%)) &
\	PROG%=PROG%+255% IF PROG%<0% &
\	J$=RIGHT(J$,3%) &
\	ACC$(USER%)=NUM1$(PROJ%)+","+NUM1$(PROG%) &
\	T1%=CVT$%(J$) &
\	T2%=CVT$%(RIGHT(J$,3%)) &
\	J$=RIGHT(J$,5%) &
	! Skip dummy variables. &
\	FOR I%=0% TO 9% &
\	OB$(USER%,I%)=LEFT(J$,30%) &
\	J$=RIGHT(J$,31%) &
\	NEXT I% &
\	FATIGUE%(USER%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	JJJ$=FNSHOUT$(CVT$$(NAM$(USER%),128%)+" enters.") &
\	CALL ADVDSP &
\	PRINT #1%, RECORD 32767%+1%+KB%(USER%), CRLF$; &
	'Last game finished at ';TIME$(1440%-T2%);' on ';DATE$(T1%) &
	IF T1% &
\	PRINT #1%, RECORD 32767%+1%+KB%(USER%), CRLF$;"> "; &
\	NO.OF.USERS%=NO.OF.USERS%+1% &
\	GOTO 1000 &

60	TRY%=TRY%+1% &
\	SLEEP 1% &
\	RESUME 30 UNLESS TRY%=8% &
\	RESUME 1000 &

70	MESS$="No room for any more players." &
\	GOTO 90 &

80	MESS$="That character is already playing." &
\	GOTO 90 &

85	MESS$="ADVENT : Can't create - Character file is full." &
\	GOTO 90 &

90	J$=SYS(CHR$(6%)+CHR$(-5%)+CHR$(SENT.KB%)+CRLF$+MESS$+CRLF$) &
	IF SENT.KB%<50% &
\	GOTO 1000 &

100	J$=SYS(CHR$(6%)+CHR$(-5%)+CHR$(SENT.KB%)+CRLF$+ &
	"Message received."+CRLF$) IF SENT.KB%<50% &
\	GOTO 200 UNLESS LEFT("CREATE",LEN(MESS$))=MESS$ &
\	I%=0% &
\	I%=I%+1% UNTIL CVT$$(LEFT(CHARACTER$(I%),10%),255%)="" &
\	WHO$=LEFT(MESS.AR$,INSTR(1%,MESS.AR$," ")-1%) &
\	MESS.AR$=RIGHT(MESS.AR$,LEN(WHO$)+2%) &
\	P$=LEFT(MESS.AR$,INSTR(1%,MESS.AR$," ")-1%) &
\	MESS.AR$=RIGHT(MESS.AR$,LEN(P$)+2%) &
\	ACC$=CVT$$(MESS.AR$,255%) &
\	J$=WHO$ &
\	J$=J$+CHR$(0%) WHILE LEN(J$)<30% &
\	J$=J$+P$ &
\	J$=J$+CHR$(0%) WHILE LEN(J$)<40% &
\	J$=J$+CVT%$(1%) &
\	J$=J$+CVT%$(0%) &
\	J$=J$+CVT%$(10%) &
\	J$=J$+"00000" &
\	J$=J$+CVT%$(0%) &
\	J$=J$+CVT%$(3%) &
\	J$=J$+"A small dagger"+STRING$(6%,0%) &
\	J$=J$+CHR$(VAL(LEFT(ACC$,INSTR(1%,ACC$,",")-1%)))+ &
	CHR$(VAL(RIGHT(ACC$,INSTR(1%,ACC$,",")+1%))) &
\	J$=J$+CVT%$(0%) &
\	J$=J$+CVT%$(0%) &
\	J$=J$+STRING$(300%,0%) &
\	J$=J$+CVT%$(30%) &
\	CHARACTER$(I%)=J$ &
\	I$=CHARACTER$(0%)+CHARACTER$(100%) &
\	GOTO 1000 &

200	GOTO 210 UNLESS MESS$="MON" &
\	J%=CVT$%(MESS.AR$) &
\	J$=RIGHT(MESS.AR$,3%) &
\	MONSTER$(J%)=J$ &
\	GOTO 1000 &

210	GOTO 220 UNLESS LEFT("BROADCAST",LEN(MESS$))=MESS$ &
\	PRINT #1%, RECORD 32767%+1%+KB%(I%), CRLF$; &
	"Now hear this - ";CVT$$(MESS.AR$,128%) IF CVT$$(NAM$(I%),255%) &
	<>"" FOR I%=1% TO 8% &
\	GOTO 1000 &

220	GOTO 230 UNLESS LEFT("SLEEP",LEN(MESS$))=MESS$ &
\	GOSUB 24000 &
\	PRINT #1%, RECORD 32767%+1%+KB%(I%), CRLF$; &
	"ADVENT going to sleep - ^Z to exit." &
	IF CVT$$(NAM$(I%),255%)<>"" FOR I%=1% TO 8% &
\	CLOSE #3% &
\	CLOSE #6% &
\	C%=-1% &
\	OPEN "_DK1:"+RUN.ACC$+"ADVENT.DTA" AS FILE #3%, MODE 4096% &
\	GOTO 1000 &

230	GOTO 240 UNLESS LEFT("WAKE",LEN(MESS$))=MESS$ &
\	OPEN "_DK1:"+RUN.ACC$+"ADVENT.DTA" AS FILE #3%, MODE 257% &
\	OPEN "_DK1:"+RUN.ACC$+"BOARD.NTC" AS FILE #6% &
\	C%=0% &
\	PRINT #1%, RECORD 32767%+1%+KB%(I%), CRLF$; &
	"ADVENT has woken up." &
	IF CVT$$(NAM$(I%),255%)<>"" FOR I%=1% TO 8% &
\	GOTO 1000 &

240	GOTO 250 UNLESS LEFT("WORKMAN",LEN(MESS$))=MESS$ &
\	OPEN "_DK0:"+RUN.ACC$+"REFRSH.CTL" FOR INPUT AS FILE #8%, MODE 4096% &
\	USER%=1% &
\	USER%=USER%+1% WHILE CVT$$(NAM$(USER%),255%)<>"" AND USER%<=7% &
\	SUBEXIT IF USER%=8% &
\	SENT.KB%=11% &
\	EXTRA.FLAG%=128% &
\	GOTO 31 &

250	GOTO 260 UNLESS LEFT("LIST",LEN(MESS$))=MESS$ &
\	J$=SYS(CHR$(6%)+CHR$(-5%)+CHR$(SENT.KB%)+CVT$$(NAM$(I%),128%)+" on KB"+ &
	NUM1$(KB%(I%))+" in "+ACC$(I%)+CRLF$) IF CVT$$(NAM$(I%),255%) &
	<>"" FOR I%=1% TO 8% &
\	GOTO 1000 &

260	GOTO 270 UNLESS LEFT("COMA",LEN(MESS$))=MESS$ &
\	GOSUB 24000 &
\	PRINT #1%, RECORD 32767%+1%+KB%(I%), CRLF$; &
	"ADVENT falling into a coma." IF CVT$$(NAM$(I%),255%)<>"" &
	FOR I%=1% TO 8% &
\	CLOSE #7% &
\	J$=SYS(CHR$(6%)+CHR$(-13%)+CHR$(255%)+CHR$(255%)+CHR$(-128%)) &
\	OPEN "_DK1:"+RUN.ACC$+"ADVENT.CHR" AS FILE #7% &
\	PRINT #1%, RECORD 32767%+1%+KB%(I%), CRLF$; &
	"ADVENT has staged a miraculous recovery." IF CVT$$(NAM$(I%),255%)<>"" &
	FOR I%=1% TO 8% &
\	GOTO 1000 &

270	GOTO 290 UNLESS MESS$="LT" &
\	GOTO 1000 UNLESS LEN(CVT$$(MESS.AR$,255%)) &
\	J%=VAL(MESS.AR$) &
\	GOTO 280 IF KB%(USER%)=J% FOR USER%=1% TO 8% &
\	GOTO 1000 &

280	C%=-2% &
\	PRINT #1%, RECORD 32767%+1%+KB%(USER%), CRLF$; &
	"Your game has been terminated." &
\	GOTO 1000 &

290	GOTO 300 UNLESS MESS$='NPC' &
\	SENT.KB%=11% &
\	NPC%=64% &
\	SENT.MESS$='LOG '+CVT$$(RIGHT(SENT.MESS$,4%),255%) &
\	GOTO 20 &

300	GOTO 310 UNLESS MESS$="TIDY" &
\	CALL ADVTDY &
\	GOTO 1000 &

310	&

320	GOTO 340 UNLESS MESS$='TELL' &
\	J%=INSTR(1%,MESS.AR$,' ') &
\	GOTO 1000 UNLESS J% &
\	J$=LEFT(MESS.AR$,J%-1%) &
\	GOTO 330 IF CVT$$(LEFT(NAM$(I%),10%),255%)=CVT$$(J$,255%) &
	FOR I%=1% TO 8% &
\	GOTO 1000 &

330	PRINT #1%, RECORD 32767%+1%+KB%(I%), CRLF$;RIGHT(MESS.AR$,J%) &
\	GOTO 1000 &

340	GOTO 360 UNLESS MESS$='STAT' &
\	J$=CVT$$(MESS.AR$,255%) &
\	GOTO 1000 UNLESS LEN(J$) &
\	GOTO 350 IF CVT$$(LEFT(NAM$(I%),10%),255%)=J$ &
	FOR I%=1% TO 8% &
\	GOTO 1000 &

350	MESS$='Name'+CHR$(9%)+NAM$(I%)+CRLF$+'Level'+CHR$(9%)+ &
	NUM1$(LEVEL%(I%))+CRLF$+'HP'+CHR$(9%)+NUM1$(HP%(I%))+CRLF$+ &
	'Room'+CHR$(9%)+NUM1$(ROOM%(I%))+CRLF$ &
\	GOTO 90 &

360	GOTO 370 UNLESS MESS$='SEND' &
\	J%=INSTR(1%,MESS.AR$,' ') &
\	SUBEXIT UNLESS J% &
\	WHO$=LEFT(MESS.AR$,J%-1%) &
\	WHERE%=VAL(RIGHT(MESS.AR$,J%+1%)) &
\	WHO$=CVT$$(WHO$,255%) &
\	GOTO 365 IF LEFT(NAM$(I%),LEN(WHO$))=WHO$ FOR I%=1% TO 8% &
\	SUBEXIT &

365	ROOM%(I%)=WHERE% &
\	CALL ADVDSP &
\	SUBEXIT &

370	&


999	MESS$="?Illegal command." &
\	GOTO 90 &

1000	SUBEXIT &

15000	! FUNCTIONS &
	&
	&

15130	DEF* FNSHOUT$(MMESS$) &
\	PRINT #1%, RECORD 32767%+1%+KB%(JJJ%), CRLF$;MMESS$ IF ROOM%(JJJ%)= &
	ROOM%(USER%) AND JJJ%<>USER% &
	FOR JJJ%=1% TO 8% UNLESS FLAG%(USER%) AND 2% &
\	FNEND &

24000	FOR I%=1% TO 8% &
\	GOTO 24030 UNLESS ROOM%(I%) &
\	OPEN '_SY:[1,95]ADVENT.NTC' FOR INPUT AS FILE #4% &
\	UNTIL 0% &
\	INPUT LINE #4%, J$ &
\	PRINT #1%, RECORD 32767%+1%+KB%(I%), J$; &
\	NEXT &

24030	CLOSE #4% &
\	NEXT I% &
\	RETURN &

25000	! ERRORS &
	&
	&
	RESUME 85 IF ERL=100 AND ERR=55 &
\	RESUME 24030 IF ERL=24000 AND ERR=11 &

29000	OPEN "MSG:LOG1.95" AS FILE #4%, MODE 2% &
\	PRINT #4%, "ADVMSG : ";RIGHT(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERR)),3%); &
	" at line";ERL;" at ";TIME$(0%) &
\	PRINT #4%, "Command was ";SENT.MESS$;" from KB";NUM1$(SENT.KB%) &
\	CLOSE #4% &
\	RESUME 32767 &

32767	SUBEND &

