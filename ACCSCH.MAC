	.title	accsch
;
;	Look up a name/account in $ACCT.SYS.
;
;	E.D.W. Hibbert 11/8/87
;
;	Compilation:
;
;		MACR ACCSCH=PA:DEFS,A:[1,95]ACCSCH (or wherever)
;	(note MACR rather than expected MAC, as MACR is twice as fast.)
;		TKB ACCSCH=ACCSCH,PA:IOLIB/LB
;
;	If you're not using it in a [1,*], then privilege it.
;
	$rsx
	$rwdef

start:	.lmargin
	.newline
	.print	#header			; Print program header
	.errprt	#0			; And system header
	.newline
	.newline

doit:	calls	l$close,<#1>
	open$r	#fnadd,#1		; Open $ACCT.SYS
	ifnoerr	1$			; Opened ok?
	.lmargin			; No...
	.print	#opnerr			; Tell them
	.errprt	er$sav			; And give them the error
	.newline
	.newline
	.exit

1$:	clr	@#24			; no ^C trap
	calls	$zapxrb			; and we want to reverse CTRL/O
	movb	#2,xrb+7
	.spec				; do it
	.lmargin
	.newline
	.print	#prompt			; Ask for something
	.input	#search			; Get reply
	iferr	20$			; ^Z?
	mov	by$cnt,r0		; Get length
	cmp	r0,#3
	blt	doit			; Can't be shorter than this
	clrb	search(r0)		; Put null terminator
	.newline			; No
	.len	#search			; Get length of search string
	calls	cvt$$,<#search,r0,#255.> ; Strip junk
	mov	r0,r4			; save length
	beq	doit			; bad account
	mov	#doit,@#24		; ^C trap

2$:	calls	l$glin,<#buf,#1,#lin>	; Get line from file
	ifnoerr	4$			; End of file
	jmp	doit

4$:	clr	r3
	clrb	lin(r0)			; put null on the end
	mov	r0,r2			; and save length for later

5$:	cmp	r3,r4			; end?
	beq	6$			; yes, then it matches
	cmpb	lin(r3),search(r3)	; check for match
	bne	2$			; failed, try next line
	inc	r3			; check next character
	br	5$

6$:	cmpb	lin(r3),#',		; make sure we get the right one
	bne	2$			; no it's not, so try next line
	add	#lin,r2
	dec	r2			; now find the name

7$:	cmpb	(r2),#',		; found a comma?
	beq	8$			; yes, print all to right of it
	dec	r2			; step back
	bne	7$			; try again
	br	2$			; just to be safe

8$:	inc	r2			; don't want the comma
	.print	r2			; and give them the name
	jmp	doit			; finished now

20$:	.newline
	.exit

opnerr:	.asciz	/Error in opening $ACCT.SYS : /
	.even
header:	.asciz	/ACCSCH /
	.even
fnadd:	.asciz	/$ACCT.SYS/		; Filename
buf:	.blkb	512.			; Buffer for $GLIN
lin:	.blkb	256.			; Line buffer
prompt:	.asciz	/Account >> /
	.even
search:	.blkb	256.			; What we're looking for
comma:	.asciz	/,/			; Macro is a bore

	.end	start

