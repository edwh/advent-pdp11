1	SUB STATUS(ARG$) &
\	ON ERROR GOTO 25000 &
	! &
	!	Written by E.D.W. Hibbert 1987-1988. &
	! &

5000	S.D$=FNGETARG$ &
\	GOTO 5015 IF LEN(S.D$) &
\	PRINT #1%, FNC$;CRLF$;'Change the loan/reference status of a book.'; &
	CRLF$; &

5010	PRINT #1%, FNC$; &
	CRLF$;'Accession number of the book >> '; &
\	INPUT LINE #1%, S.D$ &
\	S.D$=CVT$$(S.D$,255%) &
\	SUBEXIT UNLESS LEN(S.D$) &

5015	J$=CVT%$(VAL(S.D$)-32768.) &
\	GOTO 5030 &

5020	PRINT #1%, FNC$;CRLF$;'?Illegal accession number.' &
\	GOTO 30000 &

5030	GET #3%, KEY #0% EQ J$ &
\	GOTO 5050 UNLESS FLAG% AND 512% &
\	PRINT #1%, FNC$;CRLF$;"?Can't change the status on an ordered book." &
\	GOTO 30000 &

5040	PRINT #1%, FNC$;CRLF$;'?No book with that accession number.' &
\	GOTO 30000 &

5050	PRINT #1%, FNC$;CRLF$;'Accession number';TAB(25%);' >> '; &
	FNPRETTY.ACCESSION$(CVT$%(ACCESSION$)+32768.);CRLF$;'Title';TAB(25%); &
	' >> ';TITLE$;CRLF$; &
	'Author(s)';TAB(25%);' >> '; &

5060	GET #4%, KEY #0% EQ ACCESSION$ &
\	AUT$=CVT$$(LEFT(A.AUTHOR$,25%),128%) &
\	UNTIL 0% &

5070	GET #4% &
\	GOTO 5080 UNLESS A.ACCESSION$=ACCESSION$ &
\	AUT$=AUT$+' & '+CVT$$(LEFT(A.AUTHOR$,25%),128%) &
\	NEXT &

5080	PRINT #1%, AUT$;CRLF$;CRLF$; &
	'Are you sure that you want this book (Y/N) <Y> ? '; &
\	INPUT LINE #1%, J$ &
\	J$=CVT$$(J$,255%) &
\	J$='YES' UNLESS LEN(J$) &
\	GOTO 5500 UNLESS LEFT('YES',LEN(J$))=J$ &

5090	ST$=FNGETARG$ &
\	GOTO 5100 IF LEN(ST$) &
\	PRINT #1%, FNC$;CRLF$;'New status (L/S/R/Q/SC,M/UR) <L> >> '; &
\	INPUT LINE #1%, ST$ &
\	ST$=CVT$$(ST$,255%) &
\	ST$='L' UNLESS LEN(ST$) &

5100	ST$=CVT$$(ST$,255%) &
\	ST$='L'+ST$ IF LEFT(ST$,1%)=',' &
\	J%=INSTR(1%,ST$,',') &
\	ST1$=ST$ &
\	ST2$='' &
\	GOTO 5110 UNLESS J% &
\	ST1$=LEFT(ST$,J%-1%) &
\	ST2$=RIGHT(ST$,J%+1%) &

5110	J%=INSTR(1%,'/L /S /R /Q /SC/','/'+ST1$) &
\	GOTO 5200 UNLESS J% &
\	FLAG%=FLAG% XOR 2%^I% IF FLAG% AND 2%^I% FOR I%=0% TO 4% &
\	FLAG%=FLAG% XOR 2%^I% IF FLAG% AND 2%^I% FOR I%=10% TO 11% &
	! Zero out old flag bits &
\	FLAG%=FLAG% OR 2%^((J%-1%)/3%) &
	! Put in new bit &

5120	GOTO 5130 UNLESS LEN(ST2$) &
\	J%=INSTR(1%,'/UR/M','/'+ST2$) &
\	GOTO 5200 UNLESS J% &
\	FLAG%=FLAG% OR 2%^((J%-1%)/3%+10%) &
	! Put in new bit &

5130	UPDATE #3% &
\	PRINT #1%, FNC$;CRLF$;'Changed OK.' &
\	GOTO 30000 &

5200	PRINT #1%, FNC$;CRLF$;'?Illegal status.' &
\	GOTO 30000 &

5500	J$=SYS(CHR$(2%))+SYS(CHR$(0%)) &
\	PRINT #1%, FNC$;CRLF$;'%Status aborted.' &
\	GOTO 30000 &

20061	DEF* FNPRETTY.ACCESSION$(P.A.N.) &
\	J1$=NUM1$(P.A.N.) &
\	J1$=STRING$(5%-LEN(J1$),ASCII('0'))+J1$ &
\	J1%=0% &
\	J1%=J1%+I%*(ASCII(RIGHT(J1$,I%))-ASCII('0')) FOR I%=1% TO 5% &
\	J1%=J1%-J1%/26%*26% &
\	J1$=RIGHT(J1$,2%) WHILE LEFT(J1$,1%)='0' &
\	FNPRETTY.ACCESSION$=J1$+'-'+CHR$(J1%+ASCII('A')) &
\	FNEND &

25000	! ERRORS &
	&
	&
	J%=CTRLC &
\	RESUME 32767 IF (ERR=28 AND LINE=5010) OR (ERL=5010 AND ERR=11) &
\	RESUME 5500 IF ERR=28 &
\	RESUME 32767 IF ERL=5010 AND ERR=11 &
\	RESUME 5020 IF ERL=5015 &
\	RESUME 5040 IF ERL=5030 AND ERR=155 &
\	RESUME 5080 IF ERL=5070 AND ERR=11 &
\	RESUME 5500 IF ERL=5080 AND ERR=11 &
\	RESUME 32767 IF ERL=5000 AND ERR=11 &

29000	PRINT #1%, FNC$;CRLF$;'STATUS : ';ERT$(ERR);' at line';ERL &
\	RESUME 32767 &

32767	SUBEND &

