1	SUB REPAIR(ARG$) &
\	ON ERROR GOTO 25000 &
	! &
	!	Written by E.D.W. Hibbert 1987-1988. &
	! &

5000	S.D$=FNGETARG$ &
\	GOTO 5015 IF LEN(S.D$) &
\	PRINT #1%, FNC$;CRLF$;'Repair/Fix a book.';CRLF$ &

5010	PRINT #1%, FNC$;CRLF$;'Accession number >> '; &
\	INPUT LINE #1%, S.D$ &
\	S.D$=CVT$$(S.D$,255%) &
\	SUBEXIT UNLESS LEN(S.D$) &

5015	J$=CVT%$(VAL(S.D$)-32768.) &
\	GOTO 5030 &

5020	PRINT #1%, FNC$;CRLF$;'?Illegal accession number.' &
\	GOTO 30000 &

5030	GET #3%, KEY #0% EQ J$ &
\	GOTO 5050 &

5040	PRINT #1%, FNC$;CRLF$;'?No book with that accession number.' &
\	GOTO 30000 &

5050	PRINT #1%, FNC$;CRLF$;'Accession number';TAB(25%);' >> '; &
	FNPRETTY.ACCESSION$(CVT$%(ACCESSION$)+32768.);CRLF$;'Title'; &
	TAB(25%);' >> ';TITLE$;CRLF$; &
	'Author(s)';TAB(25%);' >> '; &

5060	GET #4%, KEY #0% EQ ACCESSION$ &
\	AUT$=CVT$$(LEFT(A.AUTHOR$,25%),128%) &
\	UNTIL 0% &

5070	GET #4% &
\	GOTO 5080 UNLESS A.ACCESSION$=ACCESSION$ &
\	AUT$=AUT$+' & '+CVT$$(LEFT(A.AUTHOR$,25%),128%) &
\	NEXT &

5080	IF FLAG% AND 512% THEN &
	PRINT #1%, FNC$;CRLF$;'?This book is on order.' &
\	GOTO 30000 &

5090	PRINT #1%, AUT$;CRLF$;CRLF$; &
	'Are you sure that you want this book (Y/N) <Y> ? '; &
\	INPUT LINE #1%, J$ &
\	J$=CVT$$(J$,255%) &
\	J$='YES' UNLESS LEN(J$) &
\	GOTO 5500 UNLESS LEFT('YES',LEN(J$))=J$ &

5100	PRINT #1%, FNC$;CRLF$;'Is it now under repair or fixed (R/F) <F> ? ';&
\	INPUT LINE #1%, J$ &
\	J$=CVT$$(J$,255%) &
\	J$='F' UNLESS LEN(J$) &
\	IF J$='R' THEN FLAG%=FLAG% OR 2%^10% &
\	GOTO 5130 &

5110	IF J$='F' THEN FLAG%=FLAG% XOR 2%^10% IF FLAG% AND 2%^10% &
\	GOTO 5130 &

5120	PRINT #1%, FNC$;CRLF$;'?Illegal reply.' &
\	GOTO 5100 &

5130	UPDATE #3% &
\	PRINT #1%, FNC$;CRLF$;'Changed OK.' &
\	GOTO 30000 &

5500	J$=SYS(CHR$(2%))+SYS(CHR$(0%)) &
\	PRINT #1%, FNC$;CRLF$;'%Repair aborted.' &
\	GOTO 30000 &

20000	DEF* FNPRETTY.ACCESSION$(P.A.N.) &
\	J1$=NUM1$(P.A.N.) &
\	J1$=STRING$(5%-LEN(J1$),ASCII('0'))+J1$ &
\	J1%=0% &
\	J1%=J1%+I%*(ASCII(RIGHT(J1$,I%))-ASCII('0')) FOR I%=1% TO 5% &
\	J1%=J1%-J1%/26%*26% &
\	J1$=RIGHT(J1$,2%) WHILE LEFT(J1$,1%)='0' &
\	FNPRETTY.ACCESSION$=J1$+'-'+CHR$(J1%+ASCII('A')) &
\	FNEND &

25000	! ERRORS &
	&
	&
	J%=CTRLC &
\	RESUME 32767 IF (ERL=5010 AND ERR=11) OR (ERR=28 AND LINE=5010) &
\	RESUME 5500 IF ERR=28 &
\	RESUME 5020 IF ERL=5015 &
\	RESUME 5040 IF ERL=5030 AND ERR=155 &
\	RESUME 5080 IF ERL=5070 AND ERR=11 &
\	RESUME 5500 IF ERL=5080 AND ERR=11 &

29000	PRINT #1%, FNC$;CRLF$;'REPAIR : ';ERT$(ERR);' at line';ERL &
\	RESUME 32767 &

32767	SUBEND &

