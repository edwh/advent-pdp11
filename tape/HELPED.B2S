1      SUB HELPED(ARG$) &
\	ON ERROR GOTO 25000 &
	! &
	!	Written by E.D.W. Hibbert 1987-1988. &
	! &

1999    MAP (HELP) H.TOPIC$=20%, &
                   H.HELPFILE$=6%, &
                   H.FLAG% &
                   ! Bit        Meaning &
                   &
                   !  0         Only visible from LIBR, not USER. &

2000    OPEN 'DP2:'+PKG.LOC$+'HELP.RMS' AS FILE #7%, &
        ORGANIZATION INDEXED FIXED, &
        MAP HELP, &
        ACCESS MODIFY, &
        ALLOW MODIFY, &
        PRIMARY KEY H.TOPIC$ NODUPLICATES &

5000    S.D$=CVT$$(ARG$,36%) &
\       GOTO 5050 IF LEN(S.D$) &
\       PRINT #1%, FNC$;CRLF$;'Maintain HELP System.';CRLF$; &

5010    PRINT #1%, FNC$;CRLF$; &
        'HELP Topic (LF for list of keywords) >> '; &
\       INPUT LINE #1%, S.D$ &
\       GOTO 5020 IF ASCII(S.D$)=10% &
\       S.D$=CVT$$(S.D$,36%) &
\       SUBEXIT UNLESS LEN(S.D$) &
\       GOTO 5050 &

5020    RESTORE #7 &
\	PRINT #1% &

5030    GET #7% &
\       UNLOCK #7% &
\       PRINT #1%, H.TOPIC$; &
\       GOTO 5030 &

5040	PRINT #1%, FNC$;CRLF$;'?Maximum 20 characters.' &
\	GOTO 5010 &

5050	GOTO 5040 IF LEN(S.D$)>20% &
\	NEW.TOPIC%=0% &
\       GET #7%, KEY #0% EQ S.D$ &
\       PRINT #1%, FNC$;CRLF$;'Topic';TAB(25%);'HelpFile';TAB(41%); &
        'Info.' &
\       PRINT #1%, H.TOPIC$;TAB(25%);'DP1:'+CVT$$(PKG.LOC$,128%)+ &
	H.HELPFILE$;' '; &
        TAB(40%); &
\       PRINT #1%, 'LIBR only'; IF H.FLAG% AND 1% &
\       PRINT #1% &

5060    PRINT #1%, FNC$;CRLF$;'Delete this topic <N> ? '; &
\       INPUT LINE #1%, J$ &
\       J$=CVT$$(J$,255%) &
\       J$='N' UNLESS LEN(J$) &
\       GOTO 5070 UNLESS LEFT('YES',LEN(J$))=J$ &
\       DELETE #7% &
\       PRINT #1%, FNC$;CRLF$;'Topic deleted Ok.' &
\       GOTO 30000 &

5070    PRINT #1%, FNC$;CRLF$;'HelpFile <DP1:'+CVT$$(PKG.LOC$,128%) &
	+H.HELPFILE$;'.HLP>'; &
        STRING$(11%,8%); &
\       INPUT LINE #1%, J$ &
\       J$=CVT$$(J$,255%) &
\       J$=H.HELPFILE$ UNLESS LEN(J$) &
\       IF LEN(J$)>6 THEN PRINT #1%, FNC$;CRLF$; &
        '?Maximum of six letters.' &
\       GOTO 5070 &

5080    GOTO 5090 IF MID(J$,I%,1%)<'A' OR MID(J$,I%,1%)>'Z' &
        FOR I%=1% TO LEN(J$) &
\       GOTO 5100 &

5090    PRINT #1%, FNC$;CRLF$;'?Alphabetic characters only.' &
\       GOTO 5070 &

5100    H.HELPFILE$=J$ &

5110    PRINT #1%, FNC$;CRLF$;'LIBR-visible only <'; &
\       J$='N' &
\       J$='Y' IF H.FLAG% AND 1% &
\       PRINT #1%, J$;'> ? '; &
\       INPUT LINE #1%, J1$ &
\       J1$=CVT$$(J1$,255%) &
\       J1$=J$ UNLESS LEN(J1$) &
\       IF J1$<>J$ THEN &
        H.FLAG%=H.FLAG% XOR 1% IF H.FLAG% AND 1% &
\       H.FLAG%=H.FLAG% OR -(J1$='Y') &

5120    PRINT #1%, FNC$;CRLF$;'Really save this <YES> ? '; &
\       INPUT LINE #1%, J$ &
\       J$=CVT$$(J$,255%) &
\       J$='YES' UNLESS LEN(J$) &
\       IF LEFT('YES',LEN(J$))=J$ THEN &
        UPDATE #7% UNLESS NEW.TOPIC% &
\       PUT #7% IF NEW.TOPIC% &
\       PRINT #1%, FNC$;CRLF$;'Saved Ok.' &
\       GOTO 30000 &

5130	PRINT #1%, FNC$;CRLF$;'%HelpEdit aborted.' &
\	GOTO 30000 &

5200    H.HELPFILE$='' &
\       H.FLAG%=0% &
\       PRINT #1%, FNC$;CRLF$;'Entering New Help Topic : ';S.D$ &
\	H.TOPIC$=S.D$ &
\	NEW.TOPIC%=-1% &
\	GOTO 5070 &

5500    PRINT #1%, FNC$;CRLF$;'%HelpEdit aborted.' &
\       GOTO 30000 &

15080
15090
15100
15110
15120
25000   ! Errors &
        &
        &
        J%=CTRLC &
\       RESUME 5200 IF ERL=5050 AND ERR=155 &
\	RESUME 5010 IF ERL=5030 AND ERR=11 &
\	RESUME 32767 IF ERR=28 AND LINE=5010 &
\	RESUME 32767 IF ERR=11 AND ERL=5010 &
\	RESUME 5130 IF ERR=28 OR ERR=11 &

29000	PRINT #1%, FNC$;CRLF$;'HELPED : ';ERT$(ERR);' at line';ERL &
\	RESUME 32767 &

32767	SUBEND &

