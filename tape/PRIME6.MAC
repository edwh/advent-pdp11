	.title	prime6
	$rsx
	$rwdef
	prnt = 0
;
;	A prime program.  It's very,very fast (0.6 secs CPU time for
;	primes upto 32767). Reasons it is so fast are:
;
;	1. Sieve.
;	2. Steps of two - even numbers obviously not prime.
;	3. Loop to set up sieve goes from prime^2 to 32767/prime.
;	   Reason for prime^2 is that all smaller multiples of
;	   prime have been knocked out (eg prime*3 etc.).  This idea
;	   from Karl Shaw.
;	4. Only loops for sieve check upto 182 (root 32767), for the
;	   same reason as loop goes from prime^2.
;
;	Change prnt to 1 to print primes, 0 not to.
;
;
;	Compilation :
;
;		MACR PRIME6=PA:DEFS,A:PRIME6
;	(use MACR as it's twice as fast as MAC)
;		TKB PRIME6=PRIME6,PA:IOLIB/LB
;
;	E.D.W. Hibbert  September 1987.
;
;

start:	mov	#3,r3
1$:	tstb	store(r3)
	bne	4$
	mov	#32767.,r1
	clr	r0
	div	r3,r0
	mov	r3,r4
	mul	r3,r4
	.if	ne,prnt
	.ift
	.wrdec	r3
	.newline
	.endc
2$:	cmp	r4,r0
	bhi	4$
	movb	#1,store(r4)
	add	r3,r4
	br	2$
4$:	add	#2,r3
	cmp	r3,#182.
	blo	1$
6$:	tstb	store(r3)
	bne	7$
	.if	ne,prnt
	.ift
	.wrdec	r3
	.newline
	.endc
7$:	add	#2,r3
	cmp	r3,#32767.
	blo	6$
	.exit

store:	.blkb	32767.
	.even

	.end	start
