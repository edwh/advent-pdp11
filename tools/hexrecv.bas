10 PRINT "HEXRECV V1.0"
20 T%=0
30 F%=0
100 INPUT L$
110 IF LEN(L$)=0 THEN 100
120 IF LEFT$(L$,1)<>":" THEN 100
130 IF L$=":END" THEN 500
150 C%=0
160 FOR I%=2 TO LEN(L$)
165 IF C%=0 THEN IF MID$(L$,I%,1)=":" THEN C%=I%
180 NEXT I%
185 IF C%>0 THEN 200
190 IF F%=1 THEN CLOSE #1
192 F$=MID$(L$,2,LEN(L$)-1)
194 OPEN F$ FOR OUTPUT AS FILE #1
196 F%=1
197 T%=0
198 PRINT "OK"
199 GOTO 100
200 IF F%=0 THEN PRINT "ERR"
202 IF F%=0 THEN 100
220 H$=MID$(L$,C%+6,LEN(L$))
230 FOR J%=1 TO LEN(H$)-1 STEP 2
240 H1$=MID$(H$,J%,1)
242 H2$=MID$(H$,J%+1,1)
244 N1%=0
245 N2%=0
246 IF H1$>="0" THEN IF H1$<="9" THEN N1%=ASC(H1$)-48
247 IF H1$>="A" THEN IF H1$<="F" THEN N1%=ASC(H1$)-55
248 IF H1$>="a" THEN IF H1$<="f" THEN N1%=ASC(H1$)-87
250 IF H2$>="0" THEN IF H2$<="9" THEN N2%=ASC(H2$)-48
252 IF H2$>="A" THEN IF H2$<="F" THEN N2%=ASC(H2$)-55
254 IF H2$>="a" THEN IF H2$<="f" THEN N2%=ASC(H2$)-87
256 B%=N1%*16+N2%
260 PRINT #1,CHR$(B%);
270 T%=T%+1
280 NEXT J%
290 PRINT "OK"
295 GOTO 100
500 IF F%=1 THEN CLOSE #1
520 PRINT "DONE:";T%
530 END
