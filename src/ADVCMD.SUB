10	SUB ADVCMD &
\	COMMON ACC$(8%)=7%,AR$=80%,C%,C$=10%,CRLF$=2%, &
	FLAG%(8%),HP%(8%),LEVEL%(8%),KB%(8%),NAM$(8%)=30%,NO.OF.USERS%, &
	XP.(8%),STUN(8%), &
	NPCMESS$(8%)=40%,OB$(8%,9%)=30%,PASS$(8%)=10%, &
	ROOM%(8%),SENT.KB%,SENT.MESS$=80%,SENT.PROG%,SENT.PROJ%, &
	USER%,CI$=1%,WEAPON$(8%)=20%,WEAPON%(8%), &
	ATT.LEVEL%(10%),DEF.LEVEL%(10%),MON.HP%(10%),MON.DAM%(10%), &
	SPEC$(10%)=6%,MON.ROOM%(10%),MON.XP%(10%),ACC.RUN$=9%,MMM$=128%, &
	BLIND%(8%),USER1%,SPY%(10%),FATIGUE%(10%),SINGLE.USER% &
\	ON ERROR GOTO 25000 &
\	DIM #6%, INDEX%(511%), BOARD$(511%)=512% &
\	FIELD #3%, 1% AS ROOM$, 16% AS EX$, 83% AS PEOPLE$, 100% AS OBJECT$, &
	312% AS DESC$ &
\	DIM #5%, MONSTER$(10000%)=20% &
\	DIM #7%, CHARACTER$(100%)=512% &
\	ON ERROR GOTO 25000 &

20	ON C%-19% GOSUB 30,50,110,140,160,200,220,230,280,290,310,320,330 &
	,360,370,380,390,410,420,440,460,480,510,530,550 &
\	SUBEXIT &

30	! WEAPON &
	&
	&
	GOTO 40 IF INSTR(1%,CVT$$(OB$(USER%,I%),255%),CVT$$(AR$,255%))<>0% &
	FOR I%=0% TO 9% &
\	GOTO 11010 &

40	GOTO 11040 UNLESS INSTR(1%,OB$(USER%,I%),"$") &
\	OLD$=CVT$$(WEAPON$(USER%),128%) &
\	OLD$="his"+RIGHT(OLD$,INSTR(1%,OLD$," ")) IF INSTR(1%,OLD$," ")<=3% &
	AND INSTR(1%,OLD$," ")>0% &
\	J$=CVT$$(WEAPON$(USER%),128%)+"$"+NUM1$(WEAPON%(USER%)) &
\	I$=OB$(USER%,I%) &
\	N$=CVT$$(I$,128%) &
\	N$="his"+RIGHT(N$,INSTR(1%,N$," ")) IF INSTR(1%,N$," ")<=3% &
	AND INSTR(1%,N$," ")>0% &
\	N$=LEFT(N$,INSTR(1%,N$,"$")-1%) &
\	WEAPON$(USER%)=LEFT(I$,INSTR(1%,I$,"$")-1%) &
\	OB$(USER%,I%)=J$ &
\	WEAPON%(USER%)=VAL(RIGHT(I$,INSTR(1%,I$,"$")+1%)) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" puts away "+OLD$+ &
	" and gets out "+N$) &
\	GOTO 11020 UNLESS WEAPON%(USER%)>30% &
\	CALL ADVSHT(CRLF$+'Unfortunately, in so doing he seals his fate.') &
\	HP%(USER%)=-10000% &
\	CALL ADVOUT(KB%(USER%),CRLF$+'Oh dear, a corrupt weapon.  Bye!') &
\	RETURN &

50	! READ &
	&
	&
	GOTO 11500 IF FLAG%(USER%) AND 2%^10% &
\	GOTO 11040 UNLESS LEN(CVT$$(AR$,255%)) &
\	A$=CVT$$(AR$,255%) &
\	GOTO 70 IF LEFT("BOARD",LEN(A$))=A$ OR LEFT("NOTICEBOARD",LEN(A$))=A$ &
\	GOTO 90 IF LEFT("PAPER",LEN(A$))=A$ OR LEFT("NEWSPAPER",LEN(A$))=A$ &
\	MESS$="There is nothing worth reading." &
\	MESS$="Use the HELP system for instructions." IF ROOM%(USER%)=1% AND &
	AR$="SIGN" &
\	CALL ADVOUT(KB%(USER%), CRLF$+MESS$) &
\	RETURN &

70	! READ NOTICEBOARD &
	&
	&
	GOTO 80 IF ROOM%(USER%)=INDEX%(I%) FOR I%=0% TO 511% &
\	GOTO 11040 &

80	CALL ADVOUT(KB%(USER%),CRLF$+BOARD$(I%)+CRLF$) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" reads the noticeboard.") &
\	RETURN &

90	! READ NEWSPAPER &
	&
	&
	GOTO 91 IF INSTR(1%,CVT$$(OB$(USER%,I%),255%),"NEWSPAPER") FOR I% &
	=0% TO 9% &
\	GOTO 11040 &

91	OB$(USER%,I%)="" &
\	OPEN "_DK0:"+ACC.RUN$+"PAPER.NWS" AS FILE #4% &
\	UNTIL 0% &
\	INPUT LINE #4%, LIN$ &
\	LIN$=CVT$$(LIN$,4%) &
\	CALL ADVOUT(KB%(USER%),LIN$) &
\	NEXT &

100	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" reads his newspaper.") &
\	RETURN &

110	! WRITE &
	&
	&
	GOTO 120 IF INSTR(1%,CVT$$(OB$(USER%,I%),255%),"CHALK") FOR I%=0% TO &
	9% &
\	GOTO 11040 &

120	GOTO 130 IF INDEX%(I%)=ROOM%(USER%) FOR I%=0% TO 511% &
\	GOTO 11040 &

130	BOARD$(I%)=CVT$$(AR$,128%)+CRLF$+BOARD$(I%) &
\	CALL ADVSHT(CVT$$(NAM$(USER%),255%)+" writes on the noticeboard.") &
\	GOTO 11020 &

140	! WIPE &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=16% &
\	GOTO 150 IF INDEX%(I%)=ROOM%(USER%) FOR I%=0% TO 511% &
\	GOTO 11040 &

150	BOARD$(I%)=STRING$(512%,0%) &
\	GOTO 11020 &

160	! EAT &
	&
	&
	GOTO 170 IF INSTR(1%,CVT$$(OB$(USER%,I%),255%),CVT$$(AR$,255%)) &
	FOR I%=0% TO 9% &
\	GOTO 11010 &

170	GOTO 180 UNLESS INSTR(1%,CVT$$(OB$(USER%,I%),255%),"FOOD") &
\	J%=5%*INT((14%+2.8*LEVEL%(USER%)^1.8)/5%) &
\	HP%(USER%)=HP%(USER%)+5%+INT(RND*J%/4%) &
\	HP%(USER%)=J% IF HP%(USER%)>J% &
\	FATIGUE%(USER%)=30% &
\	GOTO 190 &

180	HP%(USER%)=HP%(USER%)-INT(RND*5%) &
\	GOTO 190 &

190	J$=CVT$$(OB$(USER%,I%),128%) &
\	J$=LEFT(J$,INSTR(1%,J$,"$")-1%) IF INSTR(1%,J$,"$") &
\	J$=LEFT(J$,INSTR(1%,J$,"~")-1%) IF INSTR(1%,J$,"~") &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" eats "+J$) &
\	OB$(USER%,I%)="" &
\	GOTO 11020 &

200	! OPEN &
	&
	&
	S1$="Key "+NUM1$(ROOM%(USER%)) &
\	S2$="Key "+NUM1$(ROOM%(USER%)-1%) &
\	GOTO 210 IF INSTR(1%,OB$(USER%,I%),S1$) OR INSTR(1%,OB$(USER%,I%), &
	S2$) FOR I%=0% TO 9% &
\	GOTO 210 IF (INSTR(1,OBJECT$,S1$) OR INSTR(1%,OBJECT$,S2$)) AND &
	INSTR(1%,"#*",LEFT(PEOPLE$,1%))=0% &
\	GOTO 11040 &

210	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" goes through the locked door.") &
\	J$=FNCLEAR$ &
\	ROOM%(USER%)=ROOM%(USER%)+1% IF INSTR(1%,OB$(USER%,I%),S1$)<>0% OR &
	INSTR(1%,OBJECT$,S1$)<>0% &
\	ROOM%(USER%)=ROOM%(USER%)-1% IF INSTR(1%,OB$(USER%,I%),S2$)<>0% OR &
	INSTR(1%,OBJECT$,S2$)<>0% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" enters through the locked door." &
	) &
\	CALL ADVDSP &
\	RETURN &

220	! HELP &
	&
	&
	CALL ADVOUT(KB%(USER%), CRLF$+ &
	'Please use the HELP system on Beta') &
\	RETURN &

230	! ROB &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=3% AND INSTR(1%,CVT$$(DESC$,255%), &
	"SHRINE")=0% &
\	CALL ADVFND(CVT$$(AR$,128%)) &
\	I%=USER1% &
\	I%=ABS(USER1%) IF LEVEL%(USER%)>=30% &
\	GOTO 240 IF I%>0% AND I%<>USER% &
\	RETURN &

240	USER1%=I% &
\	CH%=10%+5%*(LEVEL%(USER%)-LEVEL%(USER1%)) &
\	CH%=0% IF LEVEL%(USER%)<LEVEL%(USER1%) &
\	IF INT(RND*100%)>CH% THEN &
	MESS$=CVT$$(NAM$(USER%),128%)+" tries to rob " &
\	GOTO 11050 &

250	GOTO 260 IF CVT$$(OB$(USER1%,I%),128%)<>"" FOR I%=0% TO 9% &
\	CALL ADVOUT(KB%(USER%),CRLF$+ &
	"He's not carrying anything") &
\	RETURN &

260	GOTO 270 IF CVT$$(OB$(USER%,J%),128%)="" FOR J%=0% TO 9% &
\	GOTO 11000 &

270	OB$(USER%,J%)=OB$(USER1%,I%) &
\	OB$(USER1%,I%)="" &
\	J$=OB$(USER%,J%) &
\	J$=CHR$(ASCII(J$)+32%)+RIGHT(J$,2%) &
\	J$=LEFT(J$,INSTR(1%,J$,"$")-1%) IF INSTR(1%,J$,"$") &
\	J$=LEFT(J$,INSTR(1%,J$,"~")-1%) IF INSTR(1%,J$,"~") &
\	J$=CVT$$(J$,128%) &
\	CALL ADVOUT(KB%(USER%), CRLF$+'You stole '+J$) &
\	J$=CVT$$(CRLF$+NAM$(USER%),128%)+' robs '+CVT$$(NAM$(USER1%),128%)+'.' &
\	OLD.ROOM%=ROOM%(USER1%) &
\	ROOM%(USER1%)=0% &
\	CALL ADVSHT(J$) &
\	ROOM%(USER1%)=OLD.ROOM% &
\	RETURN &

280	! RING &
	&
	&
	GOTO 11040 UNLESS INSTR(1%,CVT$$(DESC$,255%),"BELL") &
\	J$=CVT$$(NAM$(USER%),128%)+" rings the bell in room "+NUM1$( &
	ROOM%(USER%)) &
\	CALL ADVOUT(KB%(I%), CRLF$+J$) &
	IF LEVEL%(I%)>=11% &
	FOR I%=1% TO 8% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" rings the bell") &
\	GOTO 11020 &

290	! USE &
	&
	&
	J$="" &
\	J$=CVT$$(RIGHT(DESC$,INSTR(1%,DESC$,"$")+1%),255%) &
\	GOTO 11040 UNLESS LEN(J$) &
\	GOTO 11040 UNLESS LEFT(J$,1%)="U" &
\	J%=INSTR(1%,J$,":") &
\	CH%=VAL(RIGHT(J$,J%+1%)) &
\	J$=MID(J$,2%,J%-2%) &
\	GOTO 300 IF INSTR(1%,CVT$$(OB$(USER%,I%),255%),J$) FOR I%=0% TO 9% &
\	GOTO 11040 &

300	J$=FNCLEAR$ &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" uses "+OB$(USER%,I%)) &
\	ROOM%(USER%)=CH% &
\	GET #3%, RECORD ROOM%(USER%) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" enters.") &
\	CALL ADVDSP &
\	RETURN &

310	! PRAY &
	&
	&
	GOTO 11040 IF FLAG%(USER%) AND 2%^9% &
\	GOTO 11040 UNLESS INSTR(1%,CVT$$(DESC$,255%),"SHRINE") &
\	J$=CRLF$+CVT$$(NAM$(USER%),128%)+" prays in room "+NUM1$(ROOM%(USER%)) &
\	CALL ADVOUT(KB%(I%), J$) IF LEVEL%(I%)>=11% AND &
	LEVEL%(I%)<=16% AND KB%(I%)<>0% FOR I%=1% TO 8% &
\	GOTO 11020 &

320	! ANNOUNCE &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=30% &
\	J$=CVT$$(NAM$(USER%),128%)+" announces '"+CVT$$(AR$,128%)+"'" &
\	CALL ADVOUT(KB%(I%),CRLF$+J$) IF CVT$$(NAM$(I%), &
	255%)<>"" FOR I%=1% TO 8% &
\	GOTO 11020 &

330	! TELEPORT &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=11% &
\	GOTO 356 IF (ROOM%(USER%)=ROOM%(I%) AND I%<>USER% AND &
	LEVEL%(I%)<=30%) &
	FOR I%=1% TO 8% UNLESS &
	LEVEL%(USER%)>=20% &
!\	CH%=5%*LEVEL%(USER%) &
!\	IF RND*100%>CH% THEN &
!	PRINT #1%, RECORD 32767%+1%+KB%(USER%), CRLF$; &
!	"Your fingers stumble over the intricate gestures." &
!\	RETURN &

340	A$=CVT$$(AR$,255%) &
\	GOTO 11040 UNLESS LEN(A$) &
\	GOTO 350 IF LEFT(NAM$(I%),LEN(A$))=A$ FOR I%=1% TO 8% &
\	CALL ADVOUT(KB%(USER%),CRLF$+ &
	"He's temporarily vacated the universe.") &
\	RETURN &

350	GOTO 11040 UNLESS ROOM%(I%) &
\	GOTO 11040 UNLESS LEVEL%(USER%)>=LEVEL%(I%) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" vanishes in a puff of smoke.") &
\	J$=FNCLEAR$ &
\	OLD.ROOM%=ROOM%(USER%) &
\	GET #3%, RECORD ROOM%(USER%) &
\	ROOM%(USER%)=ROOM%(I%) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" appears.") &
\	CALL ADVDSP &

355	RETURN &

356	CALL ADVOUT(KB%(USER%), CRLF$+ &
	"There is someone else here !") &
\	RETURN &

360	! ROOM &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=16% &
\	GOTO 356 IF ROOM%(USER%)=ROOM%(I%) AND I%<>USER% AND &
	LEVEL%(I%)<=30% &
	FOR I%=1% TO &
	8% UNLESS LEVEL%(USER%)>=20% &
\	J%=VAL(AR$) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" vanishes in a puff of smoke.") &
\	J$=FNCLEAR$ &
\	OLD.ROOM%=ROOM%(USER%) &
\	ROOM%(USER%)=J% &
\	GET #3%, RECORD ROOM%(USER%) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" appears.") &
\	CALL ADVDSP &
\	RETURN &

365	ROOM%(USER%)=OLD.ROOM% &
\	GET #3%, RECORD ROOM%(USER%) &
\	GOTO 11040 &

370	! LIST &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=11% &
\	CALL ADVOUT(KB%(USER%),'') &
\	CALL ADVOUT(KB%(USER%), CVT$$(NAM$(I%),128%)+" on KB"+ &
	NUM1$(KB%(I%))+" in "+ACC$(I%)) IF CVT$$(NAM$(I%),255%)<>"" &
	AND LEVEL%(I%)<=LEVEL%(USER%) &
	FOR &
	I%=1% TO 8% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+"'s eyes grow misty as "+ &
	'they scan the '+CRLF$+'far reaches of the dungeon.') &
\	RETURN &

380	! INVISIBLE &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=16% &
\	MESS$=CRLF$+"You are now " &
\	MESS$=MESS$+"in" UNLESS FLAG%(USER%) AND 2% &
\	MESS$=MESS$+"visible" &
\	CALL ADVOUT(KB%(USER%),MESS$) &
\	MESS$=CVT$$(NAM$(USER%),128%) &
\	MESS$=MESS$+" vanishes in a puff of smoke." UNLESS FLAG%(USER%) AND 2% &
\	MESS$=MESS$+" appears." IF FLAG%(USER%) AND 2% &
\	CALL ADVSHT(CRLF$+MESS$) &
\	FLAG%(USER%)=FLAG%(USER%) XOR 2% &
\	RETURN &

390	! HEAL &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=6% &
\	GOTO 11060 IF FLAG%(USER%) AND 2%^4% &
\	AR$=LEFT(NAM$(USER%),10%) UNLESS LEN(CVT$$(AR$,255%)) &
\	A$=CVT$$(AR$,255%) &
\	GOTO 400 IF LEFT(NAM$(I%),LEN(A$))=A$ AND I%<>USER% AND &
	ROOM%(USER%)=ROOM%(I%) FOR I%=1% TO 8% &
\	I%=USER% &
\	GOTO 400 IF LEFT(NAM$(USER%),LEN(A$))=A$ &
\	GOTO 11030 &

400	FLAG%(USER%)=FLAG%(USER%) OR 2%^4% UNLESS LEVEL%(USER%)>=30% &
\	J%=5%*INT((14%+2.8*LEVEL%(I%)^1.8)/5%) &
\	JJ%=5%*INT((14%+2.8*LEVEL%(USER%)^1.8)/5%) &
\	JJ%=JJ%/3% &
\	N%=INT(RND*JJ%)+1% &
\	HP%(I%)=HP%(I%)+N% &
\	HP%(I%)=J% IF HP%(I%)>J% &
\	FATIGUE%(I%)=50% &
\	CALL ADVOUT(KB%(I%), CRLF$+"You now have "+ &
	NUM1$(HP%(I%))+" hit points.") &
\	GOTO 11020 IF LEFT(NAM$(USER%),LEN(A$))<>A$ &
\	RETURN &

410	! FIREBALL &
	&
	&
	GOTO 11060 IF FLAG%(USER%) AND 2%^5% &
\	GOTO 11040 UNLESS LEVEL%(USER%)>=20% &
\	GOTO 11040 IF CVT$$(AR$,255%)="" &
\	J$=LEFT(AR$,1%) &
\	GOTO 11040 UNLESS INSTR(1%,EX$,J$) &
\	J%=CVT$%(RIGHT(EX$,INSTR(1%,EX$,J$)+1%)) &
\	OLD.ROOM%=ROOM%(USER%) &
\	ROOM%(USER%)=J% &
\	GET #3%, RECORD J% &
\	LSET OBJECT$="" &
\	JJ%=INSTR(1%,'#*',LEFT(PEOPLE$,1%)) &
\	R%=FNGMD%(ROOM%(USER%)) IF JJ% &
\	N%=LEVEL%(USER%)*5%+INT(RND*LEVEL%(USER%)*10%) &
\	MON.HP%(MON%)=MON.HP%(MON%)-N% IF JJ% &
\	LSET PEOPLE$='!'+MID(PEOPLE$,2%,INSTR(1%,PEOPLE$,'/')-2%)+ &
	"'s corpse"+RIGHT(PEOPLE$,INSTR(1%,PEOPLE$,'/')) &
	IF MON.HP%(MON%)<=0% IF JJ% &
\	PUT #3%, RECORD J% &
\	GET #3%, RECORD J% &
\	HP%(I%)=HP%(I%)-N% IF ROOM%(I%)=ROOM%(USER%) AND I%<>USER% FOR I%=1% TO 8% &
\	CALL ADVSHT(CRLF$+"A large fireball appears in the middle of the room "+ &
	"and explodes.") &
\	CALL ADVOUT(KB%(I%),CRLF$+ &
	'A large fireball appears in the middle of the room and explodes.') &
	IF ROOM%(I%)=J% AND I%<>USER% FOR I%=1% TO 8% &
\	ROOM%(USER%)=OLD.ROOM% &
\	GET #3%, RECORD ROOM%(USER%) &
\	GOTO 11020 &

420	! STUN &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=13% &
\	A$=CVT$$(AR$,255%) &
\	GOTO 11040 UNLESS LEN(A$) &
\	GOTO 430 IF LEFT(NAM$(I%),LEN(A$))=A$ AND (ROOM%(I%)=ROOM%(USER%) &
	OR LEVEL%(USER%)>=30%) &
	FOR I%=1% TO 8% &
\	GOTO 11030 &

430	GOTO 11040 UNLESS LEVEL%(USER%)>LEVEL%(I%) &
\	I%=USER% IF LEVEL%(USER%)=13% AND RND<0.1 &
\	J%=20%-LEVEL%(I%) &
\	J%=5% IF J%<=5% &
\	STUN(I%)=TIME(0%)+J%*NO.OF.USERS% &
\	GOTO 435 IF I%=USER% &
\	USER1%=I% &
\	MESS$=CVT$$(NAM$(USER%),128%)+" stuns " &
\	MESS1$="" &
\	GOTO 11070 &

435	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+"'s stun backfires !") &
\	CALL ADVOUT(KB%(USER%),"It backfires !") &
\	RETURN &

440	! ZAP &
	&
	&
	GOTO 11040 UNLESS CVT$$(NAM$(USER%),255%)="GOD" &
\	A$=CVT$$(AR$,255%) &
\	GOTO 450 IF LEFT(NAM$(I%),LEN(A$))=A$ FOR I%=1% TO 8% &
\	GOTO 11030 &

450	HP%(I%)=-1000% &
\	USER1%=I% &
\	MESS$="GOD throws a thunderbolt at " &
\	MESS1$="" &
\	GOTO 11070 &

460	! SEND &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=30% &
\	A$=LEFT(AR$,INSTR(1%,AR$," ")-1%) &
\	WHERE%=VAL(RIGHT(AR$,LEN(A$)+2%)) &
\	GOTO 470 IF LEFT(NAM$(I%),LEN(A$))=A$ FOR I%=1% TO 8% &
\	GOTO 11040 &

470	OLD.USER%=USER% &
\	USER%=I% &
\	GET #3%, RECORD ROOM%(USER%) &
\	J$=FNCLEAR$ &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" vanishes in a puff of smoke.") &
\	ROOM%(USER%)=WHERE% &
\	CALL ADVDSP &
\	CALL ADVSHT(CVT$$(NAM$(USER%),128%)+" appears.") &
\	PRINT #1%, RECORD 32767%+1%+KB%(USER%), CRLF$;"> "; &
\	USER%=OLD.USER% &
\	GET #3%, RECORD ROOM%(USER%) &
\	GOTO 11020 &

480	! PASS &
	&
	&
	J$=LEFT(AR$,INSTR(1%,AR$," ")-1%) &
\	GOTO 11040 UNLESS LEN(J$) &
\	IF CVT$$(PASS$(USER%),255%)<>CVT$$(J$,255%) THEN &
	PRINT #1%, RECORD 32767%+1%+KB%(USER%), CHR$(24%); &
	"Old password wrong." &
\	RETURN &

490	J$=CVT$$(RIGHT(AR$,LEN(J$)+2%),255%) &
\	IF LEN(J$)>10% OR LEN(J$)=0% THEN &
	PRINT #1%, RECORD 32767%+1%+KB%(USER%), CHR$(24%); &
	"New password too long or null in length." &
\	RETURN &

495	J%=0% &
\	J%=-1% IF ASCII(RIGHT(J$,I%))<32% FOR I%=1% TO LEN(J$) &
\	IF J% THEN &
	PRINT #1%, RECORD 32767%+1%+KB%(USER%), CRLF$; &
	'No control characters allowed.' &
\	RETURN &

500	PASS$(USER%)=J$ &
\	PRINT #1%, RECORD 32767%+1%+KB%(USER%), CHR$(24%) &
\	GOTO 11020 &

510	! HOLD &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=16% &
\	J$=CVT$$(AR$,255%) &
\	GOTO 520 IF LEFT(NAM$(I%),LEN(J$))=J$ FOR I%=1% TO 8% &
\	GOTO 11030 &

520	FLAG%(I%)=FLAG%(I%) XOR 2%^8% &
\	GOTO 11020 &

530	! UNSTUN &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=13% &
\	J$=CVT$$(AR$,255%) &
\	GOTO 540 IF LEFT(NAM$(I%),LEN(J$))=J$ FOR I%=1% TO 8% &
\	GOTO 11030 &

540	STUN(I%)=0% &
\	USER1%=I% &
\	MESS$=CVT$$(NAM$(USER%),128%)+" unstuns " &
\	MESS1$="" &
\	GOTO 11070 &

550	! SUMMON &
	&
	&
	J$=CVT$$(AR$,255%) &
\	GOTO 560 IF LEFT(NAM$(I%),LEN(J$))=J$ FOR I%=1% TO 8% &
\	CALL ADVOUT(KB%(USER%), CRLF$+"He's not playing.") &
\	RETURN &

560	CH%=7%*(LEVEL%(I%)-LEVEL%(USER%)) &
\	CH%=0% IF LEVEL%(I%)<=30% &
\	CH%=100% IF LEVEL%(USER%)>=30% &
\	IF INT(RND*100%)>CH% THEN &
	HP%(USER%)=HP%(USER%)/2% &
\	CALL ADVOUT(KB%(USER%), CRLF$+"You fail.") &
\	RETURN &

570	OLD.USER%=USER% &
\	USER%=I% &
\	GET #3%, RECORD ROOM%(USER%) &
\	J$=FNCLEAR$ &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" vanishes in a puff of smoke.") &
\	ROOM%(USER%)=ROOM%(OLD.USER%) &
\	GET #3%, RECORD ROOM%(USER%) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" appears.") &
\	CALL ADVDSP &
\	J$=CVT$$(NAM$(OLD.USER%),128%)+' has summoned you.' &
\	J$='You have been summoned' IF LEVEL%(USER%)<LEVEL%(OLD.USER%) OR &
	FLAG%(OLD.USER%) AND 2% &
\	CALL ADVOUT(KB%(USER%),CRLF$+J$) &
\	USER%=OLD.USER% &
\	GET #3%, RECORD ROOM%(USER%) &
\	GOTO 11020 &

11000	MESS$="You can't carry any more." &
\	GOTO 11100 &

11010	MESS$="You aren't carrying it." &
\	GOTO 11100 &

11020	MESS$="Ok." &
\	GOTO 11100 &

11030	MESS$="I can't see him." &
\	GOTO 11100 &

11040	MESS$="You can't." &
\	GOTO 11100 &

11050	CALL ADVOUT(KB%(USER%),CRLF$+"You fail.") &
\	CALL ADVOUT(KB%(USER1%),CRLF$+MESS$+'you.') &
\	JJ%=ROOM%(USER1%) &
\	ROOM%(USER1%)=0% &
\	CALL ADVSHT(CRLF$+MESS$+CVT$$(NAM$(USER1%),128%)) &
\	ROOM%(USER1%)=JJ% &
\	RETURN &

11060	MESS$="You already have." &
\	GOTO 11100 &

11070	CALL ADVOUT(KB%(USER1%),CRLF$+MESS$+'you'+MESS1$) &
\	JJ%=ROOM%(USER1%) &
\	ROOM%(USER1%)=0% &
\	CALL ADVSHT(CRLF$+MESS$+CVT$$(NAM$(USER1%),128%)+MESS1$) &
\	ROOM%(USER1%)=JJ% &
\	RETURN &

11100	CALL ADVOUT(KB%(USER%),CRLF$+MESS$) &
\	RETURN &

11500	CALL ADVOUT(KB%(USER%),CRLF$+"You can't see.") &
\	RETURN &

15000	! FUNCTIONS &
	&
	&

15110	DEF* FNCLEAR$ &
\	IF FLAG%(USER%) AND 1% THEN &
	HP%(USER%)=HP%(USER%)*3%/4% &
\	FLAG%(USER%)=FLAG%(USER%)-1% &

15120	FNEND &

15130	DEF* FNSHOUT$(MMESS$) &
\	PRINT #1%, RECORD 32767%+1%+KB%(JJJ%), CRLF$;MMESS$ IF ROOM%(JJJ%)= &
	ROOM%(USER%) AND JJJ%<>USER% &
	FOR JJJ%=1% TO 8% UNLESS FLAG%(USER%) AND 2% &
\	FNEND &

15140	DEF* FNGMD%(R%) &
\	GOTO 15150 IF MON.ROOM%(MON%)=R% FOR MON%=0% TO 10% &
\	MON%=-1% &
\	MON%=I% IF MON.ROOM%(I%)=0% FOR I%=0% TO 10% &
\	MON%=INT(RND*10%) IF MON%=-1% &
\	JJJJJJ%=FNSMD%(MON%) IF MON.ROOM%(MON%) &
\	J$=MONSTER$(R%) &
\	ATT.LEVEL%(MON%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	DEF.LEVEL%(MON%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	MON.HP%(MON%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	MON.DAM%(MON%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	SPEC$(MON%)=LEFT(J$,6%) &
\	J$=RIGHT(J$,7%) &
\	MON.XP%(MON%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	MON.FLAG%(MON%)=CVT$%(J$) &
\	MON.ROOM%(MON%)=R% &

15150	FNEND &

15160	DEF* FNSMD%(R%) &
\	J$=CVT%$(ATT.LEVEL%(R%))+CVT%$(DEF.LEVEL%(R%))+CVT%$(MON.HP%(R%))+ &
	CVT%$(MON.DAM%(R%))+ &
	SPEC$(R%)+CVT%$(MON.XP%(R%))+CVT%$(MON.FLAG%(R%))+CVT%$(0%) &
\	MONSTER$(MON.ROOM%(R%))=J$ &
\	MON.ROOM%(R%)=0% &
\	FNEND &

25000	! ERRORS &
	&
	&
	RESUME 100 IF ERL=91 AND ERR=11 &
\	RESUME 365 IF ERL=360 &
\	RESUME 11040 IF ERL=360 &
\	RESUME 11040 IF ERL=460 &
\	IF ERR=11% AND ERL=350 THEN ROOM%(USER%)=OLD.ROOM% &
\	GET #3%, RECORD ROOM%(USER%) &
\	RESUME 355 &

29000	OPEN "MSG:LOG1.95" AS FILE #4%, MODE 2% &
\	PRINT #4%, "ADVCMD : ";RIGHT(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERR)),3%); &
	" at line";ERL;" at ";TIME$(0%) &
\	PRINT #4%, "User was ";NAM$(USER%);" in room";ROOM%(USER%) IF &
		(USER%>0% AND USER%<=8%) &
\	CLOSE #4% &
\	RESUME 32767 &

32767	SUBEND &

