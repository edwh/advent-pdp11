	.title	hexbin
	$rsx
	$rwdef
;
;	Converts BINHEX.TMP from hex into binary (output file specified).
;	Don't forget to set the protection and RTS.
;
;	Compilation : MACR HEXBIN=PA:DEFS,SY:HEXBIN
;		      TKB HEXBIN=HEXBIN,PA:IOLIB/LB
;
;
;	Edward D.W. Hibbert August 1987
;

start:	.print	#prompt
	.input	#fil
	mov	by$cnt,xrb
	mov	by$cnt,xrb+2
	mov	#fil,xrb+4
	clr	xrb+10
	clr	xrb+12
	calls	$zapfqb
	.fss
	tstb	firqb
	bne	start
	movb	#crefq,firqb+fqfun
	movb	#4,firqb+fqfil
	calfip
	calls	$zapfqb
	mov	#^RBIN,firqb+fqnam1
	mov	#^RHEX,firqb+fqnam1+2
	mov	#^RTMP,firqb+fqext
	movb	#opnfq,firqb+fqfun
	movb	#2,firqb+fqfil
	calfip
	tstb	firqb
	beq	1$
	movb	firqb,r0
	.wrdec	r0
	.exit

1$:	mov	#1024.,xrb
	clr	xrb+2
	mov	#buf1,xrb+4
	mov	#2,xrb+6
	clr	xrb+xrblk
	clr	xrb+xrtime
	clr	xrb+xrmod
	.read
	tstb	firqb
	beq	5$
	inc	err

5$:	mov	#buf1,r0
	mov	#buf2,r2

2$:	movb	(r0)+,r4
	cmp	r4,#'9
	bgt	3$
	sub	#'0,r4
	br	4$

3$:	sub	#'A,r4
	add	#10.,r4

4$:	mov	r4,r5
	asl	r5
	asl	r5
	asl	r5
	asl	r5
	movb	(r0)+,r4
	cmp	r4,#'9
	bgt	6$
	sub	#'0,r4
	br	7$

6$:	sub	#'A,r4
	add	#10.,r4

7$:	add	r4,r5
	movb	r5,(r2)+
	cmp	r0,#buf1+1024.
	blt	2$

8$:	mov	#512.,xrb
	mov	#512.,xrb+2
	mov	#buf2,xrb+4
	mov	#4,xrb+xrci
	clr	xrb+xrblk
	clr	xrb+xrmod
	.write
	tst	err
	bne	10$
	br	1$

10$:	movb	#2,firqb+fqfil
	movb	#clsfq,firqb+fqfun
	calfip
	movb	#4,firqb+fqfil
	movb	#clsfq,firqb+fqfun
	calfip
	.exit

prompt:	.asciz	/Output file >> /
	.even
buf1:	.blkb	1024.
buf2:	.blkb	512.
err:	.word	0
fil:	.blkb	30.

	.end	start

