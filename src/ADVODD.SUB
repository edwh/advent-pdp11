1	SUB ADVODD &
\	COMMON ACC$(8%)=7%,AR$=80%,C%,C$=10%,CRLF$=2%, &
	FLAG%(8%),HP%(8%),LEVEL%(8%),KB%(8%),NAM$(8%)=30%,NO.OF.USERS%, &
	XP.(8%),STUN(8%), &
	NPCMESS$(8%)=40%,OB$(8%,9%)=30%,PASS$(8%)=10%, &
	ROOM%(8%),SENT.KB%,SENT.MESS$=80%,SENT.PROG%,SENT.PROJ%, &
	USER%,CI$=1%,WEAPON$(8%)=20%,WEAPON%(8%), &
	ATT.LEVEL%(10%),DEF.LEVEL%(10%),MON.HP%(10%),MON.DAM%(10%), &
	SPEC$(10%)=6%,MON.ROOM%(10%),MON.XP%(10%),RUN.ACC$=9%,MMM$=128%, &
	BLIND%(8%),USER1%,SPY%(10%),FATIGUE%(10%),SINGLE.USER% &
\	FIELD #3%, 1% AS ROOM$, 16% AS EX$, 83% AS PEOPLE$, 100% AS OBJECT$, &
	312% AS DESC$ &
\	DIM #5%, MONSTER$(10000%)=20% &
\	DIM #7%, CHARACTER$(100%)=512% &
\	ON ERROR GOTO 25000 &

10	ON C%-44% GOSUB 20,50,60,80,90,100,120,140,160,180,210,250,260 &
\	SUBEXIT &

20	! KISS &
	&
	&
	WHO$=CVT$$(AR$,255%) &
\	GOTO 11040 UNLESS LEN(WHO$) &
\	CALL ADVFND(WHO$) &
\	GOTO 30 IF USER1%>0% &
\	RETURN &

30	IF USER%=USER1% THEN &
	CALL ADVOUT(KB%(USER%),CRLF$+"Don't be Narcissistic.") &
\	RETURN &

40	HP%(USER1%)=-10000% IF HP%(USER1%)<=0% &
\	HP%(USER1%)=HP%(USER1%)+(-1%)^INT(RND*2%) &
\	OLD.ROOM%=ROOM%(USER1%) &
\	ROOM%(USER1%)=0% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' kisses '+ &
	CVT$$(NAM$(USER1%),128%)) &
\	ROOM%(USER1%)=OLD.ROOM% &
\	CALL ADVOUT(KB%(USER1%),CRLF$+CVT$$(NAM$(USER%),128%)+ &
	' kisses you.') &
\	GOTO 11020 &

50	! COMMUNE &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=11% &
\	J$=CRLF$+CVT$$(NAM$(USER%),128%)+" says '"+CVT$$(AR$,128%)+"'." &
\	CALL ADVOUT(KB%(I%),J$) IF ((LEVEL%(I%)>=11% AND &
	LEVEL%(I%)<=LEVEL%(USER%)) OR LEVEL%(I%)=13%) AND &
	I%<>USER% &
	AND CVT$$(NAM$(USER%),255%)<>"" &
	FOR I%=1% &
	TO 8% &
\	GOTO 11020 &

60	! BLESS &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=11% AND (INSTR(1%,CVT$$(DESC$,255%), &
	"SHRINE")<>0% OR INSTR(1%,CVT$$(DESC$,255%),"TEMPLE")<>0%) &
\	CALL ADVFND(CVT$$(AR$,255%)) &
\	I%=USER1% &
\	GOTO 70 IF I%>0% &
\	GOTO 11040 &

70	J%=5%*INT((14%+2.8*LEVEL%(USER%)^1.8)/5%) &
\	JJ%=5%*INT((14%+2.8*LEVEL%(I%)^1.8)/5%) &
\	J%=JJ% IF J%>JJ% &
\	GOTO 11040 IF HP%(I%)>=J% &
\	HP%(I%)=J% &
\	FATIGUE%(I%)=50% &
\	CALL ADVOUT(KB%(I%),CRLF$+CVT$$(NAM$(USER%),128%)+' blesses you.') &
\	GOTO 11020 &

80	! CREATE &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)<=30% AND LEVEL%(USER%)>=11% &
\	J$=SYS(CHR$(6%)+CHR$(22%)+CHR$(-1%)+CHR$(0%)+"ADVENT"+STRING$(10%,0%) &
	+"CREATE "+CVT$$(AR$,128%)) &
\	CALL ADVOUT(KB%(USER%),CRLF$+'Allow a few seconds (not minutes!) for'+ &
	' the character to be created.') &
\	RETURN &

90	! MAKE &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=16% &
\	J1$="" &
\	J$=CVT$$(AR$,128%) &
\	FOR I%=2% TO LEN(J$) &
\	JJ%=ASCII(MID(J$,I%,1%)) &
\	JJ%=JJ%+32% IF JJ%<91% AND JJ%>64% &
\	J1$=J1$+CHR$(JJ%) &
\	NEXT I% &
\	J1$=LEFT(J$,1%)+J1$ &
\	LSET OBJECT$=J1$+"/"+OBJECT$ &
\	PUT #3%, RECORD ROOM%(USER%) &
\	GET #3%, RECORD ROOM%(USER%) &
\	J1$=LEFT(J1$,INSTR(1%,J1$,'~')-1%) IF INSTR(1%,J1$,'~') &
\	J1$=LEFT(J1$,INSTR(1%,J1$,'$')-1%) IF INSTR(1%,J1$,'$') &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' conjures '+J1$+ &
	' out of thin air.') &
\	GOTO 11020 &

100	! SPY &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=11% &
\	GOTO 117 IF LEVEL%(USER%)<30% &
\	J$=CVT$$(AR$,255%) &
\	GOTO 115 IF AR$='OFF' &
\	J$=RIGHT(J$,3%) &
\	CALL ADVFND(J$) &
\	I%=ABS(USER1%) &
\	GOTO 110 IF I% &
\	RETURN &

110	GOTO 11040 UNLESS LEVEL%(USER%)>LEVEL%(I%) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' stares at the ground in '+ &
	'deep concentration.') &
\	SPY%(KB%(I%))=KB%(USER%) &
\	GOTO 11020 &

115	GOTO 116 IF SPY%(I%)=KB%(USER%) FOR I%=1% TO 10% &
\	GOTO 11020 &

116	SPY%(I%)=0% &
\	GOTO 11020 &

117	OLD.USER%=USER% &
\	CALL ADVFND(CVT$$(AR$,255%)) &
\	USER1%=ABS(USER1%) &
\	RETURN UNLESS USER1% &
\	OLD.KB%=KB%(USER1%) &
\	USER%=USER1% &
\	KB%(USER%)=KB%(OLD.USER%) &
\	GET #3%, RECORD ROOM%(USER%) &
\	CALL ADVDSP &
\	KB%(USER%)=OLD.KB% &
\	USER%=OLD.USER% &
\	GET #3%, RECORD ROOM%(USER%) &
\	GOTO 11020 &

120	! BELLOW &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=4% &
\	AR$=XLATE(AR$,MMM$) IF FLAG%(USER%) AND 2%^9% &
\	FLAG%(USER%)=FLAG%(USER%) XOR 2%^9% IF RND*100%<=LEVEL%(USER%)*10%+10% IF &
	FLAG%(USER%) AND 2%^9% &
\	FOR JJ%=1% TO 4% &
\	J%=INSTR(1%,EX$,MID('NESW',JJ%,1%)) &
\	GOSUB 130 IF J% &
\	NEXT JJ% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" bellows '"+CVT$$(AR$,128%)+"'.") &
\	GOTO 11020 &

130	CALL ADVOUT(KB%(I%),CRLF$+ &
	"A voice bellows '"+CVT$$(AR$,128%)+"'.") IF ROOM%(I%)=CVT$%( &
	MID(EX$,J%+1%,2%)) AND ROOM%(I%)<>0% FOR I%=1% TO 8% &
\	RETURN &

140	! VALUE &
	&
	&
	GOTO 11040 UNLESS LEN(CVT$$(AR$,255%))>0% AND INSTR(1%,AR$,'~')=0% &
\	GOTO 150 IF INSTR(1%,CVT$$(OB$(USER%,I%),255%), &
	CVT$$(AR$,255%)) FOR I%=0% TO 9% &
\	GOTO 11010 &

150	CH%=150%-27.5*LEVEL%(USER%)+5/4*LEVEL%(USER%)*LEVEL%(USER%) &
\	CH%=0% IF LEVEL%(USER%)>=10% &
\	J%=RND*CH%+1% &
\	J%=-J% IF RND<0.5 &
\	JJ%=RND*10%+1% &
\	JJ%=VAL(RIGHT(OB$(USER%,I%),INSTR(1%,OB$(USER%,I%),'~')+1%)) &
	IF INSTR(1%,OB$(USER%,I%),'~') &
\	JJ%=(100.0+J%)*(1.0*JJ%)/100.0 &
\	JJ%=-JJ% IF JJ%<0% &
\	CALL ADVOUT(KB%(USER%), CRLF$+"You reckon that it's worth about "+ &
	NUM1$(JJ%)+' XP.') &
\	J$=OB$(USER%,I%) &
\	J$=LEFT(J$,INSTR(1%,J$,'$')-1%) IF INSTR(1%,J$,'$') &
\	J$=LEFT(J$,INSTR(1%,J$,'~')-1%) IF INSTR(1%,J$,'~') &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' eyes '+J$+ &
	' with a greedy look.') &
\	RETURN &

160	! CALL &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=13% &
\	FIELD #1%, 128% AS BUF$ &
\	J$=RIGHT(CVT$$(BUF$,1%+4%+8%+128%),2%) &
\	J%=INSTR(1%,J$,' ') &
\	GOTO 11040 UNLESS J% &
\	J$=RIGHT(J$,J%+1%) &
\	J%=INSTR(1%,J$,' ') &
\	GOTO 11040 UNLESS J% &
\	WHO$=CVT$$(LEFT(J$,J%-1%),255%) &
\	WHAT$=RIGHT(J$,J%) &
\	CALL ADVFND(WHO$) &
\	I%=ABS(USER1%) &
\	GOTO 170 IF I% &
\	GOTO 11040 &

170	GOTO 11040 UNLESS LEVEL%(I%)<=6% UNLESS LEVEL%(USER%)>=30% &
\	WHAT$=LEFT(NAM$(I%),10%)+WHAT$ &
\	CALL ADVOUT(KB%(I%), CRLF$+CVT$$(NAM$(USER%),128%)+' turns you into '+ &
	WHAT$) &
\	J$=CVT$$(NAM$(USER%),128%)+' turns '+CVT$$(NAM$(I%),128%)+ &
	' into '+WHAT$ &
\	NAM$(I%)=WHAT$ &
\	OLD.ROOM%=ROOM%(I%) &
\	ROOM%(I%)=0% &
\	CALL ADVSHT(CRLF$+J$) &
\	ROOM%(I%)=OLD.ROOM% &
\	GOTO 11020 &

180	! GAG &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=9% &
\	J$=CVT$$(AR$,255%) &
\	IF LEN(J$)=0% THEN &
	CALL ADVOUT(KB%(USER%),CRLF$+'You choke.') &
\	CALL ADVSHT(CVT$$(NAM$(USER%),128%)+' chokes.') &
\	RETURN &

190	CALL ADVFND(J$) &
\	I%=USER1% &
\	I%=ABS(USER1%) IF LEVEL%(USER%)>=30% &
\	GOTO 200 IF I%>0% &
\	GOTO 11030 &

200	GOTO 11040 UNLESS LEVEL%(USER%)>LEVEL%(I%) &
\	FLAG%(I%)=FLAG%(I%) XOR 2%^9% &
\	GOTO 11020 IF FLAG%(USER%) AND 2% &
\	J$=CRLF$+CVT$$(NAM$(USER%),128%)+' traces runes in the air and ' &
\	JJ$='you feel a strange torpor'+CRLF$+'creep over your tongue.' &
\	JJ$='your tongue becomes free again.' UNLESS FLAG%(I%) AND 2%^9% &
\	CALL ADVOUT(KB%(I%),J$+JJ$) &
\	OLD.ROOM%=ROOM%(I%) &
\	ROOM%(I%)=0% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+ &
	' traces strange runes in the air.') &
\	ROOM%(I%)=OLD.ROOM% &
\	GOTO 11020 &

210	! BLIND &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=8% &
\	J$=CVT$$(AR$,255%) &
\	IF LEN(J$)=0% THEN &
	CALL ADVOUT(KB%(USER%),CRLF$+ &
	'You waste your powers fruitlessly.') &
\	HP%(USER%)=HP%(USER%)-50% &
\	RETURN &

220	CALL ADVFND(J$) &
\	I%=USER1% &
\	I%=ABS(I%) IF LEVEL%(USER%)>=30% &
\	GOTO 230 IF I%>0% &
\	GOTO 11040 &

230	IF LEVEL%(I%)>LEVEL%(USER%) THEN &
	HP%(USER%)=HP%(USER%)-50% &
\	CALL ADVOUT(KB%(USER%),CRLF$+ &
	'He is too powerful for you.') &
\	RETURN &

235	IF FLAG%(I%) AND 2%^10% THEN &
	CALL ADVOUT(KB%(USER%),CRLF$+ &
	'He already is.') &
\	RETURN &

240	FLAG%(I%)=FLAG%(I%)+2%^10% &
\	BLIND%(I%)=(LEVEL%(USER%)-LEVEL%(I%))/2%+1% &
\	OLD.ROOM%=ROOM%(I%) &
\	ROOM%(I%)=0% &
\	CALL ADVSHT(CVT$$(NAM$(USER%),128%)+' waves his hands and '+ &
	CVT$$(NAM$(I%),128%)+' rubs his eyes.') &
\	ROOM%(I%)=OLD.ROOM% &
\	CALL ADVOUT(KB%(I%), CHR$(24%)+'Everything goes black.') &
\	HP%(USER%)=HP%(USER%)-50% &
\	GOTO 11020 &

250	! MESSAGE &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=30% &
\	J$=SYS(CHR$(6%)+CHR$(22%)+CHR$(-1%)+CHR$(0%)+'ADVENT'+STRING$(10%,0%)+ &
	AR$) &
\	RETURN &

260	! REST &
	&
	&
	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' sits down in a corner'+ &
	' and closes his eyes.') &
\	FLAG%(USER%)=FLAG%(USER%) OR 2%^11% &
\	CALL ADVOUT(KB%(USER%),CRLF$+'You sit down and close your eyes.') &
\	RETURN &

11000	MESS$="You can't carry any more." &
\	GOTO 11100 &

11010	MESS$="You aren't carrying it." &
\	GOTO 11100 &

11020	MESS$="Ok." &
\	GOTO 11100 &

11030	MESS$="I can't see him." &
\	GOTO 11100 &

11040	MESS$="You can't." &
\	GOTO 11100 &

11060	MESS$="You already have." &
\	GOTO 11100 &

11100	CALL ADVOUT(KB%(USER%),CRLF$+MESS$) &
\	RETURN &

15000	! FUNCTIONS &
	&
	&

25000	! ERRORS &
	&
	&

29000	OPEN "MSG:LOG1.95" AS FILE #4%, MODE 2% &
\	PRINT #4%, "ADVODD : ";RIGHT(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERR)),3%); &
	" at line";ERL;" at ";TIME$(0%) &
\	PRINT #4%, "User was ";NAM$(USER%);" in room";ROOM%(USER%) IF &
	(USER%>0% AND USER%<=8%) &
\	CLOSE #4% &
\	RESUME 32767 &

32767	SUBEND &

