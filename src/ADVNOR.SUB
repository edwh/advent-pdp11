1	SUB ADVNOR &
\	COMMON C%,NO.OF.USERS%,SENT.KB%,SENT.PROG%,SENT.PROJ%,USER%,USER1%,SINGLE.USER%, &
	FLAG%(8%),HP%(8%),LEVEL%(8%),KB%(8%),ROOM%(8%),WEAPON%(8%), &
	ATT.LEVEL%(10%),DEF.LEVEL%(10%),MON.HP%(10%),MON.DAM%(10%), &
	MON.ROOM%(10%),MON.XP%(10%),BLIND%(8%),SPY%(10%),FATIGUE%(10%), &
	XP.(8%),STUN(8%), &
	AR$=80%,C$=10%,CRLF$=2%,SENT.MESS$=80%,CI$=1%,RUN.ACC$=9%,MMM$=128%, &
	ACC$(8%)=7%,NAM$(8%)=30%,NPCMESS$(8%)=40%,OB$(8%,9%)=30%,PASS$(8%)=10%, &
	WEAPON$(8%)=20%,SPEC$(10%)=6% &
\	FIELD #3%, 1% AS ROOM$, 16% AS EX$, 83% AS PEOPLE$, 100% AS OBJECT$, &
	312% AS DESC$ &
\	DIM #5%, MONSTER$(10000%)=20% &
\	DIM #7%, CHARACTER$(100%)=512% &
\	ON ERROR GOTO 25000 &

10	ON C% GOSUB 20,30,30,30,30,80,130,90,90,90,140,140,180,200,210,220, &
	230,330,330 &
\	SUBEXIT &

20	! LOOK &
	&
	&
	CALL ADVDSP &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' looks around.') &
\	RETURN &

30	! MOVE &
	&
	&
	OLD.ROOM%=ROOM%(USER%) &
\	FL%=0% &
\	GOTO 35 IF MID(EX$,PO%,1%)=LEFT(C$,1%) FOR PO%=1% TO 16% STEP 4% &
\	CALL ADVOUT(KB%(USER%),CRLF$+'You cannot go in that direction.') &
\	RETURN &

35	IF FLAG%(USER%) AND 2%^8% THEN CALL ADVOUT(KB%(USER%),CRLF$+ &
	"You are held in place by a strong force.") &
\	RETURN &

40	J$=" goes " &
\	J$=" staggers " IF HP%(USER%)<0.5*INT((14%+2.8*LEVEL%(USER%)^1.8)/5%) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+J$+CVT$$(C$,128%)) &
\	J$=FNCLEAR$ &
\	FATIGUE%(USER%)=FATIGUE%(USER%)-3% &
\	NEW.ROOM%=CVT$%(MID(EX$,PO%+1%,2%)) &
\	GOTO 70 UNLESS NEW.ROOM% &
\	GOTO 60 UNLESS LEFT(PEOPLE$,1%)="*" &
\	NAM$=LEFT(PEOPLE$,INSTR(1%,PEOPLE$,"/")-1%) &
\	J%=FNGMD%(ROOM%(USER%)) &
\	GOTO 50 IF MON.FLAG%(MON%) AND 1% &
\	J%=INSTR(1%,SPEC$(MON%),"/FO") &
\	CH%=VAL(MID(SPEC$(MON%),4%,2%)) IF J% &
\	CH%=20% UNLESS J% &
\	GOTO 60 UNLESS RND*100%<=CH% &

50	GOTO 55 IF INSTR(1%,OBJECT$,"$") OR INSTR(1%,OBJECT$,"~") UNLESS &
	CH%/2.0<>CH%/2% &
\	GOTO 55 IF FLAG%(USER%) AND 2% &
\	GET #3%, RECORD NEW.ROOM% &
\	GOTO 51 UNLESS CHR$(NEW.ROOM%)=ROOM$ &
\	GOTO 55 IF INSTR(1%,"#*",LEFT(PEOPLE$,1%)) &
\	SEND.NAM$=RIGHT(NAM$,2%) &
\	SEND.NAM$="The"+RIGHT(NAM$,2%) IF LEFT(NAM$,2%)="A " &
\	OLD.ROOM%=ROOM%(USER%) &
\	ROOM%(USER%)=NEW.ROOM% &
\	CALL ADVSHT(CRLF$+SEND.NAM$+' enters.') &
\	FL%=-1% &
\	ROOM%(USER%)=OLD.ROOM% &
\	LSET PEOPLE$=NAM$+"/"+PEOPLE$ &
\	MON.ROOM%(MON%)=NEW.ROOM% &
\	PUT #3%, RECORD NEW.ROOM% &
\	GET #3%, RECORD ROOM%(USER%) &
\	GOTO 51 UNLESS CHR$(ROOM%(USER%))=ROOM$ &
\	CALL ADVSHT(CRLF$+SEND.NAM$+' goes '+CVT$$(C$,128%)) &
\	LSET PEOPLE$=RIGHT(PEOPLE$,INSTR(1%,PEOPLE$,"/")+1%) &
\	PUT #3%, RECORD ROOM%(USER%) &
\	GET #3%, RECORD ROOM%(USER%) &
\	GOTO 51 UNLESS CHR$(ROOM%(USER%))=ROOM$ &
\	GOTO 60 &

51	ROOM%(USER%)=OLD.ROOM% &
\	GET #3%, RECORD ROOM%(USER%) &
\	CALL ADVOUT(KB%(USER%),CRLF$+"You can't move into a corrupt room - "+ &
	"get a Demigod to sort it out.") &
\	SUBEXIT &

55	GET #3%, RECORD ROOM%(USER%) &
\	GOTO 51 UNLESS CHR$(ROOM%(USER%))=ROOM$ &
\	MON.FLAG%(MON%)=MON.FLAG%(MON%) XOR 1% &
\	GOTO 60 &

60	ROOM%(USER%)=NEW.ROOM% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' enters.') &
\	GET #3%, RECORD ROOM%(USER%) &
\	GOTO 51 UNLESS CHR$(ROOM%(USER%))=ROOM$ &
\	CALL ADVDSP &
\	CALL ADVOUT(KB%(USER%),CRLF$+SEND.NAM$+' has followed you.') IF FL% &
\	RETURN &

70	! EXIT ROOMS &
	&
	&
	J%=FLAG%(USER%) AND 64% &
\	FLAG%(USER%)=0% &
\	FLAG%(USER%)=FLAG%(USER%) OR 64% IF J% &
\	J%=((XP.(USER%)>=FNXP%(LEVEL%(USER%)) OR XP.(USER%)<0.) AND LEVEL%(USER%)<10%) &
\	LEVEL%(USER%)=LEVEL%(USER%)+1% IF J% &
\	XP.(USER%)=0. IF J% &
\	HP%(USER%)=5%*INT((14%+2.8*LEVEL%(USER%)^1.8)/5%) IF J% &
\	CALL ADVOUT(KB%(USER%),CRLF$+'You leave the dungeon') &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' enters.') &
\	CALL ADVDSP &
\	RETURN &

80	! INVENTORY &
	&
	&
	GOTO 11500 IF FLAG%(USER%) AND 2%^10% &
\	A$=CVT$$(LEFT(NAM$(USER%),10%),255%) &
\	A$=CVT$$(AR$,255%) IF LEN(CVT$$(AR$,255%))>0% AND LEVEL%(USER%)>=16% &
\	H$="You are" &
\	H$="He is" UNLESS A$=CVT$$(LEFT(NAM$(USER%),10%),255%) &
\	CALL ADVFND(A$) &
\	USER1%=ABS(USER1%) IF LEVEL%(USER%)>=16% &
\	RETURN UNLESS USER1%>0% &
\	GOTO 11020 UNLESS LEVEL%(USER%)>LEVEL%(USER1%) UNLESS USER1%=USER% &
	UNLESS LEVEL%(USER%)=30% &
\	USER1%=ABS(USER1%) &
\	I%=USER1% &
\	CALL ADVOUT(KB%(USER%),CRLF$+ &
	"Current weapon : "+CVT$$(WEAPON$(I%),128%)+" of damage bonus "+ &
	NUM1$(WEAPON%(I%))+CRLF$+ &
	CRLF$+H$+" carrying :"+CRLF$) &
\	OBJ1$="" &
\	FOR J%=0% TO 9% &
\	J$=CVT$$(OB$(I%,J%),128%) &
\	J$=LEFT(J$,INSTR(1%,J$,"~")-1%) IF INSTR(1%,J$,"~") &
	AND LEVEL%(USER%)<30% &
\	J$=LEFT(J$,INSTR(1%,J$,"$")-1%) IF INSTR(1%,J$,"$") &
	AND LEVEL%(USER%)<30% &
\	OBJ1$=OBJ1$+CI$+CI$+J$+CRLF$ IF LEN(J$) &
\	NEXT J% &
\	OBJ1$=CI$+CI$+"Nothing"+CRLF$ UNLESS LEN(OBJ1$) &
\	CALL ADVOUT(KB%(USER%),OBJ1$) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" searches his backpack.") IF &
		A$=CVT$$(NAM$(USER%),255%) &
\	RETURN &

90	! GET &
	&
	&
	GOTO 11040 IF (INSTR(1%,PEOPLE$,"*")+INSTR(1%,PEOPLE$,"#")+ &
	INSTR(1%,PEOPLE$,"+"))<>0% IF LEVEL%(USER%)<30% &
\	GOTO 11040 IF INSTR(1%,AR$,'~') OR INSTR(1%,AR$,'$') &
	UNLESS LEVEL(USER%)>=30% OR (FLAG%(USER%) AND 128%) &
\	IF INSTR(1%,CVT$$(OBJECT$,255%),CVT$$(AR$,255%))=0% THEN &
	CALL ADVOUT(KB%(USER%),CRLF$+"It's not here.") &
\	RETURN &

100	IF LEN(CVT$$(AR$,255%))=0% THEN CALL ADVOUT(KB%(USER%), &
	CRLF$+"Get what ?") &
\	RETURN &

110	GOTO 120 IF CVT$$(OB$(USER%,I%),255%)="" FOR I%=0% TO 9% &
\	GOTO 11000 &

120	J$=FNGRAB$(OBJECT$,CVT$$(AR$,128%)) &
\	OB$(USER%,I%)=J$ &
\	LSET OBJECT$=FNEDIT$(OBJECT$,J$+"/") &
\	PUT #3%, RECORD ROOM%(USER%) &
\	J$="the"+RIGHT(J$,INSTR(1%,J$," ")) IF INSTR(1%,J$," ") <3% &
	AND INSTR(1%,J$," ")>0% &
\	J$=LEFT(J$,INSTR(1%,J$,"~")-1%) IF INSTR(1%,J$,"~") &
\	J$=LEFT(J$,INSTR(1%,J$,"$")-1%) IF INSTR(1%,J$,"$") &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" picks up "+J$) &
\	GOTO 11020 &

130	! STATUS &
	&
	&
	A$=CVT$$(AR$,128%) &
\	A$=CVT$$(LEFT(NAM$(USER%),10%),255%) UNLESS LEN(A$)>0% AND LEVEL%(USER%)>=13% &
\	CALL ADVFND(A$) &
\	USER1%=ABS(USER1%) IF LEVEL%(USER%)>=13% &
\	RETURN UNLESS USER1%>0% &
\	GOTO 11040 IF LEVEL%(USER1%)>=LEVEL%(USER%) UNLESS USER1%=USER% &
	OR LEVEL%(USER%)>=30% &
\	I%=ABS(USER1%) &
\	J$=CRLF$+"Name :"+CI$+NAM$(I%) &
\	J$=J$+CRLF$+"Level :"+CI$+ &
	NUM1$(LEVEL%(I%))+CRLF$+"Xp :"+CI$+NUM1$(INT(XP.(I%)))+CRLF$+ &
	"HP :"+CI$+NUM1$(HP%(I%))+CRLF$ &
\	J$=J$+'Fatigue : '+NUM1$(FATIGUE%(I%))+CRLF$ &
\	J$=J$+"Room :"+CI$+NUM1$(ROOM%(I%))+CRLF$ IF LEVEL%(USER%)>=16% &
\	J$=J$+"(Invisible)" IF FLAG%(I%) AND 2% &
\	J$=J$+"(Held)" IF FLAG%(I%) AND 2%^8% &
\	CALL ADVOUT(KB%(USER%),J$) &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" examines his wounds.") IF &
	A$=CVT$$(NAM$(USER%),255%) &
\	RETURN &

140	! DROP &
	&
	&
	GOTO 11020 IF INSTR(1%,AR$,'~') OR INSTR(1%,AR$,'$') &
\	GOTO 150 IF INSTR(1%,CVT$$(OB$(USER%,I%),255%),CVT$$(AR$,255%)) &
	FOR I%=0% TO 9% &
\	GOTO 11010 &

150	IF CVT$$(AR$,255%)="" THEN &
	CALL ADVOUT(KB%(USER%),'Drop what?') &
\	RETURN &

160	GOTO 170 IF INSTR(1%,CVT$$(DESC$,255%),"SHRINE") &

161	GOTO 11040 IF INSTR(1%,AR$,'~') OR INSTR(1%,AR$,'$') &
\	J$=CVT$$(OBJECT$,128%) &
\	GOTO 165 IF MID(J$,J%,1%)='/' FOR J%=100% TO 1% STEP -1% &

165	J$=CVT$$(OB$(USER%,I%),128%) &
\	IF J%+LEN(J$)>100% THEN &
	CALL ADVOUT(KB%(USER%),CRLF$+"There's no room.") &
\	RETURN &

166	LSET OBJECT$=J$+"/"+OBJECT$ &
\	PUT #3%, RECORD ROOM%(USER%) &
\	J$=CVT$$(OB$(USER%,I%),128%) &
\	OB$(USER%,I%)="" &
\	J$="the"+RIGHT(J$,INSTR(1%,J$," ")) IF INSTR(1%,J$," ") <3% &
	AND INSTR(1%,J$," ")>0% &
\	J$=LEFT(J$,INSTR(1%,J$,"~")-1%) IF INSTR(1%,J$,"~") &
\	J$=LEFT(J$,INSTR(1%,J$,"$")-1%) IF INSTR(1%,J$,"$") &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" drops "+J$) &
\	GOTO 11020 &

170	! DROP IN SHRINE &
	&
	&
	GOTO 11040 UNLESS INSTR(1%,CVT$$(OB$(USER%,I%),128%),"~") &
\	J%=VAL(RIGHT(CVT$$(OB$(USER%,I%),128%),INSTR(1%,OB$(USER%,I%), &
	"~")+1%)) &
\	XP.(USER%)=XP.(USER%)+J% IF XP.(USER%)+J%>0% &
\	OB$(USER%,I%)="" &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' makes an offering.') &
\	GOTO 11020 &

180	! TELL &
	&
	&
	WHO$=LEFT(AR$,INSTR(1%,AR$," ")-1%) &
\	WHO$=AR$ UNLESS LEN(WHO$) &
\	MESS$=RIGHT(AR$,INSTR(1%,AR$," ")+1%) &
\	MESS$="" IF MESS$=WHO$ &
\	MESS$=XLATE(MESS$,MMM$) IF FLAG%(USER%) AND 2%^9% &
\	FLAG%(USER%)=FLAG%(USER%) XOR 2%^9% IF RND*100<=LEVEL%(USER%)*10%+10% IF &
	FLAG%(USER%) AND 2%^9% &
\	WHO$=CVT$$(WHO$,255%) &
\	MESS$=CVT$$(MESS$,128%) &
\	J%=LEVEL%(USER%)>=30% &
\	CALL ADVFND(WHO$) &
\	USER1%=ABS(USER1%) IF LEVEL%(USER%)>=30% &
\	RETURN UNLESS USER1%>0% &
\	I%=USER1% &

190	GOTO 11030 IF FLAG%(I%) AND 2% UNLESS J% &
\	NPCMESS$(I%)=CHR$(USER%)+MESS$ IF FLAG%(I%) AND 2%^6% &
\	J$=CVT$$(NAM$(USER%),128%)+" says '" &
\	J$="You hear a voice say '" IF (J% AND ROOM%(USER%)<>ROOM%(I%) &
	AND LEVEL%(I%)<11%) OR (FLAG%(USER%) AND 2%) &
\	CALL ADVOUT(KB%(I%),CRLF$+J$+MESS$+"'")&
\	JJ%=ROOM%(I%) &
\	ROOM%(I%)=0% UNLESS I%=USER% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' talks to '+ &
	CVT$$(NAM$(I%),128%)+'.') &
\	ROOM%(I%)=JJ% &
\	GOTO 11020 &

200	! SHOUT &
	&
	&
	GOTO 11040 UNLESS LEVEL%(USER%)>=1% &
\	AR$=XLATE(AR$,MMM$) IF FLAG%(USER%) AND 2%^9% &
\	FLAG%(USER%)=FLAG%(USER%) XOR 2%^9% IF RND*100<=LEVEL%(USER%)*10%+10% IF &
	FLAG%(USER%) AND 2%^9% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" shouts '"+CVT$$(AR$,128%)+"'") &
\	GOTO 11020 &

210	! DRAW &
	&
	&
	IF FLAG%(USER%) AND 1% THEN &
	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' brandishes his weapon.') &
\	GOTO 11020 &

211	FLAG%(USER%)=FLAG%(USER%) OR 1% &
\	J$=CVT$$(WEAPON$(USER%),128%) &
\	J$="his"+RIGHT(J$,INSTR(1%,J$," ")) IF INSTR(1%,J$," ") <= 3% &
	AND INSTR(1%,J$," ")>0% &
	AND INSTR(1%,J$," ")>0% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+' readies '+J$) &
\	GOTO 11020 &

220	! SHEATH &
	&
	&
	GOTO 11020 UNLESS FLAG%(USER%) AND 1% &
\	FLAG%(USER%)=FLAG%(USER%)-1% &
\	J$=CVT$$(WEAPON$(USER%),128%) &
\	J$="his"+RIGHT(J$,INSTR(1%,J$," ")) IF INSTR(1%,J$," ") <= 3% &
	AND INSTR(1%,J$," ")>0% &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" puts away "+J$) &
\	GOTO 11020 &

230	! HIT &
	&
	&
	GOTO 11040 UNLESS LEN(CVT$$(AR$,128%)) &
\	IF (FLAG%(USER%) AND 1%)=0% THEN &
	J$=CVT$$(WEAPON$(USER%),128%) &
\	J$='your'+RIGHT(J$,INSTR(1%,J$,' ')) IF LEFT(J$,2%)='A ' OR &
	LEFT(J$,3%)='An ' &
\	CALL ADVOUT(KB%(USER%), &
	CRLF$+'First draw '+J$) &
\	RETURN &

240	FATIGUE%(USER%)=FATIGUE%(USER%)-4% &
\	J$=CVT$$(AR$,255%) &
\	CALL ADVFND(J$) &
\	USER1%=ABS(USER1%) IF LEVEL%(USER%)>=30% &
\	GOTO 250 IF USER1%>0% AND USER1%<>USER% &
\	GOTO 300 IF (INSTR(1%,PEOPLE$,"*") OR INSTR(1%,PEOPLE$,"#")) &
	AND INSTR(1%,CVT$$(PEOPLE$,255%),CVT$$(AR$,255%))<>0% &
\	GOTO 11040 IF LEFT(NAM$(USER%),LEN(J$))=J$ &
\	RETURN &

250	GOTO 11030 IF FLAG%(USER1%) AND 2% &
\	J%=7%+LEVEL%(USER%)-LEVEL%(USER1%) &
\	CH%=5% &
\	CH%=5%+J%*5% IF J%/7%=0% &
\	CH%=J%*10%-30% IF J%/7% &
\	J.=(FATIGUE%(USER%)*1.0)/50.+.25 &
\	J.=1. IF J.>1. &
\	CH%=J.*CH% &
\	D%=INT(RND*100%) &
\	IF D%>CH% THEN &
	MESS$=CVT$$(NAM$(USER%),128%)+" tries to hit " &
\	GOTO 11050 &

260	UU%=USER% &
\	DAM%=LEVEL%(USER%)/2%+RND*(LEVEL%(USER%)/2%) &
\	DAM%=DAM%*2% IF D%<=5% &
\	WEAPON%(USER%)=WEAPON%(USER%)-1% IF D%>85% AND WEAPON%(USER%)>0% &
\	DAM%=DAM%+RND*WEAPON%(USER%) &
\	HP%(USER1%)=HP%(USER1%)-DAM% &
\	UU%=USER1% IF HP%(USER1%)<=0% &
\	MESS$=CVT$$(NAM$(USER%),128%)+" hits " &
\	MESS$=CVT$$(NAM$(USER%),128%)+" kills " IF HP%(USER1%)<=0% &
\	J$=CVT$$(WEAPON$(USER%),128%) &
\	J$="his"+RIGHT(J$,INSTR(1%,J$," ")) IF INSTR(1%,J$," ") <= 3% &
	AND INSTR(1%,J$," ")>0% &
\	MESS1$=" with "+J$ &
\	J$="You did "+NUM1$(DAM%)+" points of damage" &
\	J$=J$+" and killed "+CVT$$(NAM$(USER1%),128%) IF HP%(USER1%)<=0% &
	AND (HP%(USER1%)+DAM%)>0% &
\	XP.(USER%)=XP.(USER%)+FNXP%(LEVEL%(USER1%)-1%)/2%*10%/10% IF &
	HP%(USER1%)<=0% AND (HP%(USER1%)+DAM%)>0% &
\	CALL ADVOUT(KB%(USER%),CRLF$+J$) &
\	GOSUB 11070 &
\	IF UU%<>USER% THEN &
	J$='> ' &
\	J$='-]--- ' IF FLAG%(USER%) AND 1% &
\	PRINT #1%, RECORD 32767%+1%+KB%(USER%), CRLF$;J$; &

299	USER%=UU% &
\	GET #3%, RECORD ROOM%(USER%) &
\	RETURN &

300	! HIT MONSTER &
	&
	&
	WHO$=FNGRAB$(PEOPLE$,CVT$$(AR$,128%)) &
\	WHO$=RIGHT(WHO$,2%) &
\	WHO$="the"+RIGHT(WHO$,INSTR(1%,WHO$," ")) IF INSTR(1%,WHO$," ") <= 3% &
	AND INSTR(1%,WHO$," ")>0% &
\	J%=FNGMD%(ROOM%(USER%)) &
\	J%=7%+LEVEL%(USER%)-DEF.LEVEL%(MON%) &
\	CH%=5% &
\	CH%=5%+J%*5% UNLESS J%/7% &
\	CH%=10%*J%-30% IF J%/7% &
\	CH%=CH%*((1.0*FATIGUE%(USER%))/30.0) &
\	D%=INT(RND*100%) &
\	IF D%>CH% THEN &
	CALL ADVOUT(KB%(USER%),CRLF$+"You fail.") &
\	CALL ADVSHT(CRLF$+CVT$$(NAM$(USER%),128%)+" tries to hit "+WHO$) &
\	RETURN &

310	DAM%=LEVEL%(USER%)/2%+RND*(LEVEL%(USER%)/2%) &
\	DAM%=DAM%*2% IF D%<=5% &
\	WEAPON%(USER%)=WEAPON%(USER%)-1% IF D%>85% IF WEAPON%(USER%)>0% &
\	DAM%=DAM%+RND*WEAPON%(USER%) &
\	MON.HP%(MON%)=MON.HP%(MON%)-DAM% &
\	MESS$=CVT$$(NAM$(USER%),128%)+" hits " &
\	MESS$=CVT$$(NAM$(USER%),128%)+" kills " IF MON.HP%(MON%)<=0% &
\	J$=CVT$$(WEAPON$(USER%),128%) &
\	J$="his"+RIGHT(J$,INSTR(1%,J$," ")) IF INSTR(1%,J$," ") <= 3% &
	AND INSTR(1%,J$," ")>0% &
\	MESS1$=" with "+J$ &
\	CALL ADVSHT(CRLF$+MESS$+WHO$+MESS1$) &
\	J$="You did "+NUM1$(DAM%)+" points of damage" &
\	J$=J$+" and killed "+WHO$ IF MON.HP%(MON%)<=0% &
\	J1.=DAM% &
\	J2.=MON.XP%(MON%) &
\	J3.=MON.HP%(MON%) &
\	J3.=0. IF J3.<0. &
\	J4.=J1./(J3.+J1.+1.) &
\	XP.(USER%)=XP.(USER%)+J4.*MON.XP%(MON%) &
\	MON.XP%(MON%)=MON.XP%(MON%)-J4.*MON.XP%(MON%) &
\	CALL ADVOUT(KB%(USER%),CRLF$+J$) &
\	IF MON.HP%(MON%)<=0% THEN &
	J%=FNSMD%(MON%) &
\	J$=FNGRAB$(PEOPLE$,CVT$$(AR$,128%)) &
\	LSET PEOPLE$=FNEDIT$(PEOPLE$,J$+"/") &
\	LSET PEOPLE$="!"+RIGHT(J$,2%)+"'s corpse/"+PEOPLE$ &
\	PUT #3%, RECORD ROOM%(USER%) &

320	RETURN &

330	! EXIT &
	&
	&
	CALL ADVOUT(KB%(USER%),CRLF$+'Type ^Z to exit.') &
\	RETURN &

11000	MESS$="You can't carry any more." &
\	GOTO 11100 &

11010	MESS$="You aren't carrying it." &
\	GOTO 11100 &

11020	MESS$="Ok." &
\	GOTO 11100 &

11030	MESS$="I can't see him." &
\	GOTO 11100 &

11040	MESS$="You can't." &
\	GOTO 11100 &

11050	CALL ADVOUT(KB%(USER%),CRLF$+"You fail.") &
\	CALL ADVOUT(KB%(USER1%),CRLF$+MESS$+'you.') &
\	JJ%=ROOM%(USER1%) &
\	ROOM%(USER1%)=0% &
\	CALL ADVSHT(CRLF$+MESS$+CVT$$(NAM$(USER1%),128%)) &
\	ROOM%(USER1%)=JJ% &
\	RETURN &

11060	MESS$="You already have." &
\	GOTO 11100 &

11070	CALL ADVOUT(KB%(USER1%),CRLF$+MESS$+'you'+MESS1$) &
\	JJ%=ROOM%(USER1%) &
\	ROOM%(USER1%)=0% &
\	CALL ADVSHT(CRLF$+MESS$+CVT$$(NAM$(USER1%),128%)+MESS1$) &
\	ROOM%(USER1%)=JJ% &
\	RETURN &

11100	CALL ADVOUT(KB%(USER%),CRLF$+MESS$) &
\	RETURN &

11500	CALL ADVOUT(KB%(USER%),CRLF$+"You can't see.") &
\	RETURN &

15000	! FUNCTIONS &
	&
	&

15040	DEF* FNXP%(LL%) &
\	L%=LL%+1% &
\	J%=30% IF L%=1% &
\	J%=200% IF L%=2% &
\	J%=500% IF L%=3% &
\	J%=1000% IF L%=4% &
\	J%=2000% IF L%=5% &
\	J%=3000% IF L%=6% &
\	J%=6000% IF L%=7% &
\	J%=10000% IF L%=8% &
\	J%=20000% IF L%=9% &
\	J%=20000% IF L%=10% &
\	J%=30000% IF L%>=11% &
\	FNXP%=J% &
\	FNEND &

15050	DEF* FNEDIT$(X$,B$) &
\	A$=X$ &
\	Z%=1% &

15060	GOTO 15070 UNLESS INSTR(1%,A$,B$) &
\	Y%=INSTR(1%,A$,B$) &
\	A$=LEFT(A$,Y%-1%)+RIGHT(A$,Y%+LEN(B$)) &

15070	FNEDIT$=A$ &
\	FNEND &

15080	DEF* FNGRAB$(O$,P$) &
\	O%=INSTR(1%,CVT$$(O$,36%),CVT$$(P$,36%)) &
\	GOTO 15090 IF MID(O$,II%,1%)="/" FOR II%=O% TO 1% STEP -1% &
\	OO%=1% &
\	GOTO 15100 &

15090	OO%=II%+1% &

15100	FNGRAB$=MID(O$,OO%,INSTR(OO%,O$,"/")-OO%) &
\	FNEND &

15110	DEF* FNCLEAR$ &
\	IF FLAG%(USER%) AND 1% THEN &
	HP%(USER%)=HP%(USER%)*3%/4% &
\	FLAG%(USER%)=FLAG%(USER%)-1% &

15120	FNEND &

15140	DEF* FNGMD%(R%) &
\	GOTO 15150 IF MON.ROOM%(MON%)=R% FOR MON%=0% TO 10% &
\	MON%=-1% &
\	MON%=I% IF MON.ROOM%(I%)=0% FOR I%=0% TO 10% &
\	MON%=INT(RND*10%) IF MON%=-1% &
\	J%=FNSMD%(MON%) IF MON.ROOM%(MON%) &
\	J$=MONSTER$(R%) &
\	ATT.LEVEL%(MON%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	DEF.LEVEL%(MON%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	MON.HP%(MON%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	MON.DAM%(MON%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	SPEC$(MON%)=LEFT(J$,6%) &
\	J$=RIGHT(J$,7%) &
\	MON.XP%(MON%)=CVT$%(J$) &
\	J$=RIGHT(J$,3%) &
\	MON.FLAG%(MON%)=CVT$%(J$) &
\	MON.ROOM%(MON%)=R% &

15150	FNEND &

15160	DEF* FNSMD%(R%) &
\	J$=CVT%$(ATT.LEVEL%(R%))+CVT%$(DEF.LEVEL%(R%))+CVT%$(MON.HP%(R%))+ &
	CVT%$(MON.DAM%(R%))+ &
	SPEC$(R%)+CVT%$(MON.XP%(R%))+CVT%$(MON.FLAG%(R%))+CVT%$(0%) &
\	MONSTER$(MON.ROOM%(R%))=J$ &
\	MON.ROOM%(R%)=0% &
\	FNEND &

25000	! ERRORS &
	&
	&
	RESUME 11040 IF ERL=40 &
\	GOTO 29100 IF SINGLE.USER% &

29000	OPEN "MSG:LOG1.95" AS FILE #4%, MODE 2% &
\	PRINT #4%, "ADVNOR : ";RIGHT(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERR)),3%); &
	" at line";ERL;" at ";TIME$(0%) &
\	PRINT #4%, "User was ";NAM$(USER%);" in room";ROOM%(USER%) IF &
	(USER%>0% AND USER%<=8%) &
\	CLOSE #4% &
\	RESUME 32767 &

29100	PRINT "ADVNOR Error ";ERR;" at line ";ERL &
\	RESUME 32767 &

32767	SUBEND &

