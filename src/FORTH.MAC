                .title              FORTH
                .PSECT
                .enabl              lc
;               FIG-FORTH           for the pdp11.
;
;            (c) D Holroyd    1986       and Forth Interest Group.
;
;  Release and version numbers...
         figrel=1
         figrev=1
         usrver=2
;  ASCII characters used.
         abl=32.
         acr=13.
         adot=46.
         bell=7.
         bsin=177
         bsout=8.
         lf=10.
         ff=12.
;  Memory allocation...
         em=70000
         nscr=4.
         kbbuf=512.
         us=64.
         rts=160.
         co=kbbuf+4
         nbuf=nscr*1024./kbbuf
         bbyte=nbuf*co
         buf1=em-bbyte
         initr0=buf1-us-160.
         inits0=initr0-rts
;  Start of main code at 2000
orig:    jmp      cld      ;Vector to cold start.
         .word    0
         jmp      wrm      ;Vector to warm start.
.macro   .meven   reg,?l1,?m1
         mov      r'reg,-(sp)
         bic      #177776,r'reg
         beq      l1
         mov      (sp)+,r'reg
         add      #1,r'reg
         br       m1
l1:      mov      (sp)+,r'reg
m1:      
         .endm
         .byte    figrel
         .byte    figrev
         .byte    usrver
         .byte    16
         .word    task-10
         .word    bsin
         .word    initr0
;  Following data used by COLD
         .word    inits0
         .word    initr0
         .word    inits0    ;Init (TIB)
         .word    31.       ;Init (Width)
         .word    1.        ;Init (Warning)
                            ;(0=No screen 4)
                            ;(1=Forth screens are available)
         .word    initdp    ;Init(Fence)
         .word    initdp    ;Init(DP)
         .word    forth+10   ;Init(Voc-Link)
         .word    23,145260 ;RSTS in base 36
;  End of COLD data.
up:      .word    initr0    ;User area pointer.
rpp:     .word    initr0    ;Return stack pointer.
dpush:   mov      r1,-(sp)
hpush:   mov      r2,-(sp)
next:    mov      (r0)+,r2
next1:   mov      r2,r1
         mov      (r1),r2
         inc      r1
         jmp      (r2)
         .word    0,0
;  Forth  Dictionary
dp0:     .byte    203
         .ascii   "LIT"
         .word    0
lit:     .word    .+2
         mov      (r0)+,r2
         jmp      hpush
         .byte    207
         .ascii   "EXECUTE"
         .word    lit-6
exec:    .word    .+2
         mov      (sp)+,r2
         jmp      next1
         .byte    206
         .ascii   "BRANCH"
         .even
         .word    exec-12
bran:    .word    .+2
bran1:   add      (r0),r0
         jmp      next
         .byte    207
         .ascii   "0BRANCH"
         .word    bran-12
zbran:   .word    .+2
         mov      (sp)+,r2
         tst      r2
         beq      bran1
         inc      r0
         inc      r0
         jmp      next
         .byte    206
         .ascii   "(LOOP)"
         .even
         .word    zbran-12
xloop:   .word    .+2
         mov      #1,r1
xloo1:   mov      rpp,r2
         mov      r1,r3
         add      (r2),r1
         mov      r1,(r2)+
         tst      r3
         bmi      xloo2
         mov      r1,r3
         sub      (r2)+,r3
         jmp      xloo3
xloo2:   mov      (r2)+,r3
         sub      r1,r3
xloo3:   tst      r3
         bmi      bran1
         mov      r2,rpp
         inc      r0
         inc      r0
         jmp      next
         .byte    207
         .ascii   "(+LOOP)"
         .word    xloop-12
xploo:   .word    .+2
         mov      (sp)+,r1
         jmp      xloo1
         .byte    204
         .ascii   "(DO)"
         .even
         .word    xploo-12
xdo:     .word    .+2
         mov      rpp,r2
         dec      r2
         dec      r2
         dec      r2
         dec      r2
         mov      r2,rpp
         mov      (sp)+,(r2)+
         mov      (sp)+,(r2)
         jmp      next
         .byte    201
         .ascii   "I"
         .word    xdo-10
ido:     .word    .+2
         mov      rpp,r2
         mov      (r2),-(sp)
         jmp      next
         .byte    205
         .ascii   "DIGIT"
         .word    ido-4
digit:   .word    .+2
         mov      (sp)+,r2
         mov      (sp)+,r1
         movb     r1,r3
         sub      #60,r3
         swab     r3
         bmi      digi2
         swab     r3
         cmpb     #12,r3
         bcc      digi1
         sub      #7,r3
         cmpb     #11,r3
         bcc      digi2
digi1:   cmpb     r3,r2
         bcc      digi2
         movb     r3,r1
         mov      #1,r2
         jmp      dpush
digi2:   clr      r2
         jmp      hpush
         .byte    206
         .ascii   "(FIND)"
         .even
         .word    digit-10
pfind:   .word    .+2
         mov      (sp)+,r1
pfin1:   mov      (sp),r2
         movb     (r1),r3
         movb     (r2),r4
         bic      #177700,r3
         mov      r3,r5
         bic      #40,r5
         xor      r4,r3
         tst      r3
         bne      pfin3
         movb     (r1),r3
         bic      #177400,r3
pfin2:   inc      r2
         inc      r1
         cmpb     (r1),(r2)
         bne      pfin3
         sob      r4,pfin2
         mov      #5,r2
         add      r1,r2
         .meven   2
         mov      r2,(sp)
pfin6:   mov      r3,r1
         mov      #1,r2
         jmp      dpush
pfin3:   movb     (r1),orig+4
         bmi      pfin5
pfin4:   dec      r1
         jmp     pfin3
pfin5:   add      r5,r1
         inc      r1
         .meven   1
         mov      (r1),r2
         mov      r2,r1
         tst      r1
         bne      pfin1
         inc      sp
         inc      sp
         clr      r2
         jmp      hpush
         .byte    207
         .ascii   "ENCLOSE"
         .word    pfind-12
encl:    .word    .+2
         mov      (sp)+,r1
         mov      (sp),r2
         movb     r1,r4
         mov      #-1.,r3
         dec      r2
encl1:   inc      r2
         inc      r3
         cmpb     r4,(r2)
         beq      encl1
         mov      r3,-(sp)
         movb     (r2),orig+4
         bne      encl2
         inc      r3
         mov      r3,-(sp)
         dec      r3
         mov      r3,-(sp)
         jmp      next
encl2:   inc      r2
         inc      r3
         cmpb     r4,(r2)
         beq      encl4
         movb     (r2),orig+4
         bne      encl2
encl3:   mov      r3,-(sp)
         mov      r3,-(sp)
         jmp      next
encl4:   mov      r3,-(sp)
         inc      r3
         mov      r3,-(sp)
         jmp      next
         .byte    204
         .ascii   "EMIT"
         .even
         .word    encl-12
emit:    .word    docol
         .word    pemit
         .word    one,outt
         .word    pstor,semis
         .byte    203
         .ascii   "KEY"
         .word    emit-10
gkey:     .word    .+2
         jmp      pgkey
         .byte    211
         .ascii   "?TERMINAL"
         .word    gkey-6
qterm:   .word    .+2
         clr      r2
         jmp      hpush
         .byte    202
         .ascii   "CR"
         .even
         .word    qterm-14
cr:      .word    .+2
         jmp      pcr
         .byte    205
         .ascii   "CMOVE"
         .word    cr-6
cmove:   .word    .+2
         mov      r0,r2
         mov      (sp)+,r0
         mov      (sp)+,r1
         mov      (sp),r5
         mov      r2,(sp)
         mov      r5,r2
         inc      r0
         jmp      cmov2
cmov1:   movb     (r2)+,(r1)+
cmov2:   sob      r0,cmov1
         mov      (sp)+,r0
         jmp      next
         .byte    202
         .ascii   "U*"
         .even
         .word    cmove-10
ustar:   .word    .+2
         mov      (sp)+,r1
         mov      (sp)+,r3
         clr       r4
         clr      r2
         tst      r3
         beq      usta2
usta1:   add      r1,r4
         adc      r2
         sob      r3,usta1
usta2:   mov      r4,-(sp)
         jmp      hpush
         .byte    202
         .ascii   "U/"
         .even
         .word    ustar-6
uslas:   .word    .+2
         mov      (sp)+,r1
         mov      (sp)+,r2
         mov      (sp)+,r3
         clr      r4
usla1:   sub      r1,r3
         sbc      r2
         tst      r2
         blt      usla2
         inc      r4
         br       usla1
usla2:   add      r1,r3
         adc      r2
         mov      r3,-(sp)
         mov      r4,-(sp)
         jmp      next
         .byte    203
         .ascii   "AND"
         .word    uslas-6
andd:    .word    .+2
         mov      (sp)+,r1
         mov      (sp)+,r2
         com      r1
         bic      r1,r2
         jmp      hpush
         .byte    202
         .ascii   "OR"
         .even
         .word    andd-6
orr:     .word    .+2
         mov      (sp)+,r1
         mov      (sp)+,r2
         bis       r1,r2
         jmp       hpush
         .byte     203
         .ascii    "XOR"
         .word     orr-6
xorr:    .word     .+2
         mov       (sp)+,r1
         mov       (sp)+,r2
         xor       r1,r2
         jmp       hpush
         .byte     203
         .ascii    "SP@"
         .word     xorr-6
spat:    .word     .+2
         mov       sp,r2
         jmp       hpush
         .byte     203
         .ascii    "SP!"
         .word     spat-6
spsto:   .word     .+2
         mov       up,r2
         add       #6,r2
         mov       (r2),sp
         jmp       next
         .byte     203
         .ascii    "RP@"
         .word     spsto-6
rpat:    .word     .+2
         mov       rpp,r2
         jmp       hpush
         .byte     203
         .ascii    "RP!"
         .word     rpat-6
rpsto:   .word     .+2
         mov       up,r2
         mov       10(r2),r1
         mov       r1,rpp
         jmp       next
         .byte     202
         .ascii    ";S"
         .even
         .word     rpsto-6
semis:   .word     .+2
         mov       rpp,r2
         mov       (r2)+,r0
         mov       r2,rpp
         jmp       next
         .byte     205
         .ascii    "LEAVE"
         .word     semis-6
leave:   .word     .+2
         mov       rpp,r2
         mov       (r2)+,r1
         mov       r1,(r2)
         jmp       next
         .byte     202
         .ascii    ">R"
         .even
         .word     leave-10
tor:     .word     .+2
         mov       (sp)+,r1
         mov       rpp,r2
         dec       r2
         dec       r2
         mov       r2,rpp
         mov       r1,(r2)
         jmp       next
         .byte     202
         .ascii    "R>"
         .even
         .word     tor-6
fromr:   .word     .+2
         mov       rpp,r2
         mov       (r2)+,r1
         mov       r2,rpp
         mov       r1,-(sp)
         jmp       next
         .byte     201
         .ascii    "R"
         .word     fromr-6
rr:      .word     ido+2
         .byte     202
         .ascii    "0="
         .even
         .word     rr-4
zequ:    .word     .+2
         mov       (sp)+,r2
         tst       r2
         bne       zequ1
         mov       #1,r2
         jmp       hpush
zequ1:   clr       r2
         jmp       hpush
        .byte      202
        .ascii     "0<"
        .even
        .word      zequ-6
zless:  .word      .+2
        mov        (sp)+,r2
        tst        r2
        bpl        zles1
        mov        #1,r2
        jmp        hpush
zles1:  clr        r2
        jmp        hpush
        .byte      201
        .ascii     "+"
        .word      zless-6
plus:   .word      .+2
        mov        (sp)+,r1
        mov        (sp)+,r2
        add        r1,r2
        jmp        hpush
        .byte      202
        .ascii     "D+"
        .even
        .word      plus-4
dplus:  .word      .+2
        mov        6(sp),r1
        mov        r0,6(sp)
        mov        (sp)+,r0
        mov        (sp)+,r2
        add        r2,r1
        mov        (sp)+,r2
        adc        r2
        add        r0,r2
        mov        (sp),r0
        mov        r1,(sp)
        jmp        hpush
        .byte      205
        .ascii     "MINUS"
        .word      dplus-6
minus:  .word      .+2
        mov        (sp)+,r2
        com        r2
        inc        r2
        jmp        hpush
        .byte      206
        .ascii     "DMINUS"
        .even
        .word      minus-10
dminu:  .word      .+2
        mov        (sp)+,r2
        mov        (sp)+,r1
        com        r1
        com        r2
        add        #1,r1
        adc        r2
        mov        r1,-(sp)
        jmp        hpush
        .byte      204
        .ascii     "OVER"
        .even
        .word      dminu-12
over:   .word      .+2
        mov        (sp)+,r1
        mov        (sp),r2
        jmp        dpush
        .byte      204
        .ascii     "DROP"
        .even
        .word      over-10
drop:   .word      .+2
        inc        sp
        inc        sp
        jmp        next
        .byte      204
        .ascii     "SWAP"
        .even
        .word      drop-10
swap:   .word      .+2
        mov        (sp)+,r2
        mov        (sp),r1
        mov        r2,(sp)
        mov        r1,-(sp)
        jmp        next
        .byte      203
        .ascii     "DUP"
        .word      swap-10
dup:    .word      .+2
        mov        (sp),r2
        jmp        hpush
        .byte      204
        .ascii     "2DUP"
        .even
        .word      dup-6
tdup:   .word      .+2
        mov        (sp),r2
        mov        2(sp),r1
        jmp        dpush
        .byte      202
        .ascii     "+!"
        .even
        .word      tdup-10
pstor:  .word      .+2
        mov        (sp)+,r2
        mov        (sp)+,r1
        add        r1,(r2)
        jmp        next
        .byte      206
        .ascii     "TOGGLE"
        .even
        .word      pstor-6
toggl:  .word      .+2
        mov        (sp)+,r1
        mov        (sp)+,r2
        clr        r3
        movb       (r2),r3
        xor        r1,r3
        movb       r3,(r2)
        jmp        next
        .byte      201
        .ascii     "@"
        .even
        .word      toggl-12
at:     .word      .+2
        mov        (sp),r2
        mov        (r2),(sp)
        jmp        next
        .byte      202
        .ascii     "C@"
        .even
        .word      at-4
cat:    .word      .+2
        mov        (sp)+,r2
        mov        r2,r3
        clr        r2
        movb       (r3),r2
        bic        #177400,r2
        jmp        hpush
        .byte      202
        .ascii     "2@"
        .even
        .word      cat-6
tat:    .word      .+2
        mov        (sp)+,r2
        inc        r2
        inc        r2
        mov        (r2),-(sp)
        dec        r2
        dec        r2
        mov        (r2),-(sp)
        jmp        next
        .byte      201
        .ascii     "!"
        .word      tat-6
store:  .word      .+2
        mov        (sp)+,r2
        mov        (sp)+,r1
        mov        r1,(r2)
        jmp        next
        .byte      202
        .ascii     "C!"
        .even
        .word      store-4
cstor:  .word      .+2
        mov        (sp)+,r2
        mov        (sp)+,r1
        movb       r1,(r2)
        jmp        next
        .byte      202
        .ascii     "2!"
        .even
        .word      cstor-6
tstor:  .word      .+2
        mov        (sp)+,r2
        mov        (sp)+,r1
        mov        r1,(r2)+
        mov        (sp)+,r1
        mov        r1,(r2)+
        jmp        next
        .byte      301
        .ascii     ":"
        .word      tstor-6
colon:  .word      docol
        .word      qexec
        .word      scsp
        .word      curr
        .word      at
        .word      cont
        .word      store
        .word      creat
        .word      rbrac
        .word      pscod
docol:  mov        rpp,r2
        mov        r0,-(r2)
        mov        r2,rpp
        inc        r1
        mov        r1,r0
        jmp        next
        .byte      301
        .ascii     ";"
        .word      colon-4
semi:   .word      docol
        .word      qcsp
        .word      comp
        .word      semis
        .word      smudg
        .word      lbrac
        .word      semis
        .byte      204
        .ascii     "NOOP"
        .even
        .word      semi-4
noop:   .word      docol
        .word      semis
        .byte      210
        .ascii     "CONSTANT"
        .even
        .word      noop-10
con:    .word      docol
        .word      creat
        .word      smudg
        .word      comma
        .word      pscod
docon:  mov        r1,r2
        inc        r2
        mov        (r2),-(sp)
        jmp        next
        .byte      210
        .ascii     "VARIABLE"
        .even
        .word      con-14
var:    .word      docol
        .word      con
        .word      pscod
dovar:  inc        r1
        mov        r1,-(sp)
        jmp        next
        .byte      204
        .ascii     "USER"
        .even
        .word      var-14
user:   .word      docol
        .word      con
        .word      pscod
douse:  inc        r1
        mov        r1,r2
        clr        r1
        movb       (r2),r1
        mov        up,r2
        add        r1,r2
        jmp        hpush
        .byte      201
        .ascii     "0"
        .word      user-10
zero:   .word      docon
        .word      0
        .byte      201
        .ascii     "1"
        .word      zero-4
one:    .word      docon
        .word      1
        .byte      201
        .ascii     "2"
        .word      one-4
two:    .word      docon
        .word      2
        .byte      201
        .ascii     "3"
        .word      two-4
three:  .word      docon
        .word      3
        .byte      202
        .ascii     "BL"
        .even
        .word      three-4
bl:     .word      docon
        .word      40
        .byte      203
        .ascii     "C/L"
        .word      bl-6
csll:   .word     docon
        .word      110
        .byte      205
        .ascii     "FIRST"
        .word      csll-6
first:  .word      docon
        .word      buf1
        .byte      205
        .ascii     "LIMIT"
        .word      first-10
limit:  .word      docon
        .word      em
        .byte      205
        .ascii     "B/BUF"
        .word      limit-10
bbuf:   .word      docon
        .word      kbbuf
        .byte      205
        .ascii     "B/SCR"
        .word      bbuf-10
bscr:   .word      docon
        .word      2000/kbbuf
        .byte      207
        .ascii     "+ORIGIN"
        .word      bscr-10
porig:  .word      docol
        .word      lit
        .word      orig 
        .word      plus
        .word      semis
;  User variables.
;
;
        .byte      202
        .ascii     "S0"
        .even
        .word      porig-12
szero:  .word     douse
        .word     6
        .byte     202
        .ascii    "R0"
        .even
        .word     szero-6
rzero:  .word     douse
        .word     10
        .byte     203
        .ascii    "TIB"
        .word     rzero-6
tib:    .word     douse
        .word     12
        .byte     205
        .ascii    "WIDTH"
        .word     tib-6
width:  .word     douse
        .word     14
        .byte     207
        .ascii    "WARNING"
        .word     width-10
warn:   .word     douse
        .word     16
        .byte     205
        .ascii    "FENCE"
        .word     warn-12
fence:  .word     douse
        .word     20
        .byte     202
        .ascii    "DP"
        .even
        .word     fence-10
dp:     .word     douse
        .word     22
        .byte     210
        .ascii    "VOC-LINK"
        .even
        .word     dp-6
vocl:   .word     douse
        .word     24
        .byte     203
        .ascii    "BLK"
        .word     vocl-14
blk:    .word     douse
        .word     26
        .byte     202
        .ascii    "IN"
        .even
        .word     blk-6
inn:    .word     douse
        .word     30
        .byte     203
        .ascii    "OUT"
        .word     inn-6
outt:   .word     douse
        .word     32
        .byte     203
        .ascii    "SCR"
        .word     outt-6
scr:    .word     douse
        .word     34
        .byte     206
        .ascii    "OFFSET"
        .even
        .word     scr-6
ofset:  .word     douse
        .word     36
        .byte     207
        .ascii    "CONTEXT"
        .word     ofset-12
cont:   .word     douse
        .word     40
        .byte     207
        .ascii    "CURRENT"
        .word     cont-12
curr:   .word     douse
        .word     42
        .byte     205
        .ascii    "STATE"
        .word     curr-12
state:  .word     douse
        .word     44
        .byte     204
        .ascii    "BASE"
        .even
        .word     state-10
base:   .word     douse
        .word     46
        .byte     203
        .ascii    "DPL"
        .word     base-10
dpl:    .word     douse
        .word     50
        .byte     203
        .ascii    "FLD"
        .word     dpl-6
fld:    .word     douse
        .word     52
        .byte     203
        .ascii    "CSP"
        .word     fld-6
cspp:   .word     douse
        .word     54
        .byte     202
        .ascii    "R#"
        .even
        .word     cspp-6
rnum:   .word     douse
        .word     56
        .byte     203
        .ascii    "HLD"
        .word     rnum-6
hld:    .word     douse
        .word     60
;
;  End of user variables.
;
        .byte     202
        .ascii    "1+"
        .even
        .word     hld-6
onep:   .word     docol
        .word     one
        .word     plus
        .word     semis
        .byte     202
        .ascii    "2+"
        .even
        .word     onep-6
twop:   .word     docol
        .word     two
        .word     plus
        .word     semis
        .byte     204
        .ascii    "HERE"
        .even
        .word     twop-6
here:   .word     docol
        .word     dp
        .word     at
        .word     semis
        .byte     205
        .ascii    "ALLOT"
        .word     here-10
allot:  .word     docol
        .word     dp
        .word     pstor
        .word     semis
        .byte     201
        .ascii    ","
        .word     allot-10
comma:  .word     docol
        .word     here
        .word     even
        .word     store
        .word     two
        .word     allot
        .word     here,even,dp,store
        .word     semis
        .byte     202
        .ascii    "C,"
        .even
        .word     comma-4
ccomm:  .word     docol
        .word     here
        .word     cstor
        .word     one
        .word     allot
        .word     semis
        .byte     201
        .ascii    "-"
        .word     ccomm-6
subb:   .word     .+2
        mov       (sp)+,r1
        mov       (sp)+,r2
        sub       r1,r2
        jmp       hpush
        .byte      201
        .ascii     "="
        .word     subb-4
equal:  .word     docol
        .word     subb
        .word     zequ
        .word     semis
        .byte     201
        .ascii    "<"
        .word     equal-4
less:   .word     .+2
        mov       (sp)+,r1
        mov       (sp),r2
        xor       r1,r2
        mov       (sp)+,r2
        bmi       les1
        sub       r1,r2
les1:   tst       r2
        bmi       les2
        clr       r2
        jmp       hpush
les2:   mov       #1,r2
        jmp       hpush
        .byte     202
        .ascii    "U<"
        .even
        .word     less-4
uless:  .word     docol,tdup
        .word     xorr,zless
        .word     zbran,ules1-.
        .word     drop,zless
        .word     zequ
        .word     bran,ules2-.
ules1:  .word     subb,zless
ules2:  .word     semis
        .byte     201
        .ascii    ">"
        .word     uless-6
great:  .word     docol
        .word     swap
        .word     less
        .word     semis
        .byte     203
        .ascii    "ROT"
        .word     great-4
rot:    .word     .+2
        mov       (sp)+,r1
        mov       (sp)+,r2
        mov       (sp),r3
        mov       r2,(sp)
        mov       r3,r2
        jmp       dpush
        .byte     205
        .ascii    "SPACE"
        .word     rot-6
space:  .word     docol
        .word     bl
        .word     emit
        .word     semis
        .byte     204
        .ascii    "-DUP"
        .even
        .word     space-10
ddup:   .word     docol
        .word     dup
        .word     zbran
        .word     ddup1-.
        .word     dup
ddup1:  .word     semis
        .byte     210
        .ascii    "TRAVERSE"
        .even
        .word     ddup-10
trav:   .word     docol
        .word     dup,zless,zbran,trav2-.
        .word     swap
trav1:  .word     over
        .word     plus
        .word     lit,177
        .word     over,cat,less
        .word     zbran,trav1-.
        .word     swap,drop,semis
trav2:  .word     drop,dup,cat,lit,37,andd,plus,one,subb,even,semis
        .byte     206
        .ascii    "LATEST"
        .even
        .word     trav-14
lates:  .word     docol
        .word     curr,at,at,semis
        .byte     203
        .ascii    "LFA"
        .word     lates-12
lfa:    .word     docol,lit,4,subb,semis
        .byte     203
        .ascii    "CFA"
        .word     lfa-6
cfa:    .word     docol,two,subb,semis
        .byte     203
        .ascii    "NFA"
        .word     cfa-6
nfa:    .word     docol,lit,6,subb,lit,-1,trav,semis
        .byte     203
        .ascii    "PFA"
        .word     nfa-6
pfa:    .word     docol,one,trav,lit,6,plus,semis
        .byte     204
        .ascii    "!CSP"
        .even
        .word     pfa-6
scsp:   .word     docol,spat,cspp,store,semis
        .byte     206
        .ascii    "?ERROR"
        .even
        .word     scsp-10
qerr:   .word     docol,swap,zbran,qerr1-.,error,bran,qerr2-.
qerr1:  .word     drop
qerr2:  .word     semis
        .byte     205
        .ascii    "?COMP"
        .word     qerr-12
qcomp:  .word     docol,state,at,zequ,lit,21,qerr,semis
        .byte     205
        .ascii    "?EXEC"
        .word     qcomp-10
qexec:  .word     docol,state,at,lit,22,qerr,semis
        .byte     206
        .ascii    "?PAIRS"
        .even
        .word     qexec-10
qpair:  .word     docol,subb,lit,23,qerr,semis
        .byte     204
        .ascii    "?CSP"
        .even
        .word     qpair-12
qcsp:   .word     docol,spat,cspp,at,subb,lit,24,qerr,semis
        .byte     210
        .ascii    "?LOADING"
        .even
        .word     qcsp-10
qload:  .word     docol,blk,at,zequ,lit,26,qerr,semis
        .byte     207
        .ascii    "COMPILE"
        .word     qload-14
comp:   .word     docol
        .word     qcomp,fromr,dup,twop,tor,at,comma,semis
        .byte     301
        .ascii    "["
        .word     comp-12
lbrac:  .word     docol,zero,state,store,semis
        .byte     201
        .ascii    "]"
        .word     lbrac-4
rbrac:  .word     docol,lit,300,state,store,semis
        .byte     206
        .ascii    "SMUDGE"
        .even
        .word     rbrac-4
smudg:  .word     docol,lates,lit,40,toggl,semis
        .byte     203
        .ascii    "HEX"
        .word     smudg-12
hex:    .word     docol,lit,20,base,store,semis
        .byte     205
        .ascii    "OCTAL"
        .word     hex-6
octal:  .word     docol,lit,10,base,store,semis
        .byte     207
        .ascii    "DECIMAL"
        .word     octal-10
dec:    .word     docol,lit,12,base,store,semis
        .byte     207
        .ascii    "(;CODE)"
        .word     dec-12
pscod:  .word     docol,fromr,lates,pfa,cfa,store,semis
        .byte     305
        .ascii    ";CODE"
        .word     pscod-12
semic:  .word     docol,qcsp,comp,pscod,lbrac
semi1:  .word     noop,semis
        .byte     207
        .ascii    "<BUILDS"
        .word     semic-10
build:  .word     docol,zero,con,semis
        .byte     205
        .ascii    "DOES>"
        .word     build-12
does:   .word     docol,fromr,lates,pfa,store,pscod
dodoe:  mov       rpp,r2
        mov       r0,-(r2)
        mov       r2,rpp
        mov       r1,r2
        inc       r2
        mov       (r2)+,r0
        jmp       hpush
        .byte     205
        .ascii    "COUNT"
        .word     does-10
count:  .word     docol,dup,onep,swap,cat,semis
        .byte     204
        .ascii    "TYPE"
        .even
        .word     count-10
type:   .word     docol
        .word     ddup,zbran
        .word     type1-.
        .word     over,plus,swap,xdo
type2:  .word     ido,cat,emit,xloop
        .word     type2-.,bran
        .word     type3-.
type1:  .word     drop
type3:  .word     semis
        .byte     211
        .ascii    "-TRAILING"
        .word     type-10
dtrai:  .word     docol,dup,zero,xdo
dtra1:  .word     over,over,plus,one,subb,cat,bl,subb,zbran
        .word     dtra2-.,leave,bran
        .word     dtra3-.
dtra2:  .word     one,subb
dtra3:  .word     xloop
        .word     dtra1-.,semis
        .byte     204
        .ascii    '(.")'
        .even
        .word     dtrai-14
pdotq:  .word     docol,rr,count,dup,onep,even,fromr,plus,tor,type,semis
        .byte     302
        .ascii    '."'
        .even
        .word     pdotq-10
dotq:   .word     docol,lit,42,state,at,zbran
        .word     dotq1-.
        .word     comp,pdotq,word,here,cat,onep,allot,bran
        .word     dotq2-.
dotq1:  .word     word,here,count,type
dotq2:  .word     semis

        .byte     204
        .ascii    "EVEN"
        .even
        .word     dotq-6
even:   .word     .+2
        mov       (sp)+,r2
        .meven    2
        mov       r2,-(sp)
        jmp       next
        .byte     206
        .ascii    "EXPECT"
        .even
        .word     even-10
expec:  .word     docol,over,plus,over,xdo
expe1:  .word     gkey,dup,lit,bsin,equal,zbran
        .word     expe2-.,drop,dup,ido,equal,dup,fromr,two,subb,plus,tor
        .word     zbran,expe6-.,lit,bell,emit,bran
        .word     expe7-.
expe6:  .word     lit,bsout,emit,bl,emit,lit,bsout,emit
expe7:  .word     bran,expe3-.
expe2:  .word     dup,lit,15,equal,zbran
        .word     expe5-.,gkey,drop,leave,drop,zero
expe5:  .word     ido,cstor,zero,ido,onep,cstor
expe3:  .word     xloop,expe1-.,drop,semis
        .byte     205
        .ascii    "QUERY"
        .word     expec-12
query:  .word     docol,tib,at,lit,120,expec,zero,inn,store,semis
        .byte     301
        .byte     0
        .word     query-10
null:   .word     docol,blk,at,zbran,null1-.
        .word     one,blk,pstor,zero,inn,store,blk,at,bscr,one,subb,andd
        .word     zequ,zbran,null2-.,qexec,fromr,drop
null2:  .word     bran,null3-.
null1:  .word     fromr,drop
null3:  .word     semis
        .byte     204
        .ascii    "FILL"
        .even
        .word     null-4
fill:   .word     .+2
        mov       r0,r2
        mov       (sp)+,r1
        mov       (sp)+,r0
        mov       r2,r4
        mov       (sp),r2
        mov       r4,(sp)
        mov       r2,r4
        mov       r1,r2
        mov       r4,r1
fill1:  tst       r0
        beq       fill2
        movb      r2,(r1)+
        dec       r0
        jmp       fill1
fill2:  mov       (sp)+,r0
        jmp       next
        .byte     205
        .ascii    "ERASE"
        .word     fill-10
erasee: .word     docol,zero,fill,semis
        .byte     206
        .ascii    "BLANKS"
        .even
        .word     erasee-10
blank:  .word     docol,bl,fill,semis
        .byte     204
        .ascii    "HOLD"
        .even
        .word     blank-12
hold:   .word     docol,lit,-1,hld,pstor,hld,at,cstor,semis
        .byte     203
        .ascii    "PAD"
        .word     hold-10
pad:    .word     docol,here,lit,104,plus,semis
        .byte     204
        .ascii    "WORD"
        .even
        .word     pad-6
word:   .word     docol,blk,at,zbran,word1-.,blk,at,block,bran,word2-.
word1:  .word     tib,at
word2:  .word     inn,at,plus,swap,encl,here,lit,101,blank,inn,pstor
        .word     over,subb,tor,rr,here,cstor,plus,here,onep,fromr,cmove,semis
        .byte     210
        .ascii    "(NUMBER)"
        .even
        .word     word-10
pnumb:  .word     docol
pnum1:  .word     onep,dup,tor,cat,base,at,digit,zbran,pnum2-.
        .word     swap,base,at,ustar,drop,rot,base,at,ustar,dplus,dpl,at,onep,zbran
        .word     pnum3-.,one,dpl,pstor
pnum3:  .word     fromr,bran,pnum1-.
pnum2:  .word     fromr,semis
        .byte     206
        .ascii    "NUMBER"
        .even
        .word     pnumb-14
numb:   .word     docol,zero,zero,rot,dup,onep,cat,lit,55,equal,dup,tor
        .word     plus,lit,-1
numb1:  .word     dpl,store,pnumb,dup,cat,bl,subb,zbran,numb2-.,dup,cat,lit
        .word     56,subb,zero,qerr,zero,bran,numb1-.
numb2:  .word     drop,fromr,zbran,numb3-.,dminu
numb3:  .word     semis
        .byte     205
        .ascii    "-FIND"
        .word     numb-12
dfind:  .word     docol,bl,word,here,cont,at,at
        .word     pfind,dup,zequ,zbran,dfin1-.,drop,here,lates,pfind
dfin1:  .word     semis
        .byte     207
        .ascii    "(ABORT)"
        .word     dfind-10
pabor:  .word     docol,abort,semis
        .byte     205
        .ascii    "ERROR"
        .word     pabor-12
error:  .word     docol,warn,at,zless,zbran,erro1-.,pabor
erro1:  .word     here,count,type,pdotq
        .byte     2
        .ascii    "? "
        .even
        .word     mess,spsto,inn,at,blk,at,ddup,zbran,erro2-.,inn,at
        .word     swap
erro2:  .word     spsto,quit
        .byte     203
        .ascii    "ID."
        .word     error-10
iddot:  .word     docol,pad,lit,32.,lit,137,fill,dup,pfa,lfa,over,subb,pad,swap
        .word     cmove,pad,count,lit,37,andd,type,space,semis
        .byte     206
        .ascii    "CREATE"
        .even
        .word     iddot-6
creat:  .word     docol,dfind,zbran,crea1-.,drop,nfa,iddot,lit,4,mess,space
crea1:  .word     here,dup,cat,width,at,min,onep,allot,dup,lit,240,toggl
        .word     lates,comma,curr,at,store,here
        .word     twop,comma,semis,0,0,0,0,0,0
        .byte     311
        .ascii    "[COMPILE]"
        .word     creat-12
bcomp:  .word     docol,dfind,zequ,zero,qerr,drop,cfa,comma,semis
        .byte     207
        .ascii    "LITERAL"
        .word     bcomp-14
liter:  .word     docol,state,at,zbran,lite1-.,comp,lit,comma
lite1:  .word     semis
        .byte     310
        .ascii    "DLITERAL"
        .even
        .word     liter-12
dlite:  .word     docol,state,at,zbran,dlit1-.,swap,liter,liter
dlit1:  .word     semis
        .byte     206
        .ascii    "?STACK"
        .even
        .word     dlite-14
qstac:  .word     docol,spat,szero,at,swap,uless,one,qerr,spat,here,lit,200
        .word     plus,uless,lit,7,qerr,semis
        .byte     211
        .ascii    "INTERPRET"
        .word     qstac-12
inter:  .word     docol
inte1:  .word     dfind,zbran,inte2-.,state,at
        .word     less,zbran,inte3-.,cfa,comma,bran,inte4-.
inte3:  .word     cfa,exec
inte4:  .word     qstac,bran,inte5-.
inte2:  .word     here,numb,dpl,at,onep,zbran,inte6-.,dlite,bran,inte7-.
inte6:  .word     drop,liter
inte7:  .word     qstac
inte5:  .word     bran,inte1-.
inte8:  .word     drop,semis
        .byte     211
        .ascii    "IMMEDIATE"
        .word     inter-14
immed:  .word     docol,lates,lit,100,toggl,semis
        .byte     212
        .ascii    "VOCABULARY"
        .even
        .word     immed-14
vocab:  .word     docol,build,lit,120201,comma,curr,at,at,comma,here,vocl,at
        .word     comma,vocl,store,does
dovoc:  .word     twop,cont,store,semis
        .byte     305
        .ascii    "FORTH"
        .word     vocab-16
forth:  .word     dodoe,dovoc,120201,task-10,0
        .byte     213
        .ascii    "DEFINITIONS"
        .word     forth-10
defin:  .word     docol,cont,at,curr,store,semis
        .byte     301
        .ascii    "("
        .word     defin-16
paren:  .word     docol,lit,51,word,semis
        .byte     204
        .ascii    "QUIT"
        .even
        .word     paren-4
quit:   .word     docol,zero,blk,store,lbrac
quit1:  .word     rpsto,cr,query,inter,state,at,zequ,zbran,quit2-.,pdotq
        .byte     2
        .ascii    "ok"
        .even
quit2:  .word     bran,quit1-.
        .byte     205
        .ascii    "ABORT"
        .word     quit-10
abort:  .word     docol,spsto,dec,qstac,cr,dotcpu,pdotq
        .byte     16
        .ascii    "fig-FORTH "
        .byte     figrel+60,adot,figrev+60,usrver+60
        .even
        .word     forth,defin,quit
wrm:    mov       #wrm1,r0
        jmp       next
wrm1:   .word     warm
        .byte     204
        .ascii    "WARM"
        .even
        .word     abort-10
warm:   .word     docol,mtbuf,abort
cld:    mov       #cld1,r0
        mov       orig+24,sp
        mov       #28.,xrb
        .core
        mov       #break,@#24
        jmp       next
break:  mov       #brea2,r0
        mov       #break,@#24
        .ttrst
        jmp       next
brea2:  .word     brea3
brea3:  .word     docol,cr,spsto,pdotq
        .byte     7
        .ascii    "Aborted"
        .word     cr,quit
cld1:   .word     cold
        .byte     204
        .ascii    "COLD"
        .even
        .word     warm-10
cold:   .word     docol,mtbuf,lit,buf1,use,store,lit,buf1,prev,store
        .word     two,popen

        .byte     11
        .ascii    "FORTH.DAT"
        .word     lit,orig+24,lit,up
        .word     at,lit,6,plus,lit,20,cmove,lit,orig+16,at,lit,forth+6,store
        .word     abort
        .byte     203
        .ascii    "UUO"
        .word     cold-10
uo:     .word     .+2
        .uuo
        jmp       next
        .byte     204
        .ascii    "S->D"
        .even
        .word     uo-6
stod:   .word     .+2
        mov       (sp)+,r1
        clr       r2
        tst       r1
        bpl       stod1
        dec       r2
stod1:  jmp       dpush
        .byte     202
        .ascii    "+-"
        .even
        .word     stod-10
pm:     .word     docol,zless,zbran,pm1-.,minus
pm1:    .word     semis
        .byte     203
        .ascii    "D+-"
        .word     pm-6
dpm:    .word     docol,zless,zbran,dpm1-.,dminu
dpm1:   .word     semis
        .byte     203
        .ascii    "ABS"
        .word     dpm-6
abs:    .word     docol,dup,pm,semis
        .byte     204
        .ascii    "DABS"
        .even
        .word     abs-6
dabs:   .word     docol,dup,dpm,semis
        .byte     203
        .ascii    "MIN"
        .word     dabs-10
min:    .word     docol,tdup,great,zbran,min1-.,swap
min1:   .word     drop,semis
        .byte     203
        .ascii    "MAX"
        .word     min-6
max:    .word     docol,tdup,less,zbran,max1-.,swap
max1:   .word     drop,semis
        .byte     202
        .ascii    "M*"
        .even
        .word     max-6
mstar:  .word     docol,tdup,xorr,tor,abs,swap,abs,ustar,fromr,dpm,semis
        .byte     202
        .ascii    "M/"
        .even
        .word     mstar-6
mslas:  .word     docol,over,tor,tor,dabs,rr,abs,uslas,fromr,rr,xorr,pm,swap
        .word     fromr,pm,swap,semis
        .byte     201
        .ascii    "*"
        .word     mslas-6
star:   .word     docol,mstar,drop,semis
        .byte     204
        .ascii    "/MOD"
        .even
        .word     star-4
slmod:  .word     docol,tor,stod,fromr,mslas,semis
        .byte     201
        .ascii    "/"
        .word     slmod-10
slash:  .word     docol,slmod,swap,drop,semis
        .byte     203
        .ascii    "MOD"
        .word     slash-4
modd:   .word     docol,slmod,drop,semis
        .byte     205
        .ascii    "*/MOD"
        .word     modd-6
ssmod:  .word     docol,tor,mstar,fromr,mslas,semis
        .byte     202
        .ascii    "*/"
        .even
        .word     ssmod-10
ssla:   .word     docol,ssmod,swap,drop,semis
        .byte     205
        .ascii    "M/MOD"
        .word     ssla-6
msmod:  .word     docol,tor,zero,rr,uslas,fromr,swap,tor,uslas,fromr,semis
        .byte     206
        .ascii    "(LINE)"
        .even
        .word     msmod-10
pline:  .word     docol,tor,lit,100,bbuf,ssmod,fromr,bscr,star,plus,block
        .word     plus,lit,100,semis
        .byte     205
        .ascii    ".LINE"
        .word     pline-12
dline:  .word     docol,pline,dtrai,type,semis
        .byte     207
        .ascii    "MESSAGE"
        .word     dline-10
mess:   .word     docol,warn,at,zbran,mess1-.,ddup,zbran,mess2-.,lit,4,ofset
        .word     at,bscr,slash,subb,dline,space
mess2:  .word     bran,mess3-.
mess1:  .word     pdotq
        .byte     6
        .ascii    "MSG # "
        .even
        .word     dot
mess3:  .word     semis
        .byte     215
        .ascii    "EMPTY-BUFFERS"
        .word     mess-12
mtbuf:  .word     docol,first,limit,over,subb,erasee,semis
        .byte     301
        .ascii    "'"
        .word     mtbuf-20
tick:   .word     docol,dfind,zequ,zero,qerr,drop,liter,semis
        .byte     206
        .ascii    "FORGET"
        .even
        .word     tick-4
forg:   .word     docol,curr,at,cont,at,subb,lit,30,qerr,tick,dup,fence
        .word     at,less,lit,25,qerr,dup,nfa,dp,store,lfa,at,cont,at,store,semis
        .byte     204
        .ascii    "BACK"
        .even
        .word     forg-12
back:   .word     docol,here,subb,comma,semis
        .byte     305
        .ascii    "BEGIN"
        .word     back-10
begin:  .word     docol,qcomp,here,one,semis
        .byte     305
        .ascii    "ENDIF"
        .word     begin-10
endiff: .word     docol,qcomp,two,qpair,here,over,subb,swap,store,semis
        .byte     304
        .ascii    "THEN"
        .even
        .word     endiff-10
then:   .word     docol,endiff,semis
        .byte     302
        .ascii    "DO"
        .even
        .word     then-10
do:     .word     docol,comp,xdo,here,three,semis
        .byte     304
        .ascii    "LOOP"
        .even
        .word     do-6
loop:   .word     docol,three,qpair,comp,xloop,back,semis
        .byte     305
        .ascii    "+LOOP"
        .word     loop-10
ploop:  .word     docol,three,qpair,comp,xploo,back,semis
        .byte     305
        .ascii    "UNTIL"
        .word     ploop-10
until:  .word     docol,one,qpair,comp,zbran,back,semis
        .byte     303
        .ascii    "END"
        .word     until-10
endd:   .word     docol,until,semis
        .byte     305
        .ascii    "AGAIN"
        .word     endd-6
again:  .word     docol,one,qpair,comp,bran,back,semis
        .byte     306
        .ascii    "REPEAT"
        .even
        .word     again-10
repea:  .word     docol,tor,tor,again,fromr,fromr,two,subb,endiff,semis
        .byte     302
        .ascii    "IF"
        .even
        .word     repea-12
iff:    .word     docol,comp,zbran,here,zero,comma,two,semis
        .byte     304
        .ascii    "ELSE"
        .even
        .word     iff-6
elsee:  .word     docol,two,qpair,comp,bran,here,zero,comma,swap,two,endiff,two,semis
        .byte     305
        .ascii    "WHILE"
        .word     elsee-10
while:  .word     docol,iff,twop,semis
        .byte     206
        .ascii    "SPACES"
        .even
        .word     while-10
spacs:  .word     docol,zero,max,ddup,zbran,spax1-.,zero,xdo
spax2:  .word     space,xloop,spax2-.
spax1:  .word     semis
        .byte     202
        .ascii    "<#"
        .even
        .word     spacs-12
bdigs:  .word     docol,pad,hld,store,semis
        .byte     202
        .ascii    "$>"
        .even
        .word     bdigs-6
edigs:  .word     docol,drop,drop,hld,at,pad,over,subb,semis
        .byte     204
        .ascii    "SIGN"
        .even
        .word     edigs-6
sign:   .word     docol,rot,zless,zbran,sign1-.,lit,55,hold
sign1:  .word     semis
        .byte     201
        .ascii    "#"
        .word     sign-10
dig:    .word     docol,base,at,msmod,rot,lit,11,over,less,zbran,dig1-.,lit,7,plus
dig1:   .word     lit,60,plus,hold,semis
        .byte     202
        .ascii    "#S"
        .even
        .word     dig-4
digs:   .word     docol
digs1:  .word     dig,over,over,orr,zequ,zbran,digs1-.,semis
        .byte     203
        .ascii    "D.R"
        .word     digs-6
ddotr:  .word     docol,tor,swap,over,dabs,bdigs,digs,sign,edigs,fromr,over,subb
        .word     spacs,type,semis
        .byte     202
        .ascii    ".R"
        .even
        .word     ddotr-6
dotr:   .word     docol,tor,stod,fromr,ddotr,semis
        .byte     202
        .ascii    "D."
        .even
        .word     dotr-6
ddot:   .word     docol,zero,ddotr,space,semis
        .byte     201
        .ascii    "."
        .word     ddot-6
dot:    .word     docol,stod,ddot,semis
        .byte     201
        .ascii    "?"
        .word     dot-4
ques:   .word     docol,at,dot,semis
        .byte     202
        .ascii    "U."
        .even
        .word     ques-4
udot:   .word     docol,zero,ddot,semis
        .byte     205
        .ascii    "VLIST"
        .word     udot-6
vlist:  .word     docol,lit,200,outt,store,cont,at,at
vlis1:  .word     outt,at,csll,great,zbran,vlis2-.,cr,zero,outt,store
vlis2:  .word     dup,iddot,space,space,pfa,lfa,at,dup,zequ,qterm,orr,zbran,vlis1-.,drop,semis
        .byte     203
        .ascii    "BYE"
        .word     vlist-10
bye:    .word     docol,flush,iochan,at,close,ifile,at,close,ofile,at,close,dbye
        .byte     205
        .ascii    "(BYE)"
        .word     bye-6
dbye:   .word     .+2
        .exit
        .byte     204
        .ascii    "LIST"
        .even
        .word     dbye-10
list:   .word     docol,dec,cr,dup,scr,store,pdotq
        .byte     6
        .ascii    "SCR # "
        .even
        .word     dot,lit,20,zero,xdo
list1:  .word     cr,ido,lit,3,dotr,space,ido,scr,at,dline,qterm,zbran,list2-.,leave
list2:  .word     xloop,list1-.,cr,semis
        .byte     205
        .ascii    "INDEX"
        .word     list-10
index:  .word     docol,emit,cr,onep,one,swap,xdo
inde1:  .word     cr,ido,lit,3,dotr,space,zero,ido,dline,qterm,zbran,inde2-.,leave
inde2:  .word     xloop,inde1-.,semis
        .byte     205
        .ascii    "TRIAD"
        .word     index-10
triad:  .word     docol,lit,3,slash,lit,3,star,lit,3,over,plus,swap,xdo
tria1:  .word     cr,ido,list,qterm,zbran,tria2-.,leave
tria2:  .word     xloop,tria1-.,cr,lit,17,mess,cr,semis
        .byte     204
        .ascii    ".CPU"
        .even
        .word     triad-10
dotcpu: .word     docol,base,at,lit,44,base,store,lit,44,porig,tat,ddot,base,store,semis


; RSTS/E Interface routines - Disc I/O

        .byte     203
        .ascii    "USE"
        .word     dotcpu-10
use:    .word     dovar,buf1
        .byte     206
        .ascii    "IOCHAN"
        .even
        .word     use-6
iochan: .word     dovar,2
        .byte     205
        .ascii    "IFILE"
        .word     iochan-12
ifile:  .word     dovar,3
        .byte     205
        .ascii    "OFILE"
        .word     ifile-10
ofile:  .word     dovar,4
        .byte     204
        .ascii    "SIZE"
        .even
        .word     ofile-10
size:   .word     dovar,20
        .byte     204
        .ascii    "PREV"
        .even
        .word     size-10
prev:   .word     dovar,buf1
        .byte     205
        .ascii    "#BUFF"
        .word     prev-10
nobuf:  .word     docon,nbuf
        .byte     204
        .ascii    "+BUF"
        .even
        .word     nobuf-10
pbuf:   .word     docol,lit,co,plus,dup,limit,equal,zbran,pbuf1-.,drop,first
pbuf1:  .word     dup,prev,at,subb,semis
        .byte     206
        .ascii    "UPDATE"
        .even
        .word     pbuf-10
updat:  .word     docol,prev,at,at,lit,100000,orr,prev,at,store,semis
        .byte     206
        .ascii    "BUFFER"
        .even
        .word     updat-12
buffe:  .word     docol,use,at,dup,tor
buff1:  .word     pbuf,zbran,buff1-.,use,store,rr,at,zless,zbran,buff2-.
        .word     rr,twop,rr,at,lit,77777,andd,zero,iochan,at,rslw
buff2:  .word     rr,store,rr,prev,store,fromr,twop,semis
        .byte     205
        .ascii    "BLOCK"
        .word     buffe-12
block:  .word     docol,dup,zequ,zbran,bloc4-.,drop,lit,6,error
bloc4:  .word     ofset,at,plus,tor,prev,at,dup,at,rr,subb,dup,plus,zbran
        .word     bloc1-.
bloc2:  .word     pbuf,zequ,zbran,bloc3-.,drop,rr,buffe,dup,rr,one
        .word     iochan,at,rslw
        .word     two,subb
bloc3:  .word     dup,at,rr,subb,dup,plus,zequ,zbran,bloc2-.,dup,prev,store
bloc1:  .word     fromr,drop,twop,semis
        .byte     203
        .ascii    "R/W"
        .word     block-10
rslw:   .word     .+2
        mov       (sp)+,r3
        add       r3,r3
        mov       (sp)+,r2
        mov       (sp)+,xrb+xrblk
        mov       #512.,xrb+xrlen
        clr       xrb+xrbc
        mov       (sp)+,xrb+xrloc
        mov       r3,xrb+xrci
        clrb      xrb+xrblkm
        tst       r2
        bne       read
        mov       #512.,xrb+xrbc
        .write
        br        rslw1
read:   .read
rslw1:  tstb      firqb
        beq       rslw2
        mov       #6,-(sp)
        mov       #rslw3,r0
rslw2:  jmp       next
rslw3:  .word     zero,prev,at,store,error
        .byte     205
        .ascii    "NFILE"
        .word     rslw-6
nfile:  .word     docol,iochan,store,mtbuf,use,at,size,at,zero,xdo
nfil1:  .word     dup,ido,zero,iochan,at,rslw,xloop,nfil1-.,drop,semis
        .byte     205
        .ascii    "FLUSH"
        .word     nfile-10
flush:  .word     docol,nobuf,onep,zero,xdo
flus1:  .word     zero,buffe,drop,xloop,flus1-.,semis
        .byte     304
        .ascii    "OPEN"
        .even
        .word     flush-10
open:   .word     docol,state,at,zbran,open3-.,comp,popen,lit,15,word
        .word     here,cat,onep,allot,bran,open1-.
open3:  .word     dup,close,clrfqb
        .word     chan,lit,15,word,here,count,fname,dopen,zequ
        .word     zbran,open1-.
open4:  .word     size,at,fsize
        .word     crefil,dopen,zbran,open2-.
open1:  .word     semis
open2:  .word     lit,10,error
        .byte     206
        .ascii    "[OPEN]"
        .even
        .word     open-10
popen:  .word     docol,rr,count,dup,onep,even,fromr,plus,tor,rot,dup,close
        .word     clrfqb,chan,fname,dopen,zequ,zbran,open1-.,bran,open4-.
        .byte     205
        .ascii    "FSIZE"
        .word     popen-12
fsize:  .word     .+2
        mov       (sp)+,firqb+fqsiz
        clrb      firqb+fqsizm
        jmp       next
        .byte     207
        .ascii    "CHANNEL"
        .word     fsize-10
chan:   .word     .+2
        mov       (sp)+,r2
        add       r2,r2
        mov       r2,firqb+fqfil
        jmp       next
        .byte     207
        .ascii    "CREFILE"
        .word     chan-12
crefil: .word     .+2
        movb      #crefq,firqb+fqfun
        jmp       next
        .byte     206
        .ascii    "(OPEN)"
        .even
        .word     crefil-12
dopen:  .word     .+2
        clr       firqb+fqmode
        clrb      firqb+fqprot
        mov       #2,firqb+fqclus
        clr       firqb+fqnent
        calfip
        mov       #1,r2
        tstb      firqb
        beq       dopen1
        clr       r2
dopen1: jmp       hpush
        .byte     205
        .ascii    "FNAME"
        .word     dopen-12
fname:  .word     .+2
        mov       (sp),xrb+xrbc
        mov       (sp)+,xrb+xrlen
        mov       (sp)+,xrb+xrloc
        .fss
        movb      #opnfq,firqb+fqfun
        jmp       next
        .byte     206
        .ascii    "CLRFQB"
        .even
        .word     fname-10
clrfqb: .word     .+2
        mov       #36,r5
clrl:   mov       r5,r4
        add       #firqb,r4
        clrb      (r4)
        sob       r5,clrl
        jmp       next
        .byte     205
        .ascii    "CLOSE"
        .word     clrfqb-12
close:  .word     .+2
        mov       (sp)+,r2
        add       r2,r2
        movb      #clsfq,firqb+fqfun
        movb      r2,firqb+fqfil
        calfip
        tstb      firqb
        beq       clos1
        mov       #10,-(sp)
        mov       #error,r0
clos1:  jmp       next
        .byte     207
        .ascii    "IBUFFER"
        .word     close-10
ibuf:   .word     docon,buf1-160.
        .byte     207
        .ascii    "OBUFFER"
        .word     ibuf-12
obuf:   .word     docon,buf1-80.
        .byte     204
        .ascii    "LOAD"
        .even
        .word     obuf-12
load:   .word     docol,blk,at,tor,inn,at,tor,zero,inn,store,bscr,star,blk,store
        .word     inter,fromr,inn,store,fromr,blk,store,semis
        .byte     205
        .ascii    "READF"
        .word     load-10
readf:  .word     docol,pdotq
        .byte     19.
        .ascii    "Not Yet Implemented"
        .word     semis
        .byte     206
        .ascii    "WRITEF"
        .even
        .word     readf-10
writef: .word     docol,pdotq
        .byte     19.
        .ascii    "Not Yet Implemented"
        .word     semis
        .byte     303
        .ascii    "-->"
        .word     writef-12
arrow:  .word     docol,qload,zero,inn,store,bscr,blk,at,block,over,modd
        .word     subb,blk,pstor,semis
        .byte     204
        .ascii    "TASK"
        .even
        .word     arrow-6
task:   .word     docol,semis



;       RSTS/E Interface Routines - Terminal I/O

pgkey:  mov       r0,-(sp)
        call      set
        clr       xrb+xrbc
        .ttddt
        .read
        movb       buffer,r2
        mov       (sp)+,r0
        jmp       hpush
pemit:  .word     .+2
        movb       (sp),buffer
        mov       r0,(sp)
        call      set
        .write
        mov       (sp)+,r0
        jmp       next
pcr:    mov       r0,-(sp)
        movb       #10.,buffer
        call      set
        .write
        movb       #13.,buffer
        call      set
        .write
        mov       (sp)+,r0
        jmp       next
buffer: .word     0
set:    mov       #1,xrb+xrlen
        mov       #1,xrb+xrbc
        mov       #buffer,xrb+xrloc
        clr       xrb+xrci
        clr       xrb+xrtime
        clr       xrb+xrblk
        clr       xrb+xrmod
        return
initdp: .blkw     1000
        .end      orig
