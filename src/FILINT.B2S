1	ON ERROR GOTO 32767 &

50	&
	!	D I M E N S I O N   S T A T E M E N T S &
	&
	&
	DIM	POINT.2$(133%), &
	&
		I.D.NO.3$(7%),DEWEY.3$(7%),TITLE.3$(7%),AUTHOR.3$(7%), &
		SUBJECT.3$(7%),COMMENT.3$(7%),PAGES.3$(7%),FLAG.3$(7%), &
		ILLUS.3$(7%),TICKET.3$(7%),OUT.3$(7%),RES.3$(7%),DATE.3$(7%), &
		I.S.B.N.3$(7%),PUB.3$(7%),REC.3$(7%), &
	&
		INDEX.4$(15%),I.D.NO.4$(15%),AUTHOR.4$(15%), &
		SEC.AUTH.4$(15%),AUTH.FLAG.4$(15%),REC.4$(15%), &
	&
		INDEX.5$(9%),I.D.NO.5$(9%),TITLE.5$(9%), &
		REC.5$(9%), &
	&
		INDEX.6$(15%),I.D.NO.6$(15%), &
		INDEX.PTR.6$(15%,6%),FORWARD.PTR.6$(15%,6%), &
		REC.6$(15%), &
	&
		INDEX.7$(9%),I.D.NO.7$(9%),NEXT.PTR.7$(9%), &
		COMMENT.7$(9%),REC.7$(9%), &
	&
		ACCESS.NO.8$(255%), &
	&
		REC.9$(15%) , &
	&
		FIRST.PTR.9$(15%),LAST.PTR.9$(15%),DEWEY.9$(15%), &
		COUNT.9$(15%), &
		SELF.PTR.9$(15%),SUBJECT.9$(15%), &
	&
		I.D.NO.10$(1%),AUTHOR.10$(1%),TITLE.10$(1%),PUB.10$(1%), &
		I.S.B.N.10$(1%),DATE.10$(1%),CLASS.10$(1%),INDEX.10$(1%), &
	&
		! THESE ARRAYS APPEAR IN THE FIELD STATEMENTS. &
	&
		SUBJECT$(6%) &
	&

70	PRIV.ON$,PRIV.OFF$=CHR$(0%) &

71	GOSUB 29500 &

100 &
	!	F i e l d   s t a t e m e n t s &
	&
	&
	!	P O I N T . L I B &
	&
	FIELD #2%, &
		2%*UT% AS DUMMY$, &
		2% AS POINT.2$(UT%) &
			FOR UT%=0% TO 133% &
	&

110	&
	!	B O O K S . L I B &
	&
	FIELD #3%, &
		64%*UT%	AS DUMMY$,	! Dummy. &
		2%	AS I.D.NO.3$(UT%),	! Accession number of book. &
		4%	AS DEWEY.3$(UT%),	! Dewey number. &
		2%	AS TITLE.3$(UT%),	! Pointer to title file. &
		2%	AS AUTHOR.3$(UT%),	! Pointer to author file. &
		2%	AS SUBJECT.3$(UT%),	! Pointer to subject file. &
		2%	AS COMMENT.3$(UT%),	! Poiner to comment file. &
		2%	AS PAGES.3$(UT%),	! No. of pages. &
		1%	AS FLAG.3$(UT%),	! General flag: &
					! Bit value	Meaning &
					!	0	Loan &
					!	1	Short term loan &
					!	2	Ref. only &
					!	3	Quick ref. &
					!	4	Special collection &
					!	5,6,7	Unused &
					&
					!	+8	Newly-Arrived &
					!	+16	On order. (Special format) &
					!	+32	Under repair &
					!	+64	Missing &
					!	+128	Modified since last listing. &
					&
		1%	AS ILLUS.3$(UT%),	! Number of illustrations. &
		2%	AS TICKET.3$(UT%),	! Borrower who has book out. &
		2%	AS OUT.3$(UT%),	! Date due back & pointer to Order file. &
		4%	AS RES.3$(UT%),	! Borrower who has it reserved, &
					! and date it became available. &
		4%	AS DATE.3$(UT%), &
				! Date this edition printed + 1500 * ( Date &
				! this edition published + 1500 * Date of &
				! first edition ). Dates stored are the &
				! real Year - 1000. &
		26%	AS PUB.3$(UT%),		! publisher &
		7%	AS I.S.B.N.3$(UT%),	! I.S.B.N. Number. &
		1% 	AS TICKET.CNT.3$(UT%)	! Ticket index. &
			FOR UT%=0% TO 7% &
	&
	\ FIELD #3%,64%*UT% AS DUMMY$,64% AS REC.3$(UT%) FOR UT%=0% TO 7% &
		! Whole record. &
	&

120	&
	!	A U T H O R . L I B &
	&
	FIELD #4%, &
		(32%*UT%) AS DUMMY$, &
		2% AS INDEX.4$(UT%),	! Record No. in BOOK.LIB. &
		2% AS I.D.NO.4$(UT%),	! Unique accession No. &
		2% AS SEC.AUTH.4$(UT%),	! Second author pointer or 0. &
		1% AS AUTH.FLAG.4$(UT%),! Flag for two authors. &
		25% AS AUTHOR.4$(UT%)	! The author name itself &
			FOR UT%=0% TO 15% &
	&
	\ FIELD #4%,32%*UT% AS DUMMY$,32% AS REC.4$(UT%) FOR UT%=0% TO 15% &
		! Whole record. &
	&

130	&
	!	T I T L E . L I B &
	&
	FIELD #5%, &
		(51%*UT%) AS DUMMY$, &
		2% AS INDEX.5$(UT%),	! Record No. in BOOK.LIB. &
		2% AS I.D.NO.5$(UT%),	! Unique accession No. &
		47% AS TITLE.5$(UT%)	! The title name itself &
			FOR UT%=0% TO 9% &
	&
	\ FIELD #5%,51%*UT% AS DUMMY$,51% AS REC.5$(UT%) FOR UT%=0% TO 9% &
		! Whole record. &
	&

140	&
	!	S U B J C T . L I B    &
	&
	FOR UT%=0% TO 15% &
	\ FIELD #6%, &
		(32%*UT%) AS DUMMY$, &
		2% AS INDEX.6$(UT%),	! Record number in BOOKS.LIB &
		2% AS I.D.NO.6$(UT%)	! Unique accession number &
	\ FIELD #6%, &
		(32%*UT%+4%+4%*K%) AS DUMMY$, &
		2% AS INDEX.PTR.6$(UT%,K%),	! Pointer into INDEX.LIB &
		2% AS FORWARD.PTR.6$(UT%,K%)	! Pointer to next book with this subject &
			FOR K%=0% TO 6% &
	\ NEXT UT% &

150	&
	!	C O M E N T . L I B &
	&

160	&
	!	A C C E S S . L I B &
	&
	FIELD #8%, &
		2%*UT% AS DUMMY$, &
		2% AS ACCESS.NO.8$(UT%) &
			FOR UT%=0% TO 255% &
	&

170	&
	!	I N D E X . L I B      &
	&
	FIELD #9%, &
		(32%*UT%) AS DUMMY$, &
		2% AS FIRST.PTR.9$(UT%),	! Posn. in #6 of first book with this subject. &
		2% AS LAST.PTR.9$(UT%),	! Posn. in #6 of last book with this subject. &
		4% AS DEWEY.9$(UT%),	! Dewey number of this subject. &
		2% AS SELF.PTR.9$(UT%),	! Record no. of this record in #9 for sorting. &
		2% AS COUNT.9$(UT%),	! Number of books with this subject. &
		20% AS SUBJECT.9$(UT%)	! Subject text itself. &
			FOR UT%=0% TO 15% &

180	&
	!	O R D E R . L I B  &
	&
	FIELD #10%, &
		256% * UT% AS DUMMY$, &
		2% AS I.D.NO.10$(UT%), 		! Accession (Order) number. &
		2% AS INDEX.10$(UT%), 		! Posn. in BOOKS.LIB. &
		56% AS AUTHOR.10$(UT%), 	! Author(s). &
		47% AS TITLE.10$(UT%), 		! Title. &
		28% AS PUB.10$(UT%), 		! Publisher. &
		7% AS I.S.B.N.10$(UT%), 	! I.S.B.N. Number. &
		3% AS DATE.10$(UT%), 		! Date of this order in dd.mm.yy format. &
		1% AS ARRIVED.10$(UT%), 	! Flag:	0%=this is an order. &
						!	1%=this order has arrived. &
						!	255%=This record has been deleted. &
		2% AS PRICE.10$(UT%),		! Price of book or 0. &
		108% AS CLASS.10$(UT%) 		! Classification of order. &
			FOR UT%=0% TO 1% &

901	dim month.name$(11%) &
	\ data "Jan","Feb","Mar","Apr","May","Jun","Jul","Aug","Sep","Oct","Nov","Dec" &

902	read month.name$(i%) for i%=0% to 11% &
	&
	\ dim d.in.month%(11%) &
	\ data 31,28,31,30,31,30,31,31,30,31,30,31 &

903	read d.in.month%(i%) for i%=0% to 11% &

950 ZERO$ = CVT%$(0%) &

1000	MAP (BOOKS) ACCESSION$=2%,	! Accession number &
		    DEWEY$=82%,		! Dewey number or F for fiction &
		    ISBN$=10%,		! ISBN number &
		    TITLE$=72%,		! Title &
		    PUBLISHER$=26%,	! Publisher &
		    FLAG%,		! General flag word &
		    ILLUS%,		! No. of illustrations &
		    PAGES%,		! No. of pages &
		    FIRST.PUB%,		! Date first published &
		    THIS.PUB%,		! Date this edition published &
		    THIS.PRINT%		! Date this edition printed &

1010	MAP (AUTHOR) A.ACCESSION$=2%,	! Accession number &
		     A.AUTHOR$=72%	! Author for this book &
	&
	&
	! For multiple authors for one book have duplicate records. &

1020	MAP (SUBJCT) S.ACCESSION$=2%,	! Accession number &
		     S.SUBJECT$=20%	! This subject &
	&
	&
	! For multiple subjects for one book have multiple records &

2000	OPEN 'DP2:'+PKG.LOC$+'BOOKS .RMS' FOR INPUT AS FILE #1%, &
	ORGANIZATION INDEXED FIXED, &
	ACCESS WRITE, &
	ALLOW NONE, &
	MAP BOOKS, &
	PRIMARY KEY ACCESSION$ NODUPLICATES, &
	ALTERNATE KEY TITLE$ DUPLICATES CHANGES, &
	ALTERNATE KEY DEWEY$ DUPLICATES CHANGES &

3000	PRINT 'Ok, transferring books...' &
\	PRINT &
\	input tr.start.,tr.end. &
\	FOR TR.ACC.=tr.start. TO tr.end. &
\	J%=FNGET%(TR.ACC.) &
\	GOTO 3500 IF J% &
\	TITLE$=TITLE.5$(PTR.5%) &
\	TITLE$=LEFT(TITLE$,47%)+AUTHOR1$ &
\	ACCESSION$=CVT%$(INT(TR.ACC.)-32768.) &
\	ISBN$=CVT$$(I.S.B.N$,255%) &
\	DEWEY$=CVT$$(FNDEWEY$(FNDEWEY(DEWEY.3$(PTR.3%))),255%) &
\	DEWEY$=LEFT(DEWEY$,10%)+AUTHOR1$ &
\	DEWEY$=LEFT(DEWEY$,35%)+LEFT(TITLE$,47%) &
\	PAGES%=FNFL(CVT$%(PAGES.3$(PTR.3%)))-32767. &
\	J%=(ASCII(FLAG.3$(PTR.3%)) AND 7%) &
\	J1%=ASCII(FLAG.3$(PTR.3%)) &
\	FLAG%=0% &
\	FLAG%=FLAG% OR 1% IF J%=0% &
\	FLAG%=FLAG% OR 2% IF J%=1% &
\	FLAG%=FLAG% OR 4% IF J%=2% &
\	FLAG%=FLAG% OR 8% IF J%=3% &
\	FLAG%=FLAG% OR 16% IF J%=4% &
\	FLAG%=FLAG% OR 2%^10% IF J1% AND 32% &
\	FLAG%=FLAG% OR 2%^11% IF J1% AND 64% &
\	THIS.PUB%=YEAR2 &
\	THIS.PUB%=0% IF YEAR2=1000. &
\	THIS.PRINT%=YEAR1 &
\	THIS.PRINT%=0% IF YEAR1=1000. &
\	FIRST.PUB%=YEAR3 &
\	FIRST.PUB%=0% IF YEAR3=1000. &
\	ILLUS%=ASCII(ILLUS.3$(PTR.3%)) &
\	ORDER.DATE$='0'+ORDER.DATE$ IF LEN(ORDER.DATE$)<9% &
\	DEWEY$=ORDER.DATE$ IF ON.ORDER% &
\	FLAG%=FLAG% OR 2%^9% IF ON.ORDER% &
\	FLAG%=FLAG% OR 2%^8% IF NEWLY.ARRIVED% &
\	PUBLISHER$=PUB.3$(PTR.3%) &
\	PUT #1% &

3020	PRINT TR.ACC. &

3500	NEXT TR.ACC. &
\	CLOSE I% FOR I%=1% TO 15% &
\	GOTO 32767 &

20000	DEF * FNGET%(ACCESS.NO) &
	&
	!	F N G E T % ( A C C E S S . N O ) &
	&
	! This function gets all the data we have on the book with &
	! accession number ACCESS.NO, if such a book exists. &
	&
	! It performs internal caching on all GET's, and unlocks all &
	! blocks directly after getting them SO BEWARE.... &
	&
	! FNGET%()= 0% . . . . . Success, book found and valid. &
	! FNGET%()= 2% . . . . . No book with that accession number. &
	! FNGET%()=-1% . . . . . Files are corrupt, accession numbers &
	!			 are inconsistent. &
	&
	! The data found is returned :- &
	&
	!	TITLE and AUTHOR . . . . . . In TITLE.5%(PTR.5%) and &
	!				     AUTHOR1/2$, trailing &
	!				     spaces included. &
	!	SUBJECT KEYWORDS . . . . . . Number of keywords in N.SUBJECTS.OLD% (0-7) &
	!				     Each keyword in SUBJECT$(0%->6%) &
	!				     (Not nec. in any given position.) &
	!				     List, sep. by "," in SUBJECT$,SUBJECT.OLD$ &
	!	COMMENTS . . . . . . . . . . Not read, POS.7 set up to either &
	!				     0 or the record # of the first &
	!				     comment record. Can't read them &
	!				     into a string as they can be &
	!				     of unlimited length. &
	!	I.S.B.N. . . . . . . . . . . In printing form in I.S.B.N$. &
	!	DATES(NOT ORDERS). . . . . . Year this Ed. printed in YEAR1 &
	!				     Year this Ed. published in YEAR2 &
	!				     Year first Ed. published in YEAR3. &
	!				     (Year is 1000. -> N/A) &
	!	DATE(ORDERS) . . . . . . . . In printing format in ORDER.DATE$ &
	&
	! The following flags are set: &
	&
	!	SEC.AUTH%  . . . . . . . . . This book has a second author. &
	!	ON.ORDER%  . . . . . . . . . This book is on order. &
	!	NEWLY.ARRIVED% . . . . . . . This book is newly arrived. &
	&
	&
	\ FNGET%,N.SUBJECTS.OLD%=0% &
	\ SUBJECT$="" &
		! Zero things out. &
	&

20010	&
	!	First find the position of the header for this book &
	!	in BOOKS.LIB. &
	&
	BLCK8%=(ACCESS.NO-1.)/256.+1% &
	\ PTR.8%=ACCESS.NO-256.*BLCK8%+255% &
	&
	\ GET #8%, BLOCK BLCK8% UNLESS BLCK8%=OLD.BLCK8% &
	\ OLD.BLCK8%=BLCK8% &
	\ UNLOCK #8% &
	&
	\ POS.3=FNFL(CVT$%(ACCESS.NO.8$(PTR.8%))) &
	\ BLCK3%=(POS.3-1.)/8.+1% &
	\ IF POS.3=0. THEN &
		  FNGET%=2% 	! Flag no book with this accession No. &
		\ GOTO 20300 &

20020	CHECK.IT$=FNI$(ACCESS.NO) &
		! To check everything. &

20030	GET #3%,BLOCK BLCK3% UNLESS BLCK3%=OLD.BLCK3% &
	\ OLD.BLCK3%=BLCK3% &
	\ UNLOCK #3% &
	&
	\ PTR.3%=POS.3+7%-8%*BLCK3% &
	\ GOTO 20200 UNLESS I.D.NO.3$(PTR.3%)=CHECK.IT$ &
		! Get header, validate accession number. &
	&
	\ ON.ORDER%=(ASCII(FLAG.3$(PTR.3%)) AND 16% ) = 16% &
		! See if this is an order. &
	\ NEWLY.ARRIVED%=(ASCII(FLAG.3$(PTR.3%)) AND 8% ) = 8% &
		! See if this is a newly-arrived book. &

20040	POS.4=FNFL(CVT$%(AUTHOR.3$(PTR.3%))) &
	\ BLCK4.1%=(POS.4-1.)/16.+1% &
	&
	\ GET #4%, BLOCK BLCK4.1% UNLESS BLCK4.1%=OLD.BLCK4% &
	\ OLD.BLCK4%=BLCK4.1% &
	\ UNLOCK #4% &
	&
	\ PTR.4.1%=POS.4+15%-16%*BLCK4.1% &
	&
	\ GOTO 20200 UNLESS I.D.NO.4$(PTR.4.1%)=CHECK.IT$ &
		! Get entry in AUTHOR.LIB for the first author, &
		! Validate accession number. &
	&
	\ AUTHOR1$ = AUTHOR.4$(PTR.4.1%)+"" &
		! Put first author away so that we don't loose it &
		! when (if?) we get the second author. &
	\ SEC.AUTH% = (SEC.AUTH.4$(PTR.4.1%)<>ZERO$) &
		! Set flag for second author. &
	\ AUTHOR2$ = "" &
		! Zero out second author name. &
	\ AUTH2 = 0% &
		! Zero out record number of second author. &
	\ GOTO 20050 UNLESS SEC.AUTH% &
		! Skip getting second author if there isn't one. &
	\ AUTH2 = FNFL(CVT$%(SEC.AUTH.4$(PTR.4.1%))) &
		! Get the record number of the second author. &
	\ BLCK4.2% = (AUTH2 - 1%)/16% + 1% &
	\ PTR.4.2% = AUTH2 - 16%*BLCK4.2% + 15% &

20041	GET #4%,BLOCK BLCK4.2% UNLESS BLCK4.2%=OLD.BLCK4% &
	\ OLD.BLCK4%=BLCK4.2% &
	\ UNLOCK #4% &
	&
	\ GOTO 20200 UNLESS I.D.NO.4$(PTR.4.2%)=CHECK.IT$ &
		! Get second author record, check accession number. &
	\ AUTHOR2$ = AUTHOR.4$(PTR.4.2%)+"" &
		! And put it away here. &

20050	POS.5=FNFL(CVT$%(TITLE.3$(PTR.3%))) &
		! Get the pointer to the title record. &
	\ BLCK5%=(POS.5-1%)/10%+1% &
	&
	\ GET #5%, BLOCK BLCK5% UNLESS BLCK5%=OLD.BLCK5% &
	\ OLD.BLCK5%=BLCK5% &
	\ UNLOCK #5% &
	&
	\ PTR.5%=POS.5+9%-10%*BLCK5% &
	&
	\ GOTO 20200 UNLESS I.D.NO.5$(PTR.5%)=CHECK.IT$ &
		! Get the title, check accession number. &

20060	GOTO 20110 IF ON.ORDER% &
		! Don't try to get subject keywords if book on order. &
	&
	\ POS.6=FNFL(CVT$%(SUBJECT.3$(PTR.3%))) &
	\ GOTO 20070 IF POS.6=0. &
		! Get record number of subject header. 0 -> no keywords. &
	&
	\ BLCK6%=(POS.6-1%)/16%+1% &
	&

20061	GET #6%, BLOCK BLCK6% UNLESS BLCK6%=OLD.BLCK6% &
	\ OLD.BLCK6%=BLCK6% &
	\ UNLOCK #6% &
	&
	\ PTR.6%=POS.6+15%-16%*BLCK6% &
	&
	\ GOTO 20200 UNLESS I.D.NO.6$(PTR.6%)=CHECK.IT$ &
		! Get subject header, check accession number. &
	&

20065	FOR SUBJECT%=0% TO 6% &

20066	IF INDEX.PTR.6$(PTR.6%,SUBJECT%)<>ZERO$ THEN &
		  POS.9=FNFL(CVT$%(INDEX.PTR.6$(PTR.6%,SUBJECT%))) &
		\ BLCK9%=(POS.9-1.)/16%+1% &
		\ PTR.9%=POS.9-16.*BLCK9%+15% &
		&
		\ GET #9%, BLOCK BLCK9% UNLESS BLCK9%=OLD.BLCK9% &
		\ OLD.BLCK9%=BLCK9% &
		\ UNLOCK #9% &
		&
		\ SUBJECT$(N.SUBJECTS.OLD%),SUBJECT.OLD$(N.SUBJECTS.OLD%)=CVT$$(SUBJECT.9$(PTR.9%),128%) &
		\ SUBJECT$=SUBJECT$+SUBJECT$(N.SUBJECTS.OLD%)+"," &
		\ N.SUBJECTS.OLD%=N.SUBJECTS.OLD%+1% &

20067	NEXT SUBJECT% &
		! For each of the seven keys see if any of them point &
		! to INDEX.LIB ( <> 0. ). For those that do get the record. &
		! in INDEX.LIB and add that keyword to the keyword lists. &
		! (No checks poss.) &
	\ SUBJECT$,SUBJECT.OLD$=LEFT(SUBJECT$,LEN(SUBJECT$)-1%) &
		! Chop the last "," off the list. &

20070	POS.7=FNFL(CVT$%(COMMENT.3$(PTR.3%))) &
		! Get only the pointer to #7. &

20100	DTE=FNFL(CVT$%(DATE.3$(PTR.3%)))*65536.+FNFL(CVT$%(RIGHT( &
		DATE.3$(PTR.3%),3%))) &
	\ YEAR3=INT(DTE/2250000.) &
	\ YEAR2=INT((DTE-2250000.*YEAR3)/1500.) &
	\ YEAR1=DTE+1000.-1500.*YEAR2-2250000.*YEAR3 &
	\ YEAR2=YEAR2+1000. &
	\ YEAR3=YEAR3+1000. &
		! Decode information in DATE.3$(PTR.3%). &
	&

20110	I.S.B.N$="N/A" &
	\ GOTO 20120 IF CVT$$(I.S.B.N.3$(PTR.3%),4%)="" &
	\ I.S.B.N$="" &
	&
	\ FOR K%=1%TO 7% &
		\ K$=MID(I.S.B.N.3$(PTR.3%),K%,1%) &
		\ J1%=ASCII(K$) AND 15% &
		\ J2%=ASCII(K$)/16% &
		&
		\ J1$=CHR$(J1%+48%-30%*(J1%=10%)+27%*(J1%=11%)) &
		\ J2$=CHR$(J2%+48%-30%*(J2%=10%)+27%*(J2%=11%)) &
		&
		\ J1$="" IF J1$="<" &
		\ J2$="" IF J2$="<" &
		&
		\ I.S.B.N$=I.S.B.N$+J1$+J2$ &
	\ NEXT K% &
		! Decode the ISBN. &

20120	GOTO 20300 UNLESS ON.ORDER% OR NEWLY.ARRIVED% &
		! Got everything except for the order information. &
		! End if not on order or newly-arrived. &

20130	POS.10=FNFL(CVT$%(OUT.3$(PTR.3%))) &
	\ BLCK10%=(POS.10-1.)/2%+1% &
	&
	\ GET #10%, BLOCK BLCK10% UNLESS BLCK10%=OLD.BLCK10% &
	\ OLD.BLCK10%=BLCK10% &
	\ UNLOCK #10% &
	&
	\ PTR.10%=POS.10-2.*BLCK10%+1% &
	&
	\ GOTO 20200 UNLESS I.D.NO.10$(PTR.10%)=CHECK.IT$ &
		! Get order record, check accession number. &

20140	ORDER.DATE$= &
		  NUM1$(ASCII(DATE.3$(PTR.3%)))+"-" &
		+ MID("JanFebMarAprMayJunJulAugSepOctNovDec", &
			1%+3%*(ASCII(RIGHT(DATE.3$(PTR.3%),2%))-1%),3%) &
		+"-"+NUM1$(ASCII(RIGHT(DATE.3$(PTR.3%),3%))) &
		! Extract order date. &

20150	GOTO 20300 &
		! Finished for good. &

20200	PRINT '?Failed to find accession number';ACCESS.NO &
	\ FNGET%=2%	! Indicate corruption. &
		! Oh dear... &

20300	FNEND &
		! The end... &

21000	def * fndate$(dx%) &
	&
	! Convert a date from the special integer format to a &
	! printing string. &
	&
	\ dy%=(dx% and 32256%)/512%+1985% &
	\ dm%=(dx% and 480%)/32% &
	\ dm%=0% if dm%>11% &
	\ dd%=dx% and 31% &
	&
	\ fndate$=num1$(dd%)+"-"+ &
		  month.name$(dm%)+"-"+ &
		  right(num1$(dy%),3%) &
	&
	\ fnend &



24600	DEF * FNFL(I%) &
	&
	!	CONVERT AN INTEGER TO FLOATING POINT &
	&
	\ IF I%>=0% &
	THEN	FNFL=I% &
	ELSE	FNFL=32768.+(I% AND 32767%) &

24640	FNEND &

24650	DEF * FNDEWEY(D.DEWEY$)=(65536.*CVT$%(D.DEWEY$)+FNFL(CVT$%(RIGHT( &
		D.DEWEY$,3%))))/1000000. &
	&
	!	F N D E W E Y ( D E W E Y $ ) &
	&
	! Extract a Dewey number from the format it is stored in in the files &
	&
	&
	\ DEF * FNDEWEY$(DEWEY) &
	&
	!	F N D E W E Y $ ( D E W E Y ) &
	&
	! Convert a dewey number to the form it is displayed in. &
	&
	\ IF DEWEY=1000. &
	THEN	D.DEWEY$="" &
	ELSE	IF DEWEY>1000. &
		THEN	D.DEWEY$=CHR$(DEWEY-936%) &
		ELSE	D.DEWEY$=NUM1$(DEWEY) &
			\ I%=INSTR(1%,D.DEWEY$+".",".") &
			\ D.DEWEY$=STRING$(4%-I%,48%)+D.DEWEY$ IF I%<4% &
			\ I%=INSTR(1%,D.DEWEY$,".") &
			\ IF I% &
			THEN	UT%=LEN(D.DEWEY$) &
				\ WHILE RIGHT(D.DEWEY$,UT%)="0" AND UT%>I% &
				\ D.DEWEY$=LEFT(D.DEWEY$,UT%-1%) &
				\ UT%=LEN(D.DEWEY$) &
				\ NEXT &
					! Remove trailing zeros after the &
					! decimal point. &

24655	IF LEFT(D.DEWEY$,6%)="822.33"  &
		THEN I%=(VAL(D.DEWEY$)-822.33)*10000% &
		\ IF I%>64% AND I%<91% AND UT%<9% &
			THEN D.DEWEY$="822.33"+CHR$(I%) &

24660	FNDEWEY$=D.DEWEY$ &
	\ FNEND &

24670	DEF * FNI$(FL) &
	&
	!	F  N  I  $  (  F  L  ) &
	&
	\ IF FL >32767%  &
	THEN	I%=FL-65536. &
	ELSE	I%=FL &

24680	FNI$=CVT%$(I%) &
	\ FNEND &

28000	def * fncd$(n.o) &
	&
	! Generate a 1 character check digit form N.O. &
	! The check digit is calculated as: &
	!  1*(first digit)+2*(second digit)+3*(... &
	!  with the number treated as a 5 character string (leading zeros) &
	! The check digit is the sum mod. 26 represented as a letter (A->Z). &
	&
	\ cd1$=num1$(n.o) &
	\ cd1$=string$(5%-len(cd1$),48%)+cd1$ &
		! Put in leading zeros. &
	\ cd1%=0% &
	&
	\ cd1%=cd1%+i%*(ascii(mid(cd1$,i%,1%))-48%) for i%=1% to 5% &
	&
	\ cd2%=cd1%/26% &
	\ cd1%=cd1%-26%*cd2% &
	&
	\ fncd$=chr$(cd1%+65%) &
	&
	\ fnend &

28010	def * fnprcd$(n.o) &
	&
	! Returns n.o. as a string with check digit, as for ticket numbers. &
	! Format: 123-34X. &
	&
	\ cd2$=num1$(n.o) &
	\ cd2$=string$(5%-len(cd2$),48%)+cd2$ &
	\ fnprcd$=left(cd2$,3%)+"-"+right(cd2$,4%)+fncd$(n.o) &
	\ fnend &

29500	! &
	!	O p e n   f i l e s &
	!				- Alternative version - old one      &
	!				has duplicate line numbers when used &
	!				with certain functions. 	     &
	!								     &
	!					Mohit Dutta 24-Mar-86        &
	&
	&
	DUMMY$=SYS(PRIV.ON$) &
	&
	\ STR0$="DP2:[5,12]POINT.LIB" &
	\ OPEN STR0$ AS FILE 2%, MODE 4096%	! Pointers to data &
	\ STR0$="DP2:[5,12]ACCESS.LIB" &
	\ OPEN STR0$ AS FILE 8%, MODE 4096%	! Table of accession nos. &
	\ STR0$="DP2:[5,12]BOOKS.LIB" &
	\ OPEN STR0$ AS FILE 3%, MODE 4096%	! The catalogue &
	\ STR0$="DP2:[5,12]AUTHOR.LIB" &
	\ OPEN STR0$ AS FILE 4%, MODE 4096%	! List of authors &
	\ STR0$="DP2:[5,12]TITLE.LIB" &
	\ OPEN STR0$ AS FILE 5%, MODE 4096%	! List of titles &
	\ STR0$="DP2:[5,12]SUBJCT.LIB" &
	\ OPEN STR0$ AS FILE 6%, MODE 4096%	! List of subjects &
	\ STR0$="DP2:[5,12]COMENT.LIB" &
	\ OPEN STR0$ AS FILE 7%, MODE 4096%	! Comments about books &
	\ STR0$="DP2:[5,12]INDEX .LIB" &
	\ OPEN STR0$ AS FILE 9%, MODE 4096%	! SUBJECT INDEX &
	\ STR0$="DP2:[5,12]ORDER .LIB" &
	\ OPEN STR0$ AS FILE 10%, MODE 4096%	! ORDER WORKFILE &
	&
	\ DUMMY$=SYS(PRIV.OFF$) &
	&
	\ RETURN &

32767	RESUME 20200 IF ERR=11 &
\	RESUME 3500 IF ERL=3000 &
\	PRINT ERT$(ERR);ERL &
\	END &

