1	ON ERROR GOTO 25000 &
	&

1000	MAP (BORROW) B.TICKET$=2%, &
		     B.SURNAME$=18%, &
		     B.FORENAME$=24%, &
		     B.FORM$=10%, &
		     B.BOOKS.OUT%, &
		     B.BOOKS.MAX%, &
		     B.FLAG% &

1010	MAP (LOANS) L.ACCESSION$=2%,L.TICKET$=2%,L.DUE%,L.FLAG% &

5000	J$=SYS(CHR$(12%)) &
\	PKG.LOC$='['+NUM1$(ASCII(RIGHT(J$,6%)))+','+NUM1$(ASCII(RIGHT(J$,5%))) &
	+']' &
\	CRLF$=CHR$(13%)+CHR$(10%) &
\	OPEN '_KB:' AS FILE #1% &
\	PRINT #1%, CRLF$;'Vet BORROW.RMS vs LOANS.RMS' &
\	PRINT #1%,       '---------------------------';CRLF$ &

5030	OPEN 'DP2:'+PKG.LOC$+'BORROW.RMS' AS FILE #9%, &
	ORGANIZATION INDEXED FIXED, &
	MAP BORROW, &
	ACCESS MODIFY, &
	ALLOW MODIFY, &
	PRIMARY KEY B.TICKET$ NODUPLICATES, &
	ALTERNATE KEY B.SURNAME$ DUPLICATES CHANGES &
\	OPEN 'DP2:'+PKG.LOC$+'LOANS.RMS' AS FILE #10%, &
	ORGANIZATION INDEXED FIXED, &
	MAP LOANS, &
	ACCESS MODIFY, &
	ALLOW MODIFY, &
	PRIMARY KEY L.TICKET$ DUPLICATES, &
	ALTERNATE KEY L.ACCESSION$ DUPLICATES &

6000	RESTORE #9% &

6010	GET #9% &

6020	C%=0% &
\	GET #10%, KEY #0% EQ B.TICKET$ &

6030	C%=C%+1% IF (L.FLAG% AND 1%)=0% &
\	GET #10% &
\	GOTO 6050 UNLESS L.TICKET$=B.TICKET$ &
\	GOTO 6030 &

6050	IF B.BOOKS.OUT%<>C% THEN &
	B.BOOKS.OUT%=C% &
\	UPDATE #9% &

6060	GOTO 6010 &

25000	! ERRORS &
	&
	&
	RESUME 6050 IF ERL=6030 AND ERR=11 &
\	RESUME 6050 IF ERL=6020 AND ERR=155 &
\	RESUME 32767 IF ERL=6010 AND ERR=11 &

28999	IF ERR=154 THEN &
	SLEEP 5% &
\	RESUME &

29000	PRINT #1%, CRLF$;'BORVET : ';ERT$(ERR);' at line';ERL &
\	RESUME 32767 &

32767	END &

