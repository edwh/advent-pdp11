1	SUB ADVDSP &
\	COMMON C%,NO.OF.USERS%,SENT.KB%,SENT.PROG%,SENT.PROJ%,USER%,USER1%,SINGLE.USER%, &
	FLAG%(8%),HP%(8%),LEVEL%(8%),KB%(8%),ROOM%(8%),WEAPON%(8%), &
	ATT.LEVEL%(10%),DEF.LEVEL%(10%),MON.HP%(10%),MON.DAM%(10%), &
	MON.ROOM%(10%),MON.XP%(10%),BLIND%(8%),SPY%(10%),FATIGUE%(10%), &
	XP.(8%),STUN(8%), &
	AR$=80%,C$=10%,CRLF$=2%,SENT.MESS$=80%,CI$=1%,RUN.ACC$=9%,MMM$=128%, &
	ACC$(8%)=7%,NAM$(8%)=30%,NPCMESS$(8%)=40%,OB$(8%,9%)=30%,PASS$(8%)=10%, &
	WEAPON$(8%)=20%,SPEC$(10%)=6% &
\	FIELD #3%, 1% AS ROOM$, 16% AS EX$, 83% AS PEOPLE$, 100% AS OBJECT$, &
	312% AS DESC$ &
\	DIM #5%, MONSTER$(10000%)=20% &
\	DIM #7%, CHARACTER$(100%)=512% &
\	ON ERROR GOTO 25000 &

15000	GET #3%, RECORD ROOM%(USER%) &
\	IF FLAG%(USER%) AND 2%^10% THEN &
	J%=INT(RND*5%)+1% &
\	READ J$ FOR I%=1% TO J% &
\	CALL ADVOUT(KB%(USER%),CHR$(24%)+J$) &
\	BLIND%(USER%)=BLIND%(USER%)-1% &
\	FLAG%(USER%)=FLAG%(USER%) XOR 2%^10% IF BLIND%(USER%)<=0% &
\	SUBEXIT &

15001	DES$=DESC$ &
\	DES$=LEFT(DES$,INSTR(1%,DES$,"$")-1%) IF INSTR(1%,DES$,"$") &
\	CALL ADVOUT(KB%(USER%),CHR$(24%)+DES$+CRLF$+CRLF$+'Exits :'+CRLF$) &
\	J$="" &
\	J$=J$+CI$+MID('NorthEast SouthWest ',INSTR(1%, &
	'NorthEast SouthWest ',MID(EX$,I%,1%)),5%)+CRLF$ &
	IF INSTR(1%,'NESW',MID(EX$,I%,1%)) FOR &
	I%=1% TO 16% STEP 4% &
\	CALL ADVOUT(KB%(USER%),J$+CRLF$+'You see:') &

15010	OBJ$=OBJECT$ &
\	OBJ1$="" &
\	UNTIL 0% &
\	GOTO 15020 UNLESS INSTR(1%,OBJ$,"/") &
\	OBJ1$=LEFT(OBJ$,INSTR(1%,OBJ$,"/")-1%) &
\	OBJ1$=LEFT(OBJ1$,INSTR(1%,OBJ1$,"~")-1%) IF INSTR(1%,OBJ1$,"~") &
\	OBJ1$=LEFT(OBJ1$,INSTR(1%,OBJ1$,"$")-1%) IF INSTR(1%,OBJ1$,"$") &
\	CALL ADVOUT(KB%(USER%),CI$+OBJ1$) &
\	OBJ$=RIGHT(OBJ$,INSTR(1%,OBJ$,"/")+1%) &
\	NEXT &

15020	FOR I%=1% TO 8% &
\	IF ROOM%(I%)=ROOM%(USER%) AND I%<>USER% AND ((FLAG%(I%) AND 2%)=0%) &
	THEN &
	J$=CI$+NAM$(I%) &
\	CALL ADVOUT(KB%(USER%),J$) &

15025	NEXT I% &
\	PEO$=PEOPLE$ &
\	UNTIL 0% &
\	GOTO 15030 UNLESS INSTR(1%,PEO$,"/") &
\	CALL ADVOUT(KB%(USER%),CI$+RIGHT(LEFT(PEO$,INSTR(1%,PEO$,'/')-1%),2%)) &
\	PEO$=RIGHT(PEO$,INSTR(1%,PEO$,"/")+1%) &
\	NEXT &

15030	SUBEXIT &

16000	DATA "It's dark.", &
	"You can't see anything.", &
	"Strange shapes swirl around you.", &
	"The world is black.", &
	"You're eyes seem to be on holiday." &

25000	! ERRORS &
	&
	&

29000	OPEN "MSG:LOG1.95" AS FILE #4%, MODE 2% &
\	PRINT #4%, "ADVDSP : ";RIGHT(SYS(CHR$(6%)+CHR$(9%)+CHR$(ERR)),3%); &
	" at line";ERL;" at ";TIME$(0%) &
\	PRINT #4%, "User was ";NAM$(USER%);" in room";ROOM%(USER%) IF &
	(USER%>0% AND USER%<=8%) &
\	CLOSE #4% &
\	RESUME 32767 &

32767	SUBEND &

